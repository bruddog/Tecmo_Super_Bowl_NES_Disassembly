 ; Left endzone Front:  high =0x06, low = 0x70
; Left endzone Back:   high =0x06, low = 0x20
; Right endzone Front: high =0x09, low = 0x90
; Right endzone Back:  high =0x09, low = 0xE0

;OUT OF BOUNDS TOP =0x28
;OUT OF BOUNDS BOTTOM = 0xC8 


.BASE $8000

OFF_PLAY_PTR_START_ADDR					= $A400
PLAY_LIST_START_ADDR					= $B400
DEF_REACTION_START_ADDR					= $BC00

										; *** COULD BE RAPLACED WITH ACTUAL ADDRESS *****
										; Main game loop bank   BANK 17 
;JUMP_PLAYER_INJURED						= $8003
;JUMP_CHANGE_PLAYERS						= $8009
;JUMP_UPDATE_LARGE_SCOREBOARD			= $800F
										; player commands bank  BANK 21
JUMP_DO_NEXT_PLAYER_COMMAND				= $8000
JUMP_WR_JUMP_DIVE_CHECK_PASS 			= $8006
JUMP_DEF_JUMP_DIVE_CHECK_PASS 			= $8009
PLAY_SELECT_START					 	= $8800


										; player commands bank  BANK 23
JUMP_DRAW_GAMEFIELD						= $8003
JUMP_START_BANNER_TASK					= $9003
JUMP_START_COLL_CHECK_TASK				= $9006 
	
										; player commands bank  BANK 27
LOAD_ALL_SKILLS_ROUTINE	 				= $8018
LOAD_SINGLE_PLAYER_SKILLS_ROUTINE		= $801B

OFF_PLAYERS_CHEER_PLAY_PTRS[]			= $AE00
INT_RETURN_DEFENSE_PLAY_PTRS[]			= $AE18 
OFF_RECOVER_ONSIDE_PLAY_PTRS[]			= $AE30
OFF_RECOVER_BALL_PLAY_PTRS[]			= $AE48
PUNT_COVERAGE_PLAY_PTRS[]				= $AE60 
OFF_ONSIDE_KICK_RET_PLAY_PTRS[]			= $AE78
OFF_RECOVERS_OWN_FUM_PLAY_PTRS[]		= $AEA8
OFF_DEFENDS_LOST_FUM_PLAY_PTRS[]		= $AEC0

OFF_PLAYERS_CRY_PLAY_PTRS[]		= $AF08



DEF_PLAYERS_CRY_PLAY_PTRS[]		= $B600
INT_RETURN_PLAY_PTRS[]			= $B618 
DEF_RECOVER_ONSIDE_PLAY_PTRS[]	= $B630
DEF_RECOVER_BALL_PLAY_PTRS[]	= $B648
PUNT_RETURN_PLAY_PTRS[]			= $B660
DEF_ONSIDE_KICK_RET_PLAY_PTRS[]	= $B678
FUM_RET_DEF_PLAY_PTRS[]			= $B6A8
DEF_RET_LOST_FUM_PLAY_PTRS[]	= $B6C0
DEF_PLAYERS_CHEER_PLAY_PTRS[]	= $B708
CHASE_BALL_CARRIER_PLAY_PTRS[]	= $B750

													; ****CAN GET RID OF JUMP TABLES******
BANK_JUMP_ON_FIELD_GAMEPLAY_START:
	JMP ON_FIELD_GAMEPLAY_START                    ; JUMP ->START P1/P2 GAMEPLAY LOOP 

BANK_JUMP_SKP_VS_SKP_INJURY_START:
	JMP SKP_VS_SKP_INJURY_START                    ; JUMP ->SKP MODE INJURY ROUTINE

_F{_GAME_PLAY_START_CHECK_FOR_KICK_TEAM        ; DONE

ON_FIELD_GAMEPLAY_START:                       ; START P1/P2 GAMEPLAY LOOP
   LDX #GAMEPLAY_STACK_INDEX                   ; SET STACK LOCATION = ON FIELD LOOP STACK
   TXS                                         ;  
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2           ; SET BANK = BANK 20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   LDA #$00                                    ;
   STA BALL_STATUS_SPECIAL                     ; CLEAR SPECIAL BALL STATUS
   LDA BALL_STATUS                             ; DOES BALL STATUS = P2 KICKOFF
   BEQ P2_KICKOFF_START                        ; YES-> PLAYER 2 KICKOFF START
   JMP P1_KICKOFF_START                        ; NO-> PLAYER 1 KICKOFF START

_F}_GAME_PLAY_START_CHECK_FOR_KICK_TEAM   
   
_F{_P2_KICKOFF                                 ; DONE

P2_KICKOFF_START:                              ; PLAYER 2 KICKOFF START
   INIT_POSSESSION_STATUS_P2_KICKOFF
   BIT BALL_STATUS_SPECIAL                     ; DOES SPECIAL BALL STATUS=  SAFETY?
   BVS @set_p2_kickoff_location_safety         ;

@set_p2_kickoff_location_regular:              ; SET REGULAR KICKOFF LOCATION   
   SET_YARDLINE_X 65                           ; 
   JMP @set_skills_scroll_fade_in_p2_kickoff
@set_p2_kickoff_location_safety:               ; SET SAFETY KICKOFF LOCATION
   SET_YARDLINE_X 80 

@set_skills_scroll_fade_in_p2_kickoff:         ; P2 SET P1/P2 KICKOFF FORMATIONS, PLAYER SKILLS, and SCROLL WINDOW VALUES     
   SAVE_LOS_X 
   JSR CLEAR_ALL_NAMETABLES                    ; CLEAR NAMETABLES()
   JSR CHECK_FOR_QTR_OVER                      ; CHECK FOR QUARTER OVER, RESET BIT & SUSPEND SWITCH TASK IF OVER() 
   JSR SET_TO_1st_DOWN_CLEAR_RAM_VARS_SPRITES  ; SET FIRST DOWN, CLEAR SPRITE DATA AND GRAPHICS, RESET PLAY VARS & ACTIVE PLAYER LOC/ICON()
   LDA #$00                                    ;    
   STA P1_PLAY_CALL                            ; CLEAR PLAY CALL HOME
   STA P2_PLAY_CALL                            ; CLEAR PLAY CALL AWAY  
   JSR GET_P2_CPU_KICKOFF_TYPE                 ; GET CPU KICKOFF PLAY CALL, P2 KICKING()
   LDX #KICKOFF_OFF_FORMATION_ID               ; SET OFF FORMATION = KICKOFF RETURN
   JSR LOAD_P1_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P1 ON OFFENSE() 
   LDX #KICKOFF_DEF_PLAY_ID                    ; SET DEF PLAY = KICKOFF
   JSR LOAD_P2_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSE PLAY CODE PTRS P2 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #KICKER_STARTER_ID + P2_SIDE_BITFLAG    ; 
   LDX #RE_POSITION_ID + P2_SIDE_BITFLAG       ; 
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)  
   LDA #KR_STARTER_ID                          ; 
   LDX #RT_POSITION_ID                         ; 
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   JSR SET_P1_MAN_TO_KR_P2_MAN_KICKER          ; SET P2 ACTIVE PLAYER TO FIRST PLAYER, P1 ACTIVE PLAYER TO LAST PLAYER()
   LDA #MID_HASH_Y                             ; SET Y LOS TO MIDDLE 
   STA LOS_Y                                   ; 
   SET_P2_KICKOFF_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS(X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                               ; SET GAME STATUS BIT TO ON FIELD & START UPDATE PLAYERS TASK() 
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()

CHECK_FOR_P2_KICKOFF_BALL_SNAP:               ; KICKOFF START CHECK                                    
   RETURN_1_FRAME 
   IF_BALL_NOT_SNAPPED[ADDR] CHECK_FOR_P2_KICKOFF_BALL_SNAP
 

 num_tiles 			= LOCAL_1 
 num_rows  			= LOCAL_2
 kick_bar_rows		= $01
 kick_bar_tiles		= $0E
 
@clear_kick_bar_draw_clock_score 
   RETURN_IN_NUM_FRAMES 10
   LDY #<P2_KICK_BAR_PPU_ADDR                  ; SET PPU LOC = 0x2044   
   LDX #>P2_KICK_BAR_PPU_ADDR                  ;
   LDA #kick_bar_rows                          ; SET # ROWS = 1  
   STA num_rows                                ;
   LDA #kick_bar_tiles                         ; SET LENGTH = 15 TILES
   STA num_tiles                               ;
   JSR FILL_SCREEN_BLANK_TILE_AREA             ; FILL PPU LOCATIONS WITH BLANK TILE(X,Y= ADDR, 3F= NUM ROWS, 3E= LENGTH)
   RETURN_IN_NUM_FRAMES 4
   LDA #REDRAW_CLOCK_SCORE_BANNER_ID           ; SET BANNER ID =  REDRAW CLOCK AND SCORE BG AND CLOCK 
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO) 

CHECK_P2_KICKED_BALL:                            ; CHECK FOR BALL KICKED
   RETURN_1_FRAME 
   IF_BALL_NOT_KICKED[ADDR] CHECK_P2_KICKED_BALL
   
@start_clock_set_p1_has_ball: 
   SET_CLOCK_TO_RUNNING
   SET_POSSESSION_STATUS_P1_HAS_BALL
   PLAY_SOUND_ID KICK_SOUND 
   RETURN_IN_NUM_FRAMES 20
   LDA #P1_ON_FIELD_SONG_SIDE                  ; LOAD PLAYER 1 SOUND VALUE
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   IF_KICK_NOT_ONSIDES[ADDR] CHECK_P1_RECEIVED_KICKOFF
   
   JMP P1_ONSIDES_KICK_RETURN                  ; YES-> P1 ONSIDES KICK

CHECK_P1_RECEIVED_KICKOFF:                     ; CHECK FOR KICKOFF RECEIVED
   RETURN_1_FRAME 
   IF_BALL_NOT_RECEIVED[ADDR] CHECK_P1_RECEIVED_KICKOFF

@check_if_P1_out_of_endzone:   
   COMPARE_BALL_X_TO_YARDLINE 0 
   BCS @set_los_equal_ball_loc                 ; YES-> SET STARTING LOCATION TO BALL RECEIVED LOCATION 
   SAVE_YARDLINE_TO_LOS_X 0 
   JMP @set_p1_kick_return_scroll_limits         ; JUMP-> UPDATE SCROLL WINDOW

@set_los_equal_ball_loc :                      ; SET STARTING FIELD POSITION TO BALL RECEIVED LOCATION
   SET_LOS_X_EQUAL_TO_BALL_X
   
@set_p1_kick_return_scroll_limits:                ; UPDATE SCROLL WINDOW
   SET_P1_KR_SCROLL_LIMITS[]

P1_KICKOFF_RETURN_CHECKS:                      ; P1 KICKOFF RETURN CHECKS
   _WHILE CC

   RETURN_1_FRAME 
   JSR CHECK_FOR_TD_P1                         ; CHECK FOR P1 ENDZONE CROSS() 
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE() 
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS()
  
   _END_WHILE                                  ; PLAY OVER OR BALL OOB? NO-> P1 KICKOFF RETURN CHECKS 
@p1_kick_return_over                                                                            
   SET_CLOCK_TO_STOPPED
   JSR CHECK_FOR_P1_SAFETY                     ; CHECK FOR SAFETY P1 ENDZONE()
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR SET_CHAINS_HASH_SET_DOWN_TO_FIRST       ; UPDATE FIELD POSITION, HASHMARK, FD MARKER, AND SET DOWN TO 1() 
   IF_KICK_ONSIDES[ADDR] P1_KICK_RETURN_END
@else: 
   JSR CHECK_INJURY_KR_PR_P1                   ; CHECK FOR INJURY()

P1_KICK_RETURN_END:                                        ; STOP MUSIC, DO P1 PLAY SELECT
   JSR STOP_CURRENTLY_PLAYING_SOUND            ; STOP CURRENTLY PLAYING SOUND LOOP()

_F}_P2_KICKOFF

_F{_P1_PLAY_SELECT_AND_PLAY_LOAD 			   ; DONE

P1_PLAY_SELECT_START:                          ; P1 PLAY SELECT START
   LDA #CLEAR_ONSCR_BANNER_BANNER_ID                                     ; LOAD CLEAR BANNER
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA #REDRAW_CLOCK_SCORE_BANNER_ID                                     ; SET BANNER ID =  REDRAW CLOCK AND SCORE BG AND CLOCK VALUE
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR CHECK_FOR_QTR_OVER                      ; CHECK FOR QUARTER OVER RESET BIT & SUSPEND SWITCH TASK IF OVER() 
   JSR CLEAR_RAM_VARS_SPRITES                  ; CLEAR PLAY STATUS, BALL INFO BYTES,PASS TIMER, PASS TARGETS, AND SPRITES()
   LDA #PLAY_CALL_SCREEN_BANK                  ; SET BANK = 20
   SET_ADDR_XY[addr] PLAY_SELECT_START
   JSR SWAP_8000_BANK_AND_JUMP_RETURN          ; BANK 8000 SWAP AND JUMP (X,Y= ADDR, A= BANK) 
   IF_P1_PLAY_NOT_SPECIAL_TEAMS[ADDR] P1_REGULAR_PLAY_LOAD

@else_check_fg_punt:   
   JMP P1_FG_PUNT_CHECK                         ; YES->P1 FG or PUNT SELECTED

P1_REGULAR_PLAY_LOAD:                          ; P1 REGULAR PLAY START
   JSR P1_ON_OFF_LOAD_PLAY_INFO_PLAYER_RAM     ; P1 OFFENSE LOAD PLAY INFORMATION INTO PLAYER RAM(A=PLAY SELECTED) (cut in COM LOGIC HACK)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   SET_P1_PLAY_START_SCROLL_LIMITS[]		   ; 	
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= SCROLL LOC)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                               ; SET GAME STATUS BIT TO ON FIELD & START UPDATE PLAYERS TASK()
   JSR SET_SIDELINE_SCROLL_LOC_UPDATE_DOWN_MARKERS                     ; UPDATE LOS AND FIRST DOWN MARKERS VALUES AND GRAPHICS()
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   LDA #SCROLL_DOWN_DIST_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                     ; SET BANNER ID =  DOWN AND DISTANCE BANNER  AND YARD MARKERS, SCROLL FROM FROM RIGHT SIDE
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER()
   RETURN_IN_NUM_FRAMES 60
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                  ; SET BANNER ID =   NAMES CLOCK AND SCORE  + SCROLL FROM FROM RIGHT SIDE
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER()
   RETURN_IN_NUM_FRAMES 8
   JSR CHECK_P1_HIKE_P2_PLAYER_CHANGE          ; CHECK FOR HIKE AND P2 DEFENDER PLAYER CHANGE()
   LDY OFF_PLAY_ADDR                           ; LOAD OFFENSE PLAY POINTER 
   LDX OFF_PLAY_ADDR +1                        ; 
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN                               ; P1 ON OFFENSE UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS)
   LDA #P1_ON_FIELD_SONG_SIDE                  ; SET PLAYER SIDE FOR SONG =  PLAYER 1 
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   IF_P1_PLAY_RUN[ADDR] P1_RUN_PLAY_START
   
@else_do_p1_pass:   
   JMP P1_PASS_PLAY_START                     ; NO-> P1 PASS PLAY START

_F}_P1_PLAY_SELECT_AND_PLAY_LOAD 

_F{_P1_RUN_PLAY								   ; DONE	

P1_RUN_PLAY_START:                             ; P1 RUN PLAY START
   INIT_POSSESSION_STATUS_P1_RUN
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y  

@p1_run_ball_behind_los:                       ; BALL BEHIND LOS RUN PLAY LOOP
   P1_RECEPTION: 
   _WHILE CC

   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN()
   JSR CHECK_FOR_TD_P1                         ; CHECK FOR P1 ENDZONE CROSS() 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE() 
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS()
   BCS P1_PLAY_OVER_NORMAL                     ; BALL OOB OR PLAY OVER? YES-> P1 PLAY OVER 
   LDA BALL_X+1	                             ; IS BALL CARRIER PAST LINE OF SCRIMMAGE?
   CMP LOS_X                                   ;
   LDA BALL_X+2	                             ;
   SBC LOS_X +1                                ;

   _END_WHILE

P1_OFFENSE_PAST_LOS_SET_D_CHASE:               ; CLEAR TARGET INDICATOR, UPDATE PLAY STATUS, SET DEFENDERS TO CHASE
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y   ;
   SET_PLAY_STATUS_MAN_CANT_PASS[]
   JSR ADD_31MS_TO_P2_MAN_DEFENDER             ; ADD 31 MS TO TOP SPEED FOR P2 ACTIVE PLAYER()
   LDY #<CHASE_BALL_CARRIER_PLAY_PTRS[]    ; SET ADDR = DEFENSE AGRESSIVE CHASE CAN COLLIDE
   LDX #>CHASE_BALL_CARRIER_PLAY_PTRS[]     ; 
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)

  
P1_RUN_PLAY_CHECKS_PAST_LOS:
   _WHILE CC                                   ; RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK 
   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN()
   JSR CHECK_FOR_TD_P1                         ; CHECK FOR P1 ENDZONE CROSS() 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()

P1_RUN_FUMBLE_CHECKS:                          ; P1 RUN PLAY FUMBLE CHECKS
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE()
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS()
  
  _END_WHILE                                  ; NOT FUMBLED OR PLAY NOT OVER? YES-> RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK

_F}_P1_RUN_PLAY
                            
_F{_P1_PLAY_OVER_NORMAL						   ; DONE	

P1_PLAY_OVER_NORMAL:                          ; P1 PLAY OVER
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   JSR CHECK_FOR_P1_SAFETY                     ; CHECK FOR SAFETY P1 ENDZONE()
   JSR P1_CHECK_FOR_FIRST_DOWN                 ; CHECK FOR FIRST DOWN / TURNOVER ON DOWNS()
   BCS @p1_turnover_on_downs                   ; 4TH DOWN STOP> YES->P1 TURNOVER ON DOWNS

@p1_normal_play_end:   
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR UPDATE_HASH_ONLY_IF_NOT_FIRST           ; UPDATE LINE OF SCRIMMAGE AND HASHMARK TO SNAP FROM()
   JSR P1_CHECK_FOR_INJURY_NORM                ; CHECK FOR INJURY()
   JMP P1_PLAY_SELECT_START                    ; JUMP->P1 PLAY CALL START

@p1_turnover_on_downs:                         ; P1 TURNOVER ON DOWNS
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                               ; END ALL TASKS BUT CLOCK AND TASK 4& CLEAR CUTSCENE()
   JSR P1_CHECK_FOR_INJURY_NORM                ; CHECK FOR INJURY()
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                               ; JUMP -> P1 to P2 POSSESION CHANGE TO P2 PLAY SELECT

_F}_P1_PLAY_OVER_NORMAL

_F{_P1_PASS_PLAY                               ; DONE

P1_PASS_PLAY_START:                            ; P1 PASS PLAY START
   INIT_POSSESSION_STATUS_P1_PASS[]              ; 
   SET_PLAY_STATUS_MAN_CAN_PASS[]
   JSR CLEAR_CUTSCENE_TO_PLAY_SET_CUT_RAND     ; CLEAR CUTSCENE TO PLAY, SET CUTSCENE RANDOM B6

P1_QB_HASNT_THROWN_BEHIND_LOS_CHECKS:           ; QB HASN’T PASSED BALL LOOP

   RETURN_1_FRAME 
   JSR UPDATE_CURRENT_TARGET_AND_INDICATOR     ; LOAD TARGETS AND SET CURRENT TARGET ICON ()
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()
   IF_BALL_FUMBLED[ADDR] P1_RUN_FUMBLE_CHECKS 
   
@else_other_p1_run_checks:
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS()
   BCC @qb_past_los_check                      ; PLAY NOT OVER OR BALL NOT oob? YES-> QB PAST LOS CHECK   
   
@pass_over_no_throw:   
   JMP P1_PASS_PLAY_OVER_NO_THROW               ; NO-> PASS PLAY OVER QB DIDN’T THROW BALL

@qb_past_los_check:                            ; QB PAST LOS CHECK
   LDA BALL_X+1	                               ; IS BALL CARRIER BEHIND LOS
   CMP LOS_X                                   ; 
   LDA BALL_X+2	                               ;
   SBC LOS_X +1                                ;
   BCC CHECK_FOR_P1_PASS_ATTEMPT               ; YES-> CHECK FOR PASS ATTEMPT
   JMP P1_OFFENSE_PAST_LOS_SET_D_CHASE         ; NO-> CLEAR TARGET INDICATOR, UPDATE PLAY STATUS, SET DEFENDERS TO CHASE

CHECK_FOR_P1_PASS_ATTEMPT:                     ; CHECK FOR PASS ATTEMPT
   IF_PASS_NOT_ATTEMPTED[ADDR] P1_QB_HASNT_THROWN_BEHIND_LOS_CHECKS

@wait_for_pass_qtr_of_the_way:                 ; 
   _WHILE NOT_EQUAL							   ; loop until ball is 1/4 of the way
   
   RETURN_1_FRAME 
   LDA PLAY_STATUS                             ; DOES PLAY STATUS = PLAY OVER?
   BPL @pass_one_qtr_way_there_check           ; NO->P1 WAIT FOR PASS 1/4 OF the WAY LOOP
   JMP P1_PLAY_OVER_NORMAL                     ; YES->RUN PLAY OVER

@pass_one_qtr_way_there_check:                 ; P1 WAIT FOR PASS 1/4 OF the WAY LOOP
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN()
   JSR UPDATE_TARGET_INDICATOR                 ; UPDATE TARGET INDICATOR TO ONSCREEN OR OFFSCREEN()
   JSR DO_CUTSCENE_IF_NEEDED                   ; CHECK FOR CUTSCENE. RETURN WHEN DONE OR IF NO CUTSCENE.
   DEC PASS_TIMER_DIV_BY_4	                   ; PASS TIMER DIVIDED BY 4-- 
   
   _END_WHILE
   
   
   LDA #EXCITED_CROWD_SIDELINE_ID                                    ; SET SIDELINE ANIMATION  = CROWD MOVING
   STA SIDELINE_ANI_TIMER                      ; 
   JSR SET_AND_ORDER_BALL_COLLISION_PLAYERS    ; CHECK FOR DEFENDER AND WR DISTANCE TO BALL AND ORDER()

P1_CHECK_FOR_PASS_OUTCOME_BALL_IN_AIR:         ; P1 PASS ATTEMPT OUTCOME LOOP
   _WHILE VALUE_CLEAR
   
   RETURN_1_FRAME 
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN()
   JSR UPDATE_TARGET_INDICATOR                 ; UPDATE TARGET INDICATOR TO ONSCREEN OR OFFSCREEN()
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()

   
P1_PASS_OUTCOME_CHECK:
  
	SWITCH_A_BIT[] BALL_STATUS  
	CASE_BIT[bit_addr] 7, P2_INTERCEPTS_PASS 	
	CASE_BIT[bit_addr] 6, P2_TIPS_PASS 
	CASE_BIT[bit_addr] 5, P1_RECEPTION
	
@p1_pass_ball_collided_check:                 
	BIT BALL_COLLISION
	
	 _END_WHILE                   
  
    
   RETURN_IN_NUM_FRAMES 6

@check_for_outcome_after_tip:				  ; not implemented    
   IF_PASS_HAS_OUTCOME[ADDR] P1_PASS_OUTCOME_CHECK

P1_PASS_BALL_OOB_OR_HIT_GROUND:                ; P1 PASS PLAY OVER
   SET_PLAY_STATUS_PLAY_OVER[]
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   SET_CLOCK_TO_STOPPED
   LDA #INCOMPLETE_PASS_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   JSR END_GAME_FIELD_BALL_MOVE_TASKS          ; END GAME FIELD & BALL MOVEMENT TASKS & CLEAR CUTSCENE()
   SET_BALL_X_EQUAL_TO_LOS_X
   JSR STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ; STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()

@do_p1_play_select:   
   BCC @p1_update_stats_hashmark_go_to_play_select ; 4TH DOWN STOP? NO->UPDATE STATS, HASHMARK, DO P1 PLAY SELECT()

@p1_pass_turnover_on_downs:   
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                               

@p1_update_stats_hashmark_go_to_play_select:   ; UPDATE STATS, HASHMARK, DO P1 PLAY SELECT()
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()   
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR UPDATE_HASH_ONLY_IF_NOT_FIRST           ; UPDATE LINE OF SCRIMMAGE AND HASHMARK TO SNAP FROM
   JMP P1_PLAY_SELECT_START                    ; JUMP ->PLAYER 1 PLAY SELECT START

_F}_P1_PASS_PLAY   

_F{_P1_SACK_OR_SCRAMBLE                        ; DONE 
 
 
P1_PASS_PLAY_OVER_NO_THROW:                    ; PASS PLAY OVER QB DIDN’T THROW BALL
   LOAD_P1_MAN_PLAYER_POSITION_ONLY			   ; 	
   BNE P1_PASS_PLAY_OVER_NO_THROW_NO_SACK      ; IS QB? NO->  RUN PLAY OVER
   
   IF_BALL_OOB_OR_RECOVERED[ADDR] P1_PASS_PLAY_OVER_NO_THROW_NO_SACK 

@check_p1_qb_sacked:   
   BIT TACKLER_ID                              ; WAS QB SACKED?
   BVC P1_PASS_PLAY_OVER_NO_THROW_NO_SACK      ; NO->  RUN PLAY OVER

@set_starter_id_for_cutscene:    
   LDA TACKLER_ID                              ; SET P2 ICON AND CUTSCENE PLAYER = DEFENDER ID
   CLC                                         ;
   ADC #RE_STARTER_ID                          ; 
   STA P2_MAN_POS_ID_ICON_STATUS               ; SAVE IN P2 ICON
   STA DEF_SACK_BALL_BLOCK_STARTER_ID          ; SAVE IN CUTSCENE PLAYER
   SET_PLAY_STATUS_SACK[]                      ; 
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y   
   COMPARE_BALL_X_TO_LOCATION[name] FRONT_LEFT_ENDZONE + 1
   
@if_less_than:   
   BCC P1_QB_SAFETIED                          ; YES->P1 QB SAFETIED

@else_check_p1_tod:   
   JSR STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ; STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()
   
@if_p1_sacked_on_4th_down:   
   BCS P1_QB_SACKED_ON_4TH_DOWN                ; 4TH DOWN STOP? YES->P1 QB SACKED AND TURNOVER ON DOWNS
  
P1_QB_SACKED:  
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                               
   LDA #QB_SACK_SCENE_ID                       ; SET CUTSCENE ID = QB SACK 
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR UPDATE_HASH_ONLY_IF_NOT_FIRST           ; UPDATE LINE OF SCRIMMAGE AND HASHMARK TO SNAP FROM
   JSR P1_CHECK_FOR_INJURY_NORM                ; CHECK FOR INJURY()
   JMP P1_PLAY_SELECT_START                    ; JUMP ->PLAYER 1 PLAY SELECT START

P1_QB_SACKED_ON_4TH_DOWN:                    ; P1 QB SACKED AND TURNOVER ON DOWNS
   SET_CLOCK_TO_STOPPED
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                               
   LDA #QB_SACK_SIDE_CHANGE_SCENE_ID           ; SET CUTSCENE ID= QB SACK W SIDE CHANGE
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JSR P1_CHECK_FOR_INJURY_NORM                ; CHECK FOR INJURY()
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                              

P1_QB_SAFETIED:                                ; P1 QB SAFETIED
   SET_CLOCK_TO_STOPPED
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                               ; END ALL TASKS BUT CLOCK AND TASK 4 & CLEAR CUTSCENE()
   SET_BALL_STATUS_SPECIAL_SAFETY[]
   LDA #SAFETY_POINTS                          ; SET POINT VALUE= SAFETY POINTS = 2PTS
   JSR ADD_PTS_TO_P2_QTR_TOT                   ; ADD POINTS TO P2 SCORE(A= PTS) 
   LDA #QB_SACK_SAFETY_SCENE_ID                ; SET CUTSCENE ID= QB SAFETIED 
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JSR P1_CHECK_FOR_INJURY_NORM                ; CHECK FOR INJURY()
   RETURN_IN_NUM_FRAMES 8                      ; 
   JMP P1_TO_P2_POSSESSION_CHANGE_P1_KICKOFF                               ; JUMP-> P1 to P2 POSSESION CHANGE KICKOFF

P1_PASS_PLAY_OVER_NO_THROW_NO_SACK:
   JMP P1_PLAY_OVER_NORMAL                     ; JUMP-> P1 PLAY OVER

_F}_P1_SACK_OR_SCRAMBLE  

_F{_P1_SPECIAL_TEAMS_PLAY_TYPE_CHECK           ; DONE
  
P1_FG_PUNT_CHECK:                              ; FG/PUNT CHECK
   LDA P1_PLAY_CALL                            ; IS P1 PLAY CALL = FG =0X09
   CMP #FG_PLAY_CALL_ID                        ; 
   BCC P1_PUNT_START                           ; NO-> P1 PUNT START
   JMP P1_FG_START                             ; YES-> P1 FG START

 _F}_P1_SPECIAL_TEAMS_PLAY_TYPE_CHECK
 
_F{_P1_PUNT_PLAY                               ; DONE

P1_PUNT_START:                                 ; P1 PUNT START =0X08
   INIT_POSSESSION_STATUS_P1_PUNT

@set_off_formation:   
   LDX #PUNT_OFF_FORMATION_ID                  ; SET OFFENSIVE FORMATION =PUNT
   JSR LOAD_P1_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P1 ON OFFENSE()

@set_def_play:  
   LDX #PUNT_DEF_PLAY_ID                       ; SET DEFENSIVE PLAY = PUNT 
   JSR LOAD_P2_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSE PLAY CODE PTRS P2 ON DEFENSE(X=DEFENSIVE PLAY INDEX)

@load_player_skills:  
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9

@swap_in_special_teams_players:
   LDA #PUNTER_STARTER_ID                         ; LOAD P1 PUNTER INDEX (00= P1, 17=PUNTER)
   LDX #QB_POSITION_ID                         ; LOAD PLAYER POINTER TO LOAD INTO(00= player 1 side player id=00= first player)
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   LDA #PR_STARTER_ID + P2_SIDE_BITFLAG           ; LOAD P2 PR INDEX (80= P2, 19 = PR)
   LDX #SS_POSITION_ID + P2_SIDE_BITFLAG     ; LOAD PLAYER POINTER TO LOAD SKILLS INTO (80= player 2 side player id=0A= last player)
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   JSR SET_P2_MAN_TO_KR_P1_MAN_KICKER          ; SET P1 ACTIVE PLAYER TO FIRST PLAYER, P2 ACTIVE PLAYER TO LAST PLAYER()

@set_scroll_limits:   
   SET_P1_PUNT_START_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                              
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   RETURN_IN_NUM_FRAMES 2
   JSR CHECK_FOR_SNAP_P2DEF_CHANGE_P1PUNT        ; WAIT FOR SNAP()
   LDA #P2_ON_FIELD_SONG_SIDE                  ; LOAD PLAYER 2 
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)

P1_CHECK_FOR_BALL_PUNTED: 					   ; P1 PUNT HAS BEEN SNAPPED LOOP
   
   RETURN_1_FRAME 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE()
   BCC @check_for_punter_tackled               ; FUMBLE? NO-> PUNT RETURN CHECKS
   
@p1_snap_fumbled:   
   JMP P1_RUN_PLAY_CHECKS_PAST_LOS             ; YES->RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK

@check_for_punter_tackled:                     ; 
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR P1 ACTIVE PLAYER OUT OF BOUNDS
   BCC @check_ball_punted                      ; PLAY OVER OR BALL OOB?

@p1_punter_tackled   
   JMP P1_SPECIAL_TEAMS_PLAY_OVER               ; YES->P1 SPECIAL TEAMS PLAY OVER

@check_ball_punted:                            ; P1 CHECK FOR BALL PUNTED
   IF_BALL_NOT_KICKED[ADDR] P1_CHECK_FOR_BALL_PUNTED
   
  
   
P1_CHECK_FOR_PUNT_BLOCKED:                      ; P1 BALL HAS BEEN PUNTED 

_WHILE EQUAL  
	
   RETURN_1_FRAME 
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCC @check_for_punt_block                   ; PLAY OVER OR BALL OOB? NO->P1 PUNT BLOCK CHECK
   JMP P1_SPECIAL_TEAMS_PLAY_OVER              ; JUMP-> P1 SPECIAL TEAMS PLAY OVER 

@check_for_punt_block:                         ; P1 PUNT BLOCK CHECK
   BIT BALL_STATUS                             ; DOES BALL STATUS = PUNT BLOCKED? (OLD HOOK NOT POSSIBLE WITHOUT HACK)
   BVC @check_for_punt_cutscene                ; NO->P2 RETURN P1 PUNT START
   JMP P1_PUNT_BLOCKED_POSS_CHANGE_UNUSED                               ; YES->P1 to P2 POSSESION CHANGE TO P2 PLAY SELECT

@check_for_punt_cutscene:                      ; P2 RETURN P1 PUNT START
   LDA CUTSCENE_TO_DRAW                        ; IS THERE A CUTSCENE TO PLAY 
   
_END_WHILE	
   
@do_punt_cutscene: 
   JSR DO_CUTSCENE_SAVE_INFO_TO_SRAM           ; DO CUTSCENE()
   CLEAR_CLOCK_SPEED_PUNT
   SET_POSSESSION_STATUS_P2_HAS_BALL
   RETURN_IN_NUM_FRAMES 8
   SET_ADDR_XY[addr] PUNT_COVERAGE_PLAY_PTRS[] 
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; P1 ON OFFENSE UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS)
   SET_ADDR_XY[addr] PUNT_RETURN_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   
P2_PUNT_RETURN_WAIT_FOR_BALL:                 ; P2 PUNT RETURN WAIT FOR BALL LOOP
   RETURN_1_FRAME
   BIT BALL_STATUS                             ; IS PUNT BLOCKED?
   BVS P1_PUNT_BLOCKED_POSS_CHANGE_UNUSED      ; YES->P1 to P2 POSSESION CHANGE TO P2 PLAY SELECT
   JSR P2_CHECK_FOR_TOUCHBACK                  ; CHECK FOR P2 TOUCHBACK()
   BCS P2_TOUCHBACK_POSS_CHANGE                ; TOUCHBACK? YES-> P1 to P2 POSSESION CHANGE TO P2 PLAY SELECT
   IF_BALL_NOT_RECEIVED[ADDR] P2_PUNT_RETURN_WAIT_FOR_BALL 
 
 P2_PUNT_RETURN_CAUGHT_BALL: 

@set_p1_punting_stats: 
   LDX #UPDATE_PUNTS_IN_GAME_INDEX	           ; SET STAT TO UPDATE = P1 PUNT ATT 
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   JSR CALCULATE_PLAY_DISTANCE_P1              ; CALCULATE DISTANCE FROM INIT LOC(LEFT)  – CUR LOCATION (RIGHT)()
   LDX #UPDATE_PUNT_YDS_IN_GAME_INDEX          ; SET STAT TO UPDATE = P1 PUNT YARD 
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
@update_los_and_scroll:   
   SET_LOS_X_EQUAL_TO_BALL_X
   SET_P2_PR_SCROLL_LIMITS[]

@p2_punt_return:                                ; P2 PUNT RETURN CHECKS
   RETURN_1_FRAME 
   JSR CHECK_FOR_TD_P2                         ; CHECK FOR P2 ENDZONE CROSS()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE()
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCC @p2_punt_return                         ; PLAY OVER OR OOB? NO-> P2 PUNT RETURN CHECKS
 
@p2_punt_return_tackled:
   JSR CHECK_FOR_P2_SAFETY                     ; CHECK FOR P2 SAFETY()
   JSR P1_to_P2_SIDE_CHANGE_BANNER_AND_MUSIC   ; PLAY SIDE CHANGE MUSIC and BANNER P1 to P2()
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   SET_POSSESSION_STATUS_P2_HAS_BALL_HAD_BALL  ; SET POSSESSION STATUS = P2 HAS BALL, P2 LAST HAD BALL, P2 JOYPAD, P2 PALLETE
   JSR CHECK_INJURY_KR_PR_P2                   ; P2 PR/KR CHECK FOR INJURY()
   SET_POSSESSION_STATUS_P1_HAS_BALL_HAD_BALL  ; SET POSSESSION STATUS = P1 HAS BALL, P1 LAST HAD BALL FOR POSSESION CHANGE BANNER
  
P2_TOUCHBACK_POSS_CHANGE: 
	JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                             ; JUMP -> P1 to P2 POSSESION CHANGE TO P2 PLAY SELECT
																			   
P1_PUNT_BLOCKED_POSS_CHANGE_UNUSED:											  ; not implemeneted	
	JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                       


_F}_P1_PUNT_PLAY

																			  
_F{_P1_FG_PLAY                                 ; DONE

P1_FG_START:                                   ; P1 FG START
   JSR CLEAR_RAM_VARS_SPRITES                  ; CLEAR PLAY STATUS, BALL INFO BYTES,PASS TIMER, PASS TARGETS, AND SPRITES()
   INIT_POSSESSION_STATUS_P1_FG_ATT
   LDX #FG_OFF_FORMATION_ID                    ; SET OFFENSIVE FORMATION = FG 
   JSR LOAD_P1_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P1 ON OFFENSE(X=FORMATION INDEX) 
   LDX #FG_DEF_PLAY_ID                         ; SET DEFENSIVE PLAY =  FG
   JSR LOAD_P2_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSE PLAY CODE PTRS P2 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #KICKER_STARTER_ID                      ; SET PLAYER TEAM ID = FG KICKER
   LDX #RB1_POSITION_ID	                       ; SET POSITION TO LOAD INTO = RB 1
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   SET_P1_FG_XP_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                               
   JSR SET_SIDELINE_SCROLL_LOC_UPDATE_DOWN_MARKERS                        ; UPDATE LOS AND FIRST DOWN MARKERS VALUES AND GRAPHICS()
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   LDA #FG_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                 
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR CHECK_P1_HIKE_P2_PLAYER_CHANGE                               ; CHECK FOR HIKE AND P2 DEFENDER PLAYER CHANGE()
   LDA #P1_ON_FIELD_SONG_SIDE                                    ; SET SIDE = P1 SIDE
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A= SIDE (P1/P2))


   
P1_FG_NOT_KICKED_LOOP:   
   RETURN_1_FRAME 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE
   BCC @check_for_tackle_kick                  ; 
   JMP P1_RUN_PLAY_CHECKS_PAST_LOS             ; JUMP -> P1 RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK
   
@check_for_tackle_kick:                        ; 
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS()
   BCS P1_SPECIAL_TEAMS_PLAY_OVER              ; P1 SPECIAL TEAMS PLAY OVER
   IF_BALL_NOT_KICKED[ADDR] P1_FG_NOT_KICKED_LOOP
   
@fg_kicked:                                    ; FG KICKED LOOP
	_WHILE EQUAL
	
   RETURN_1_FRAME 
   LDA CUTSCENE_TO_DRAW                        ; IS THERE A CUTSCENE TO PLAY
   
    _END_WHILE
   
   IF_KICK_NOT_MADE[ADDR] DO_P1_FG_CUTSCENE
   
@stop_clock_and_add_pts:   
   SET_CLOCK_TO_STOPPED
   LDA #FG_POINTS                              ; LOAD FG POINTS                     
   JSR ADD_PTS_TO_P1_QTR_TOT                   ; ADD POINTS TO P1 SCORE (A=PTS)  

DO_P1_FG_CUTSCENE:                                ; CHECK FOR P1 FG BLOCK OR MISS
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   IF_KICK_BLOCKED[ADDR] P1_FG_BLOCKED
   
   IF_KICK_NOT_MADE[ADDR] P1_FG_MISSED
   
@p1_fg_made_end:   
   RETURN_IN_NUM_FRAMES 8
   JMP P1_TO_P2_POSSESSION_CHANGE_P1_KICKOFF   ; JUMP -> P1 to P2 POSSESION CHANGE KICKOFF

P1_SPECIAL_TEAMS_PLAY_OVER:                    ; P1 SPECIAL TEAMS PLAY OVER
   SET_CLOCK_TO_STOPPED
   JSR CHECK_FOR_P1_SAFETY                     ; CHECK FOR SAFETY P1 ENDZONE
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                               

P1_FG_BLOCKED:                                 ; P1 FG BLOCKED
   LDA #KICK_BLOCK_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                            
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR KICK_BLOCK_RECOVERY                     ; DO KICK BLOCK RECOVERY ROUTINE
   JMP P1_RUN_PLAY_CHECKS_PAST_LOS             ; JUMP TO P1 RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK

P1_FG_MISSED:                                  ; P1 FG MISSED 
   COMPARE_LOS_X_TO_LOCATION[name] P2_TWENTY_YARDLINE
   BCC @fg_not_inside_twenty                   ; NO->END PLAY AND LOAD LOS LOCATION (X) into BALL LOCATION (X)

@set_yardline_to_twenty:   
   SAVE_YARDLINE_TO_LOS_X 80 

@fg_not_inside_twenty:                         ; END PLAY AND LOAD LOS LOCATION (X) into BALL LOCATION (X)
   JSR END_GAME_FIELD_BALL_MOVE_TASKS          ; END GAME FIELD & BALL MOVEMENT TASKS & CLEAR CUTSCENE()
   SET_PLAY_STATUS_PLAY_OVER[]
   SET_BALL_X_EQUAL_TO_LOS_X
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT ; JUMP -> P1 to P2 POSSESION CHANGE TO P2 PLAY SELECT

_F}_P1_FG_PLAY

_F{_P1_ONSIDES_RETURN						   ; DONE	

P1_ONSIDES_KICK_RETURN:                        ; P1 ONSIDES KICK RETURN
   LDA #ONSIDES_KICK_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   SET_ADDR_XY[addr] OFF_RECOVER_ONSIDE_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; P1 ON OFFENSE UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS)
   SET_ADDR_XY[addr] DEF_RECOVER_ONSIDE_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)

@recovery_loop:
	_WHILE EQUAL							; P1 ONSIDES KICK WAIT FOR BALL TO BE RECOVERED
   RETURN_1_FRAME 
   LDA BALL_STATUS                             ; DOES BALL STATUS =  BALL RECOVERED?
   AND #$0C                                    ; 
   _END_WHILE
   
   AND #$04                                    ; DID P2 RECOVER KICK? 
   BNE @p2_def_recovers_onsides                ; YES-> P2 RECOVERS ONSIDE INTENDED FOR P1  
  
@p1_off_recovers_onside  
   SET_LOS_X_EQUAL_TO_BALL_X
   SET_P1_KR_SCROLL_LIMITS[]
   SET_ADDR_XY[addr] OFF_ONSIDE_KICK_RET_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR            ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON DEFENSE(X,Y= PLAY CODE ADDRESS)
   SET_ADDR_XY[addr] DEF_ONSIDE_KICK_RET_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   JSR ADD_31MS_TO_P1_MAN_DEFENDER             ; ADD 31 MS TO TOP SPEED FOR P1 MAN DEFENDER()(typically not under man control yet)
   LDA #SCROLL_IN_ONSCR_BANNER_ID +  SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 8
   JMP P1_KICKOFF_RETURN_CHECKS                ; PLAY OVER OR BALL OOB? NO->P1 KICKOFF RETURN CHECKS

@p2_def_recovers_onsides:                      ; P2 RECOVERS ONSIDE INTENDED FOR P1  
   JSR DRAW_RECOVER_TEXT                       ; DRAW RECOVER TEXT()
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                              

_F}_P1_ONSIDES_RETURN

_F{_P1_PASS_TIPPED_RESULT                      ; DONE

P2_TIPS_PASS:                        		   ; P1 PASS TIPPED
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   LDA #PASS_BLOCK_BANNER_ID  +  SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   RETURN_IN_NUM_FRAMES 40
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID  +  SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                  
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)



P2_TIPS_PASS_OUTCOME_LOOP:	
   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS
   BCC CHECK_P2_TIP_PASS_RESULT                ; PLAY OVER? 
   JMP P1_PASS_BALL_OOB_OR_HIT_GROUND          ; YES->P1 PASS PLAY OVER

   
CHECK_P2_TIP_PASS_RESULT:                      ; CHECK FOR INT
   SWITCH_A_BIT[] BALL_STATUS
   CASE_BIT[bit_addr] 7, P2_INTERCEPTS_PASS    ; checks for jumping int
   CASE_BIT[bit_addr] 5, P1_RECEPTION	       ; checks for jumping rec

@check_ball_after_block:                       ; CHECK FOR BALL RECOVERED AFTER BLOCK 
   ASL                                         ; DOES BALL STATUS = BALL RECOVERED
   ASL                                         ;
   BCS CHECK_WHO_CAUGHT_P2_TIP                 ; YES->CHECK FOR CATCH/INTERCEPTION AFTER TIP
   BIT BALL_COLLISION                          ; DOES BALL INFO = BALL COLLIDED
   BVC P2_TIPS_PASS_OUTCOME_LOOP               ; NO-> P1 PASS TIPPED OUTCOME

@ball_collided:
   RETURN_IN_NUM_FRAMES 6
   LDA BALL_STATUS                             ; DOES BALL STATUS = INT, REC, BALL CAUGHT AFTER TIP
   AND #$A8                                    ;
   BNE P2_TIPS_PASS_OUTCOME_LOOP

@p2_tipped_pass_hit_ground:
   
   JMP P1_PASS_BALL_OOB_OR_HIT_GROUND          ; NO-> P1 PASS PLAY OVER

                                             											   
CHECK_WHO_CAUGHT_P2_TIP:                       ; CHECK FOR CATCH/INTERCEPTION AFTER TIP *** NOT REACHABLE WITHOUT HACK ***
   ASL                                         ; DOES BALL STATUS = P1 CAUGHT BALL?
   BCC @p1_catches_p1_tip                      ; YES-> 
   JMP P2_INTERCEPTS_PASS                      ; YES->PLAYER 2 DEFENDER GETS INTERCEPTION

@p1_catches_p1_tip:                            ; P1 CATCHES TIPPED BALL
   JMP P1_RECEPTION                            ; JUMP->P1 RUN PLAY CHECKS

_F}_P1_PASS_TIPPED_RESULT   

_F{_P1_SAFETIED								    ; DONE
   
P1_SAFTIED:                                     ; P1 SAFETIED
   PLAY_SOUND_ID SAFETY_SONG
   SET_CLOCK_TO_STOPPED
   LDA #SAFETY_POINTS                          ; LOAD P1 SAFETY POINTS
   JSR ADD_PTS_TO_P2_QTR_TOT                   ; ADD POINTS TO P2 SCORE(A= PTS)
   SET_BALL_STATUS_SPECIAL_SAFETY[]
   LDA #SAFETY_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60    
   LDA #P2_TEAM_CITY_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                     
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60                     ;
   LDA #LARGE_SCORE_SCROLL_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60                     ;
   LDA #CLEAR_ONSCR_BANNER_BANNER_ID           ; SET BANNER SCENE ID =  CLEAR BANNER NAMETABLE 0
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA #$00                                    ; SET IRQ0 SCROLL LOCATION = NAMETABLE 0
   STA IRQ_0_NAMETABLE	                        ;
   JMP P1_TO_P2_POSSESSION_CHANGE_P1_KICKOFF   ; JUMP-> P1 to P2 POSSESION CHANGE KICKOFF

 _F}_P1_SAFETIED 

_F{_P1_TD									   ; DONE
 
DO_PLAYER_1_TOUCHDOWN:                         ; PLAYER 1 CROSSED ENDZONE TD
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   SET_PLAY_STATUS_TOUCHDOWN[]
   LDA #JUMPING_CHEERLEADER_SIDELINE_ID        ; SET SIDELINE ANIMATION  = JUMPING CHEERLEADERS, EXCITED CROWD
   STA SIDELINE_ANI_TIMER                      ; 
   SET_CLOCK_TO_STOPPED
   LDA #TD_POINTS                              ; LOAD POINTS FOR TOUCHDOWN 
   JSR ADD_PTS_TO_P1_QTR_TOT                   ; ADD POINTS TO P1 SCORE (A=PTS)  
   LDA #TOUCHDOWN_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   IF_P2_LAST_HAD_BALL[ADDR] DO_P1_DEF_TD
   
P1_OFF_TD:    
   SET_ADDR_XY[addr] OFF_PLAYERS_CHEER_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR            ; UPDATE ALL P1 PLAYERS PLAY CODE(X,Y= PLAY CODE POINTERS START ADDRESS)
   SET_ADDR_XY[addr] DEF_PLAYERS_CRY_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   JSR P1_PLAY_TD_MUSIC_UPDATE_BANNER_UPDATE_STATS                               
   LDX #RUSHING_TOUCHDOWN_SCENE_ID             ; SET  CUTSCENE ID = P1 RUSHING TD CUTSCENE
   IF_PASS_NOT_ATTEMPTED[ADDR] DO_P1_TD_CUTSCENE
   
@else_set_cutscene_id_to_pass   
   INX                                         ; CUTSCENE ID++ =  P1 PASSING TD CUTSCENE

DO_P1_TD_CUTSCENE:                             ; DO P1 TD CUTSCENE 
   TXA                                         ; LOAD CUTSCENE VALUE (PASS OR RUN TD)
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JMP CHECK_IF_XP_KICK_NEEDED_P1              ; JUMP TO P1 XP CHECK

DO_P1_DEF_TD:                                  ; P1 DEFENSIVE TD CELEBRATE START
   SET_ADDR_XY[addr] DEF_PLAYERS_CHEER_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR            ; UPDATE ALL P1 PLAYERS PLAY CODE(X,Y= PLAY CODE POINTERS START ADDRESS)
   SET_ADDR_XY[addr] OFF_PLAYERS_CRY_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   JSR P1_PLAY_TD_MUSIC_UPDATE_BANNER_UPDATE_STATS                            
   INIT_POSSESSION_STATUS_P1_RUN
   LDA #RUSHING_TOUCHDOWN_SCENE_ID            ; SET CUTSCENE ID = P1 RUSHING TD CUTSCENE
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JMP CHECK_IF_XP_KICK_NEEDED_P1              ; JUMP -> P1 XP CHECK

P1_PLAY_TD_MUSIC_UPDATE_BANNER_UPDATE_STATS:   ; PLAY TD SOUND, DRAW TEAM NAME, SET BALL LOCATION, UPDATE STATS()
   RETURN_IN_NUM_FRAMES 2
   PLAY_SOUND_ID TOUCHDOWN_DMC
   RETURN_IN_NUM_FRAMES 90
   LDA #P1_TEAM_CITY_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                  
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 90
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   SET_BALL_X_EQUAL_TO_LOCATION[name] FRONT_RIGHT_ENDZONE 
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   RTS                                         ; RETURN

CHECK_IF_XP_KICK_NEEDED_P1:                     ; P1 XP CHECK
   IF_QTR_NOT_OVERTIME[ADDR] P1_XP_START
   JSR CHECK_FOR_QTR_OVER                      ; CHECK FOR QUARTER/GAME OVER RESET BIT & SUSPEND SWITCH TASK IF OVER()

P1_XP_START:                                   ; P1 XP START
   JSR SET_TO_1st_DOWN_CLEAR_RAM_VARS_SPRITES  ; SET FIRST DOWN, CLEAR SPRITE AND GRAPHICS, RESET PLAY VARS & ACTIVE PLAYER LOC/ICON()
   LDA #$00                                    ; SET P1/P2 PLAY CALL = ZERO
   STA P1_PLAY_CALL                            ;
   STA P2_PLAY_CALL                            ;
   INIT_POSSESSION_STATUS_P1_XP
 
@set_yardline_has_p1_xp: 
   SAVE_YARDLINE_TO_LOS_X 98				   ; Sets yardline 2 yards from endzone
   LDA #MID_HASH_Y                             ; SET LOS Y = MIDDLE OF FIELD 
   STA LOS_Y                                   ;
   LDX #XP_OFF_FORMATION_ID                    ; SET OFFENSIVE FORMATION = XP FORMATION
   JSR LOAD_P1_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P1 ON OFFENSE()
   LDX #XP_DEF_PLAY_ID                         ; SET DEFENSIVE PLAY = FG/XP
   JSR LOAD_P2_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSE PLAY CODE PTRS P2 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #KICKER_STARTER_ID                         ; SET PLAYER ID = KICKER
   LDX #RB1_POSITION_ID                        ; SET POSITION TO LOAD INTO = RB
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   SET_P1_FG_XP_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                              
   LDA #REDRAW_CLOCK_SCORE_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   RETURN_IN_NUM_FRAMES 8
   JSR CHECK_P1_HIKE_P2_DEF_CHANGE_NO_CLOCK    ; CHECK FOR P1 HIKE AND P2 DEFENDER PLAYER CHANGE NO CLOCK CHECK()
   LDA #P1_ON_FIELD_SONG_SIDE                  ;
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)

CHECK_P1_XP_KICKED:                             ; WAIT FOR BALL KICKED OR HOLDER TACKLED LOOP
   RETURN_1_FRAME 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS
   BCS EXIT_P1_XP                             ; YES->EXIT P1 XP
   IF_BALL_NOT_KICKED[ADDR] CHECK_P1_XP_KICKED

@wait_xp_cutscene_done:                        ; WAIT UNTIL P1 XP CUTSCENE DONE LOOP
   RETURN_1_FRAME 
   LDA CUTSCENE_TO_DRAW                        ; CUTSCENE DONE?
   BEQ @wait_xp_cutscene_done                  ; NO->WAIT UNTIL P1 XP CUTSCENE DONE LOOP
   IF_KICK_NOT_MADE[ADDR] P1_XP_CHECK_FOR_ADDITIONAL_CUTSCENE

@add_xp_points_to_P1_score
   LDA #XP_PTS                                 ; SET PTS TO ADD = P1 EXTRA POINT VALUE
   JSR ADD_PTS_TO_P1_QTR_TOT                   ; ADD POINTS TO P1 SCORE (A=PTS)  

P1_XP_CHECK_FOR_ADDITIONAL_CUTSCENE:           ; CHECK IF ADDITIONAL CUTSCENE NEEDED
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()     

EXIT_P1_XP:                                   ; EXIT P1 XP
   JMP P1_TO_P2_POSSESSION_CHANGE_P1_KICKOFF   ; JUMP-> P1 to P2 POSSESION CHANGE KICKOFF

_F}_P1_TD   

_F{_P1_INTERCEPTED							   ; DONE
   
P2_INTERCEPTS_PASS:                            ; PLAYER 2 DEFENDER GETS INTERCEPTION
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   RETURN_IN_NUM_FRAMES 3
   SET_LOS_X_EQUAL_TO_BALL_X
   
@update_play_code_ptrs:   
   SET_ADDR_XY[addr] INT_RETURN_DEFENSE_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR            ; UPDATE ALL P1 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS) 
   
   SET_ADDR_XY[addr] INT_RETURN_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR            ; UPDATE ALL P2 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS)
   
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   LDA #INTERCEPTION_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA #P2_ON_FIELD_SONG_SIDE                  ; SET SONG SIDE = PLAYER 2
   JSR GET_ON_FIELD_SONG_ID                    ; UPDATE CURRENTLY PLAYING SOUND BASED ON GAME TYPE(A=P1 or P2)
   STA ON_FIELD_SONG_TO_PLAY_AFTER_INT         ; SAVE SONG ID TO PLAY AFTER INTERCEPTION MUSIC
   JSR P2_CHECK_FOR_TOUCHBACK                  ; CHECK FOR P2 TOUCHBACK()
   BCS P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                               
   RETURN_IN_NUM_FRAMES 60
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                  
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)

UPDATE_SCROLL_LIMITS_P1_TURNOVER:              ; UPDATE SCROLL THRESHOLDS AFTER P2 GETS TURNOVER
   SET_P1_INT_PR_FUM_SCROLL_LIMITS[]
   
@p2_int_pr_fumble_return_checks:               ; P2 INTERCEPTION RETURN CHECKS 
   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_TD_P2                         ; CHECK FOR P2 ENDZONE CROSS()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCC @p2_int_pr_fumble_return_checks         ; 
   JSR CHECK_FOR_P2_SAFETY                     ; CHECK FOR P2 SAFETY()
   JSR STOP_CURRENTLY_PLAYING_SOUND            ; STOP CURRENTLY PLAYING SOUND LOOP ()
   RETURN_IN_NUM_FRAMES 2
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT                             

 _F}_P1_INTERCEPTED  
 
_F{_P1_TO_P2_POSSESSION_CHANGE					; DONE

P1_TO_P2_POSSESSION_CHANGE_P1_KICKOFF:         ; P1 to P2 POSSESION CHANGE KICKOFF
   JSR CHECK_FADE_OUT_P1_TO_P2                ; STOP CLOCK, UPDATE STATS, UPDATE FIELD LOCA HASH, DOWN, SET POSS P2, CHECK QTR OVER()
   JMP P1_KICKOFF_START                        ; JUMP -> P2 KICK RETURN START

P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT:     ; P1 to P2 POSSESION CHANGE TO PLAY SELECT 
   JSR CHECK_FADE_OUT_P1_TO_P2                ; STOP CLOCK, UPDATE STATS, UPDATE FIELD LOCA HASH, DOWN, SET POSS P2, CHECK QTR OVER()
   JMP P2_PLAY_SELECT_START                    ; JUMP -> P2 PLAY SELECT

CHECK_FADE_OUT_P1_TO_P2:                        ; STOP CLOCK, UPDATE STATS, UPDATE FIELD LOCA HASH, DOWN, SET POSS P2, CHECK QTR OVER()
   LDA BG_FADE                                 ; IS BG PALLETE FADE = FULLY FADED OUT?
   BEQ @stop_clock_kill_tasks_update_stats     ; YES-> STOP CLOCK, KILL TASKS
   JSR P1_to_P2_SIDE_CHANGE_BANNER_AND_MUSIC   ; PLAY SIDE CHANGE MUSIC and BANNER P1 to P2()

@stop_clock_kill_tasks_update_stats:           ; STOP CLOCK, KILL TASKS, 
   SET_CLOCK_TO_STOPPED
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()   
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS
   JSR SET_CHAINS_HASH_SET_DOWN_TO_FIRST       ; UPDATE FIELD POSITION, HASHMARK, FD MARKER, AND SET DOWN TO 1()
   INIT_POSSESSION_STATUS_P2_NEW_PLAY[]   
   RETURN_IN_NUM_FRAMES 2
   JMP CHECK_FOR_QTR_OVER                      ; CHECK FOR QUARTER OVER RESET BIT & SUSPEND SWITCH TASK IF OVER()

 _F}_P1_TO_P2_POSSESSION_CHANGE

_F{_P1_KICKOFF								   ; DONE
 
 P1_KICKOFF_START:                             ; PLAYER 2 KICK RETURN START
   INIT_POSSESSION_STATUS_P1_KICKOFF
   IF_PLAY_SAFETY[ADDR] SET_P1_KICKOFF_LOCATION_SAFETY
 
SET_P1_KICKOFF_LOCATION_NORMAL:  			   ; NORMAL P1 KICKOFF LOCATION
   SET_YARDLINE_X 35
   JMP SET_SKILLS_SCROLL_FADE_IN_P1_KICKOFF    ; JUMP->SET P1 KICKOFF FORMATIONS AND SKILLS

SET_P1_KICKOFF_LOCATION_SAFETY:               ; SET P1 SAFETY KICKOFF LOCATION
   SET_YARDLINE_X 20 

SET_SKILLS_SCROLL_FADE_IN_P1_KICKOFF:         ; SET P1 KICKOFF FORMATIONS AND SKILLS 
   SAVE_LOS_X
   JSR CLEAR_ALL_NAMETABLES                    ; CLEAR NAMETABLES()
   JSR CHECK_FOR_QTR_OVER                      ; CHECK FOR QUARTER OVER RESET BIT & SUSPEND SWITCH TASK IF OVER()
   JSR SET_TO_1st_DOWN_CLEAR_RAM_VARS_SPRITES  ; SET FIRST DOWN, CLEAR SPRITE DATA AND GRAPHICS, RESET PLAY VARS & ACTIVE PLAYER LOC/ICON()
   LDA #$00                                    ; 
   STA P1_PLAY_CALL                            ; CLEAR PLAY CALL HOME
   STA P2_PLAY_CALL                            ; CLEAR PLAY CALL AWAY  
   JSR GET_P1_CPU_KICKOFF_TYPE                 ; DO COM LOGIC CHECK()(P1 team)
   LDX #KICKOFF_OFF_FORMATION_ID               ; SET FORMATION =  KICKOFF RETURN
   JSR LOAD_P2_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P2 ON OFFENSE()
   LDX #KICKOFF_DEF_PLAY_ID	                   ; SET DEFENSIVE PLAY = KICKOFF
   JSR LOAD_P1_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSIVE PLAY CODE PTRS P1 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #KICKER_STARTER_ID                      ; SET SIDE = P1, SET PLAYER ID = KICKER
   LDX #RE_POSITION_ID                         ; SET POSITION ID = QB
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER FOR SKILL LOAD, X=PLAYER POINTER TO LOAD INTO) 
   LDA #KR_STARTER_ID + P2_SIDE_BITFLAG        ; SET SIDE = P2, SET PLAYER ID = KR (0X18)
   LDX #RT_POSITION_ID + P2_SIDE_BITFLAG       ; SET SIDE = P2, SET POSITION ID = RT KR (0X0A)
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER FOR SKILL LOAD, X=PLAYER POINTER TO LOAD INTO)
   JSR SET_P2_MAN_TO_KR_P1_MAN_KICKER          ; SET P1 ACTIVE PLAYER TO FIRST PLAYER, P2 ACTIVE PLAYER TO LAST PLAYER()
   LDA #MID_HASH_Y                             ; SET Y LOS TO MIDDLE 
   STA LOS_Y                                   ; 
   SET_P1_KICKOFF_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                               ; SET GAME STATUS BIT TO ON FIELD & START UPDATE PLAYERS TASK()
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   
CHECK_FOR_P1_KICKOFF_BALL_SNAP:               ; P1 KICKOFF START CHECK
   RETURN_1_FRAME 
   IF_BALL_NOT_SNAPPED[ADDR] CHECK_FOR_P1_KICKOFF_BALL_SNAP                                

num_tiles = LOCAL_1	  
num_rows  = LOCAL_2   
@clear_p1_kick_bar_draw_clock_score    
   RETURN_IN_NUM_FRAMES 10
   SET_ADDR_XY[addr] P1_KICK_BAR_PPU_ADDR
   LDA #$01                                    ; SET # ROWS = 1
   STA num_rows                                ;
   LDA #$0E                                    ; SET LENGTH = 15 TILES
   STA num_tiles                                ;
   JSR FILL_SCREEN_BLANK_TILE_AREA             ; FILL PPU LOCATIONS WITH BLANK TILE(X,Y= ADDR, 3F= NUM ROWS, 3E= LENGTH)
   RETURN_IN_NUM_FRAMES 4
   LDA #REDRAW_CLOCK_SCORE_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)

CHECK_BALL_KICKED_P1_KICKOFF:                   ; CHECK FOR BALL KICKED ON P1 KICKOFF
   RETURN_1_FRAME 
   IF_BALL_NOT_KICKED[ADDR] CHECK_BALL_KICKED_P1_KICKOFF


@start_clock_set_p1_has_ball:   
   SET_CLOCK_TO_RUNNING
   SET_POSSESSION_STATUS_P2_HAS_BALL
   PLAY_SOUND_ID KICK_SOUND 
   RETURN_IN_NUM_FRAMES 20
   LDA #P2_ON_FIELD_SONG_SIDE                  ; LOAD PLAYER 2 SOUND VALUE
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   IF_KICK_NOT_ONSIDES[ADDR] CHECK_P1_KICKOFF_RECEIVED_LOOP

@else_p2_onsides_return 
   JMP P2_ONSIDES_KICK_RETURN                  ; JUMP-> P1 ONSIDE KICK

CHECK_P1_KICKOFF_RECEIVED_LOOP:               ; CHECK FOR P1 KICKOFF RECEIVED
   RETURN_1_FRAME 
   IF_BALL_NOT_RECEIVED[ADDR] CHECK_P1_KICKOFF_RECEIVED_LOOP 
   COMPARE_BALL_X_TO_YARDLINE 100              ; 
   BCC @set_los_equal_ball_loc:                ; YES-> SET P2 KR BALL RECEIVED LOCATION

@set_yardline_equal_to_p2_goalline  
  SAVE_YARDLINE_TO_LOS_X 100
   JMP @set_p2_kick_return_scroll_limits        ;
   
@set_los_equal_ball_loc:                       ; SET P2 KR BALL RECEIVED LOCATION
   SET_LOS_X_EQUAL_TO_BALL_X

@set_p2_kick_return_scroll_limits:               ; UPDATE SCROLL WINDOW
   SET_P2_KR_SCROLL_LIMITS[]

P2_KICKOFF_RETURN_CHECKS:                      ; P2 KICKOFF RETURN CHECKS
   RETURN_1_FRAME 
   JSR CHECK_FOR_TD_P2                         ; CHECK FOR P2 ENDZONE CROSS()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE()
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS()
   BCC P2_KICKOFF_RETURN_CHECKS                ; IF ALL FALSE->P2 KICKOFF RETURN CHECKS 
                        
 @p2_kickoff_return_over:                      ; P2 KR OVER
   SET_CLOCK_TO_STOPPED
   JSR CHECK_FOR_P2_SAFETY                     ; CHECK FOR P2 SAFETY()
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL    ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR SET_CHAINS_HASH_SET_DOWN_TO_FIRST       ; UPDATE FIELD POSITION, HASHMARK, FD MARKER, AND SET DOWN TO 1()
   IF_KICK_ONSIDES[ADDR] P2_KICK_RETURN_END
 
@else_check_for_kr_injury: 
   JSR CHECK_INJURY_KR_PR_P2                   ; P2 PR/KR CHECK FOR INJURY()

P2_KICK_RETURN_END:                           ; SKIP INJURY AND STOP MUSIC P2
   JSR STOP_CURRENTLY_PLAYING_SOUND            ; STOP CURRENTLY PLAYING SOUND LOOP ()

_F}_P2_KICK_RETURN   

_F{_P2_PLAY_SELECT_AND_PLAY_LOAD 				; DONE	
   
 
P2_PLAY_SELECT_START:        
   LDA #CLEAR_ONSCR_BANNER_BANNER_ID          
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA #REDRAW_CLOCK_SCORE_BANNER_ID +  SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR CHECK_FOR_QTR_OVER                      ; CHECK FOR QUARTER OVER RESET BIT & SUSPEND SWITCH TASK IF OVER() 
   JSR CLEAR_RAM_VARS_SPRITES                  ; CLEAR PLAY STATUS, BALL INFO BYTES,PASS TIMER, PASS TARGETS, AND SPRITES()  
   LDA #PLAY_CALL_SCREEN_BANK                  ; SET BANK = 20
   SET_ADDR_XY[addr] PLAY_SELECT_START
   JSR SWAP_8000_BANK_AND_JUMP_RETURN          ; SWAP $8000 BANK AND JUMP(X,Y= ADDRESS, A= BANK TO SWAP TO)
   IF_P2_PLAY_NOT_SPECIAL_TEAMS[ADDR] P2_REGULAR_PLAY_LOAD:
   JMP P2_FG_PUNT_CHECK                        ; YES-> P2 FG or PUNT SELECTED

P2_REGULAR_PLAY_LOAD:                          ;  P2 REGULAR PLAY START
   JSR P2_ON_OFF_LOAD_PLAY_INFO_PLAYER_RAM     ; LOAD PLAY INFORMATION INTO PLAYER RAM(A=PLAY SELECTED) (cut in here for COM LOGIC HACK)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   SET_P2_PLAY_START_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                               
   JSR SET_SIDELINE_SCROLL_LOC_UPDATE_DOWN_MARKERS                        ; UPDATE LOS AND FIRST DOWN MARKERS VALUES AND GRAPHICS()
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   LDA #SCROLL_DOWN_DIST_BANNER_ID  +  SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                  
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID  +  SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 8
   JSR CHECK_FOR_P2_HIKE_P1_PLAYER_CHANGE      ; CHECK FOR P2 HIKE AND P1 DEFENDER PLAYER CHANGE()
   LDY OFF_PLAY_ADDR                           ; LOAD OFFENSE PLAY POINTER
   LDX OFF_PLAY_ADDR + 1                       ;
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   LDA #P2_ON_FIELD_SONG_SIDE                  ; SET SOUND ID PLAYER = P2
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   IF_P2_PLAY_RUN[ADDR] P2_RUN_PLAY_START

@pass_play_chosen:   
   JMP P2_PASS_PLAY_START                      ; NO->  P2 PASS PLAY START
   
_F}_P2_PLAY_SELECT_AND_PLAY_LOAD 
 
_F{_P2_RUN_PLAY									; DONE
 
P2_RUN_PLAY_START:                             ; P2 RUN PLAY CHOSEN
   INIT_POSSESSION_STATUS_P2_RUN[]
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   
@p2_run_ball_behind_los:                       ; BALL BEHIND LOS RUN PLAY LOOP

P2_RECEPTION: 
   _WHILE CC
   
   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN() 
   JSR CHECK_FOR_TD_P2                         ; CHECK FOR P2 ENDZONE CROSS()
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()  
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
@if_play_over:   
   BCS P2_PLAY_OVER_NORMAL                     ; PLAY OVER? YES->P2 RUN PLAY OVER

@check_if_past_los:   
   LDA LOS_X                                   ; IS BALL X past LINE OF SCRIMMAGE X
   CMP BALL_X+1	                               ;
   LDA LOS_X+1                                 ;
   SBC BALL_X+2	                               ;
    _END_WHILE

P2_OFFENSE_PAST_LOS_SET_D_CHASE:               ; P2 PAST LOS CLEAR TARGET INDICATOR, UPDATE PLAY STATUS, SET DEFENDERS TO CHASE
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   SET_PLAY_STATUS_MAN_CANT_PASS[]
   JSR ADD_31MS_TO_P1_MAN_DEFENDER             ; ADD 31 MS TO TOP SPEED FOR P1 MAN DEFENDER()
   SET_ADDR_XY[addr] CHASE_BALL_CARRIER_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; P1 ON OFFENSE UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS)

  
P2_RUN_PLAY_CHECKS_PAST_LOS:                   ; P2 RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK
   _WHILE CC 
   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN() 
   JSR CHECK_FOR_TD_P2                         ; CHECK FOR P2 ENDZONE CROSS()
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()

P2_RUN_FUMBLE_CHECKS:                          ; P2 FUMBLED SNAP/TOSS START
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE() 
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   
   _END_WHILE 								   ; PLAY OVER? NO-> P2 RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK
                                  

 _F}_P2_RUN_PLAY  
 
_F{_P2_PLAY_OVER_NORMAL							; DONE
 
P2_PLAY_OVER_NORMAL:                           ; P2 RUN PLAY OVER
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   JSR CHECK_FOR_P2_SAFETY                     ; CHECK FOR P2 SAFETY()
   JSR P2_CHECK_FOR_FIRST_DOWN                 ; CHECK FOR P2 FIRST DOWN / TURNOVER ON DOWNS()
   BCS @p2_turnover_on_downs                   ; TUROVER ON DOWNS? YES->  P2 TURNOVER ON DOWNS
 
@p2_play_over: 
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR UPDATE_HASH_ONLY_IF_NOT_FIRST           ; UPDATE LINE OF SCRIMMAGE AND HASHMARK TO SNAP FROM()
   JSR P2_CHECK_FOR_INJURY_NORM                ; P2 CHECK FOR INJURY()
   JMP P2_PLAY_SELECT_START                    ; JUMP->P2 PLAY CALL START
   
@p2_turnover_on_downs :                       ; P2 TURNOVER ON DOWNS
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                              
   JSR P2_CHECK_FOR_INJURY_NORM                ; P2 CHECK FOR INJURY() 
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT        ; JUMP -> P1 PlAY SELECT START

 _F}_P2_PLAY_OVER_NORMAL
 
_F{_P2_PASS_PLAY								; DONE
 
P2_PASS_PLAY_START:                            ; P2 PASS PLAY START
   INIT_POSSESSION_STATUS_P2_PASS[]			   ; Sets ball to snapped
   SET_PLAY_STATUS_MAN_CAN_PASS[]
   JSR CLEAR_CUTSCENE_TO_PLAY_SET_CUT_RAND     ; CLEAR CUTSCENE TO PLAY, SET CUTSCENE RANDOM B6

P2_QB_HASNT_PASSED_BALL_CHECKS:                ; P2 QB HASN’T PASSED BALL LOOP
   RETURN_1_FRAME 
   JSR UPDATE_CURRENT_TARGET_AND_INDICATOR     ; LOAD TARGETS AND SET CURRENT TARGET ICON ()
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()
   IF_BALL_FUMBLED[ADDR] P2_RUN_FUMBLE_CHECKS
  
@else_check_oob_pasa_los  
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCC @check_qb_past_los                      ; P2 QB PAST LOS CHECK
   JMP P2_PASS_PLAY_OVER_NO_THROW              ; JUMP ->P2 PASS PLAY OVER QB DIDN’T THROW BALL

@check_qb_past_los:                            ; P2 QB PAST LOS CHECK
   LDA LOS_X                                   ; IS BALL (X) BEHIND LOS(X)?
   CMP BALL_X+1	                               ;
   LDA LOS_X +1                                ;
   SBC BALL_X+2	                               ;
   BCC @check_for_pass                         ; YES->  P2 CHECK FOR PASS ATTEMPT

@qb_sramble_do_run_checks:
   JMP P2_OFFENSE_PAST_LOS_SET_D_CHASE         ; NO->   P2 RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK

@check_for_pass:                               ; P2 CHECK FOR PASS ATTEMPT
   IF_PASS_NOT_ATTEMPTED[ADDR] P2_QB_HASNT_PASSED_BALL_CHECKS

P2_CHECK_PASS_BLOCK_BALL_ONE_QTR_OF_WAY:       ; P2 PASS PLAY LESS THAN 1/4 OF THE WAY LOOP
   _WHILE NOT_EQUAL
   RETURN_1_FRAME 
   IF_PLAY_NOT_OVER[ADDR] P2_PASS_ONE_QTR_WAY_THERE_CHECK:

@else_p2_pass_blocked_play_over:   
   JMP P2_PLAY_OVER_NORMAL                     ; YES-> P2 RUN PLAY OVER

P2_PASS_ONE_QTR_WAY_THERE_CHECK:               ; CHECK IF PASS MORE THAN 1/4
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN()
   JSR UPDATE_TARGET_INDICATOR                 ; UPDATE TARGET INDICATOR TO ONSCREEN OR OFFSCREEN()
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   DEC PASS_TIMER_DIV_BY_4	                   ; PASS TIMER DIVIDED BY 4—
   
   _END_WHILE								   ; loop if not done
   
   LDA #EXCITED_CROWD_SIDELINE_ID              ; SET SIDELINE ANIMATION  = CROWD MOVING
   STA SIDELINE_ANI_TIMER                      ; 
   JSR SET_AND_ORDER_BALL_COLLISION_PLAYERS    ; CHECK FOR DEFENDER AND WR DISTANCE TO BALL AND ORDER()

@p2_check_for_pass_outcome_ball_in_air:        ; P2 PASS ATTEMPT FINAL LOOP
   _WHILE VALUE_CLEAR
   RETURN_1_FRAME 
   JSR CHECK_FOR_DRAW_FD_STICK                 ; CHECK FOR UPDATE LOS MARKERS IF THEY ARE OFF SCREEN()
   JSR UPDATE_TARGET_INDICATOR                 ; UPDATE TARGET INDICATOR TO ONSCREEN OR OFFSCREEN()
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   
P2_PASS_OUTCOME_CHECK:						   ; NEEDED FOR JUPMING REC/INT/TIP
  
	SWITCH_A_BIT[] BALL_STATUS  
	CASE_BIT[bit_addr] 7, P1_INTERCEPTS_PASS 	
	CASE_BIT[bit_addr] 6, P1_TIPS_PASS 
	CASE_BIT[bit_addr] 5, P2_RECEPTION
	
                       
@p2_pass_ball_collided_check:                  ; BALL COLLIDED CHECK
   BIT BALL_COLLISION                          ; DOES BALL INFO = COLLIDED
    _END_WHILE
   
   RETURN_IN_NUM_FRAMES 6
   IF_PASS_HAS_OUTCOME[ADDR] P2_PASS_OUTCOME_CHECK
 

P2_PASS_BALL_OOB_OR_HIT_GROUND:                ; P2 PASS PLAY OVER
   SET_PLAY_STATUS_PLAY_OVER[]
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   SET_CLOCK_TO_STOPPED
   LDA #INCOMPLETE_PASS_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   JSR END_GAME_FIELD_BALL_MOVE_TASKS          ; END GAME FIELD & BALL MOVEMENT TASKS & CLEAR CUTSCENE()
   SET_BALL_X_EQUAL_TO_LOS_X
   JSR STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ; STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()
   
@if_no_turnover_on_downs:  
   BCC @update_stats_go_to_P2_play_select      ; TURNOVER ON DOWNS? NO->  UPDATE STATS, HASHMARK AND GO to P2 PLAY SELECT
@else:  
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT         ; YES-> P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT

@update_stats_go_to_P2_play_select:            ; UPDATE STATS, HASHMARK AND GO to P2 PLAY SELECT()
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR UPDATE_HASH_ONLY_IF_NOT_FIRST           ; UPDATE LINE OF SCRIMMAGE AND HASHMARK TO SNAP FROM
   JMP P2_PLAY_SELECT_START                     ; JUMP ->PLAYER 2 PLAY SELECT START

 _F}_P2_PASS_PLAY    
   
_F{_P2_SACK_OR_SCRAMBLE							; DONE
    
P2_PASS_PLAY_OVER_NO_THROW:                    ; P2 PASS PLAY OVER QB DIDN’T THROW BALL
   LOAD_P2_MAN_PLAYER_POSITION_ONLY
   BNE P2_PASS_PLAY_OVER_NO_THROW_NO_SACK      ; player QB? NO-> RUN PLAY OVER

   IF_BALL_OOB_OR_RECOVERED[ADDR] P2_PASS_PLAY_OVER_NO_THROW_NO_SACK 
  
   BIT TACKLER_ID                              ; LOAD TACKLER
   BVC P2_PASS_PLAY_OVER_NO_THROW_NO_SACK      ; NO TACKLER? YES-> RUN PLAY OVER

@check_for_sack_safety:   
   LDA TACKLER_ID                              ; LOAD PLAYER ID
   CLC                                         ; 
   ADC #RE_STARTER_ID                          ; MAKE TACKLER ID  DEFENDER ID
   STA P1_MAN_POS_ID_ICON_STATUS               ; SAVE IN P2 ICON
   STA DEF_SACK_BALL_BLOCK_STARTER_ID	       ; SAVE IN PLAYER BLOCKING PASS/SACKING
   SET_PLAY_STATUS_SACK[]
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   COMPARE_BALL_X_TO_YARDLINE 100
   BCS P2_QB_SAFETIED                          ; YES-> P2 QB SAFETIED
   JSR STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ; NO->STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()
   BCS P2_QB_SACKED_ON_4TH_DOWN              ; TURNOVER ON DOWNS? YES-  P2 QB SACKED AND TURNOVER ON DOWNS
   
P2_QB_SACKED:
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                               
   LDA #QB_SACK_SCENE_ID                       ; LOAD QB SACK CUTSCENE INDEX
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR UPDATE_HASH_ONLY_IF_NOT_FIRST           ; UPDATE LINE OF SCRIMMAGE AND HASHMARK TO SNAP FROM()
   JSR P2_CHECK_FOR_INJURY_NORM                ; P2 CHECK FOR INJURY() 
   JMP P2_PLAY_SELECT_START                    ; JUMP -> PLAYER 2 PLAY SELECT START

P2_QB_SACKED_ON_4TH_DOWN:                      ; P2 QB SACKED AND TURNOVER ON DOWNS
   SET_CLOCK_TO_STOPPED 
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                               
   LDA #QB_SACK_SIDE_CHANGE_SCENE_ID           ; LOAD QB SACK W SIDECHANGE  INDEX
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JSR P2_CHECK_FOR_INJURY_NORM                ; P2 CHECK FOR INJURY()
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT        ; JUMP -> P2 TO P1 POSSESSION CHANGE

P2_QB_SAFETIED:                                ; P2 QB SAFETIED
   SET_CLOCK_TO_STOPPED
   JSR END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT                               
   SET_BALL_STATUS_SPECIAL_SAFETY[] 
   LDA #SAFETY_POINTS                          ; LOAD SAFETY POINTS
   JSR ADD_PTS_TO_P1_QTR_TOT                   ; ADD POINTS TO P1 SCORE (A=PTS)  
   LDA #QB_SACK_SAFETY_SCENE_ID                ; LOAD QB SAFETIED CUTSCENE INDEX
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JSR P2_CHECK_FOR_INJURY_NORM                ; P2 CHECK FOR INJURY()
   RETURN_IN_NUM_FRAMES 8
   JMP P2_TO_P1_POSSESSION_CHANGE_KICKOFF      ; JUMP-> P2 to P1 POSSESION CHANGE KICKOFF

P2_PASS_PLAY_OVER_NO_THROW_NO_SACK:
   JMP P2_PLAY_OVER_NORMAL                     ; JUMP-> P2 RUN PLAY OVER

     
_F}_P2_SACK_OR_SCRAMBLE 

_F{_P2_SPECIAL_TEAMS_PLAY_TYPE_CHECK  			; DONE
  
P2_FG_PUNT_CHECK:                              ; P2 CHECK FOR FG OR PUNT
   IF_P2_PLAY_PUNT[ADDR] P2_PUNT_START
   
@else_do_p2_fg:
   JMP P2_FG_START                                ; YES-> P2 FG START

_F}_P2_SPECIAL_TEAMS_PLAY_TYPE_CHECK    
 
_F{_P2_PUNT_PLAY 								; DONE

P2_PUNT_START:                                 ; P2 PUNT START 
   INIT_POSSESSION_STATUS_P2_PUNT
   LDX #PUNT_OFF_FORMATION_ID                  ; SET OFFENSIVE FORMATION = PUNT
   JSR LOAD_P2_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P2 ON OFFENSE()                       ;
   LDX #PUNT_DEF_PLAY_ID                       ; SET DEFENSIVE PLAY  = PUNT
   JSR LOAD_P1_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSIVE PLAY CODE PTRS P1 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #PUNTER_STARTER_ID + P2_SIDE_BITFLAG    ; SET SIDE (0X80= P2) , SET PLAYED ID (0X17=PUNTER)
   LDX #RE_POSITION_ID  + P2_SIDE_BITFLAG      ; LOAD PLAYER POINTER TO LOAD INTO(80= PLAYER 2 SIDE PLAYER ID=00= FIRST PLAYER=QB)
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   LDA #PR_STARTER_ID                          ; SET SIDE (0X00= P1) , SET PLAYED ID (0X19=PR)
   LDX #RT_POSITION_ID                         ; LOAD PLAYER POINTER TO LOAD INTO(00= PLAYER 1 ID=0A= LAST PLAYER=RT)
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   JSR SET_P1_MAN_TO_KR_P2_MAN_KICKER          ; SET P2 ACTIVE PLAYER TO FIRST PLAYER, P1 ACTIVE PLAYER TO LAST PLAYER()
   SET_P2_PUNT_START_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                              
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   RETURN_IN_NUM_FRAMES 2
   JSR CHECK_FOR_SNAP_P1DEF_CHANGE_P2PUNT      ; WAIT FOR SNAP P2()
   LDA #P1_ON_FIELD_SONG_SIDE                  ; LOAD PLAYER 1 SOUND
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)

P2_WAIT_FOR_BALL_PUNTED:                       ; P2 PUNT HAS BEEN SNAPPED LOOP
   RETURN_1_FRAME 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR FUMBLE IF SHOTGUN SNAP/ RUN TOSS IN PROGRESS ()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE()
   BCC @check_for_punter_tackled               ; NO FUMBLE-> P1 PUNT RETURN CHECKS
   JMP P2_RUN_PLAY_CHECKS_PAST_LOS             ; YES->RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK

@check_for_punter_tackled:                     ; P2 PUNTER TACKLED CHECK
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCC @check_ball_punted                      ; PUNTER TACKLED? NO->P2 CHECK FOR BALL PUNTED
   JMP P2_SPECIAL_TEAMS_PLAY_OVER              ; JUMP-> P2 SPECIAL TEAMS PLAY OVER

@check_ball_punted:                            ; P2 CHECK FOR BALL PUNTED
   IF_BALL_NOT_KICKED[ADDR] P2_WAIT_FOR_BALL_PUNTED

@check_for_tackle_during_punt:                 ; P2 BALL HAS BEEN PUNTED
   _WHILE EQUAL
   RETURN_1_FRAME 
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS
   BCC @check_for_punt_block                   ; PLAY CONTINUING? YES-> P1 PUNT BLOCK CHECK
   JMP P2_SPECIAL_TEAMS_PLAY_OVER              ; JUMP TO P2 SPECIAL TEAMS PLAY OVER

@check_for_punt_block:                         ; P2 PUNT BLOCK CHECK
   IF_KICK_NOT_BLOCKED[ADDR] P1_PUNT_RETURN_START

@else_p2_punt_blocked:   
   JMP P2_PUNT_BLOCKED                         ; YES-> P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT

P1_PUNT_RETURN_START:                          ; P1 RETURN P2 PUNT START
   LDA CUTSCENE_TO_DRAW                        ; DOES CUTSCENE TO PLAY =NONE?
   _END_WHILE

@do_punt_cutscene  
   JSR DO_CUTSCENE_SAVE_INFO_TO_SRAM           ; DO CUTSCENE(A= CUTSCENE ID)
   CLEAR_CLOCK_SPEED_PUNT 
   SET_POSSESSION_STATUS_P1_HAS_BALL
   RETURN_IN_NUM_FRAMES 8
   SET_ADDR_XY[addr] PUNT_COVERAGE_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   SET_ADDR_XY[addr] PUNT_RETURN_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; P1 ON OFFENSE UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS)
   
check_p2_punt_block_after_cut:                 ; ***redundant? 
   RETURN_1_FRAME 
   IF_KICK_BLOCKED[ADDR] P2_PUNT_BLOCKED
 
CHECK_FOR_P1_CAUGHT_PUNT_OR_TOUCHBACK: 
   JSR P1_CHECK_FOR_TOUCHBACK                  ; CHECK FOR P1 TOUCHBACK()
   BCS P2_PUNT_TOUCHBACK                     ; TOUCHBACK? YES-> P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT
   
   IF_BALL_NOT_RECEIVED[ADDR] check_p2_punt_block_after_cut
   
@p1_caught_punt:
   LDX #UPDATE_PUNTS_IN_GAME_INDEX             ; LOAD UPDATE P2 PUNT ATTEMPT STAT CODE
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   JSR CALCULATE_PLAY_DISTANCE_P2              ; CALCULATE DISTANCE FROM INIT LOC(RIGHT)  – CUR LOCATION (LEFT)()
   LDX #UPDATE_PUNT_YDS_IN_GAME_INDEX          ; LOAD UPDATE P2 PUNT YARDAGE STAT CODE 
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NO-N QB P2 STATS (X = STAT TO UPDATE)
   SET_LOS_X_EQUAL_TO_BALL_X
   SET_P1_PR_SCROLL_LIMITS[]
   
@p1_punt_return_checks:                        ; P1 PUNT RETURN CHECKS
   RETURN_1_FRAME 
   JSR CHECK_FOR_TD_P1                         ; CHECK FOR P1 ENDZONE CROSS() 
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE()
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS()   
   BCC @p1_punt_return_checks                  ; NO->P1 PUNT RETURN CHECKS
   
   JSR CHECK_FOR_P1_SAFETY                     ; CHECK FOR SAFETY P1 ENDZONE()
   JSR P2_to_P1_SIDE_CHANGE_BANNER_AND_MUSIC   ; PLAY SIDE CHANGE MUSIC and BANNER P2 to P1()
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   SET_POSSESSION_STATUS_P1_HAS_BALL_HAD_BALL  ; SET POSSESSION STATUS = P1 HAD AND HAS BALL IN CASE OF INJURY
   JSR CHECK_INJURY_KR_PR_P1                   ; P1 PR/KR CHECK FOR INJURY()
   SET_POSSESSION_STATUS_P2_HAS_BALL_HAD_BALL  ; SET POSSESSION STATUS = P2 KICKED PUNT FOR STAT UPDATING
  
P2_PUNT_TOUCHBACK:                             ; P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT        ; JUMP -> P2 TO P1 POSSESSION CHANGE

P2_PUNT_BLOCKED:                               ; OLD HOOK FOR PUNT BLOCK ***NOT USED****
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT        ; 

_F}_P2_PUNT_PLAY    

_F{_P2_FG_PLAY									; DONE

    
P2_FG_START :                                  ; P2 FG START
   JSR CLEAR_RAM_VARS_SPRITES                  ; CLEAR PLAY STATUS, BALL INFO BYTES,PASS TIMER, PASS TARGETS, AND SPRITES()
   INIT_POSSESSION_STATUS_P2_FG_ATT
   LDX #FG_OFF_FORMATION_ID                    ; SET OFF FORMATION =  FG FORMATION INDEX
   JSR LOAD_P2_OFF_FORMATION                   ; LOAD OFFENSE FORMATION() 
   LDX #FG_DEF_PLAY_ID                         ; SET DEFENSIVE PLAY =  FG
   JSR LOAD_P1_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSIVE PLAY CODE PTRS P1 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #KICKER_STARTER_ID + P2_SIDE_BITFLAG    ; SET SIDE= (0X80 =P2), PLAYER ID = (0X16= KICKER)
   LDX #RB1_POSITION_ID + P2_SIDE_BITFLAG 	   ; SET SIDE= (0X80 =P2), PLAYER ID = (0X01= RB)
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   SET_P2_FG_XP_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS                              ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                               ; SET GAME STATUS BIT TO ON FIELD & START UPDATE PLAYERS TASK()
   JSR SET_SIDELINE_SCROLL_LOC_UPDATE_DOWN_MARKERS                               ; UPDATE LOS AND FIRST DOWN MARKERS VALUES AND GRAPHICS()
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   LDA #FG_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                 
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID  + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR CHECK_FOR_P2_HIKE_P1_PLAYER_CHANGE       ; CHECK FOR P2 HIKE AND P1 DEFENDER PLAYER CHANGE()
   LDA #P2_ON_FIELD_SONG_SIDE                  ; SET SONG SIDE = P2
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   
P2_FG_NOT_KICKED_LOOP:                         ; P2 WAIT FOR FG TO GET TO KICKED LOOP
   RETURN_1_FRAME 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR SHOTGUN SNAP/ RUN TOSS IN PROGRESS()
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE() SHOULD BE IMPOSSIBLE WITHOUT HACK
   BCC @check_for_tackle_kick                  ;  
   JMP P2_RUN_PLAY_CHECKS_PAST_LOS             ; JUMP -> P2 RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK

@check_for_tackle_kick:                        ; CHECK FOR FG OVER OR PLAYER OUT OF BOUNDS
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCS P2_SPECIAL_TEAMS_PLAY_OVER              ; PLAY OVER? YES->P2 SPECIAL TEAMS PLAY OVER 
   IF_BALL_NOT_KICKED[ADDR] P2_FG_NOT_KICKED_LOOP

@fg_kicked:                                    ; P2 WAIT FOR FG CUTSCENE TO FINISH LOOP
   _WHILE EQUAL
   
   RETURN_1_FRAME 
   LDA CUTSCENE_TO_DRAW                        ; DOES CUTSCENE TO PLAY = NONE?
   
	_END_WHILE
	
   IF_KICK_NOT_MADE[ADDR] DO_P2_FG_CUTSCENE
   SET_CLOCK_TO_STOPPED
   LDA #FG_POINTS                              ; SET PTS TO ADD = P2 POINTS FOR FG
   JSR ADD_PTS_TO_P2_QTR_TOT                   ; ADD POINTS TO P2 SCORE(A= PTS)
   
DO_P2_FG_CUTSCENE:                                       ; P2 FG CHECK FOR BLOCK OR MISS
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   IF_KICK_BLOCKED[ADDR] P2_FG_BLOCKED
   
   IF_KICK_NOT_MADE[ADDR] P2_FG_MISSED
                                               ; P2 FG MADE
   RETURN_IN_NUM_FRAMES 8
   JMP P2_TO_P1_POSSESSION_CHANGE_KICKOFF      ; JUMP ->P2 to P1 POSSESION CHANGE KICKOFF

P2_SPECIAL_TEAMS_PLAY_OVER:                    ; P2 SPECIAL TEAMS PLAY OVER
   SET_CLOCK_TO_STOPPED
   JSR CHECK_FOR_P2_SAFETY                     ; CHECK FOR P2 SAFETY()
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT        ; JUMP -> P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT

P2_FG_BLOCKED:                                 ; P2 FG BLOCKED
   LDA #KICK_BLOCK_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                     
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR KICK_BLOCK_RECOVERY                     ; KICK BLOCK RECOVERY ROUTINE
   JMP P2_RUN_PLAY_CHECKS_PAST_LOS             ; JUMP TO P2 RUN PLAY CHECKS WITHOUT LINE OF SCRIMMAGE CHECK

P2_FG_MISSED:                                  ; P2 FG MISSED
   COMPARE_LOS_X_TO_LOCATION[name] P1_TWENTY_YARDLINE
   BCS @fg_not_inside_twenty                   ; NO->  END PLAY AND LOAD LOS LOCATION (X) into BALL LOCATION (X)
   SAVE_YARDLINE_TO_LOS_X 20 
   
@fg_not_inside_twenty:                          ; P2 END FG TASK PLAY AND LOAD LOS LOCATION (X) into BALL LOCATION (X)
   JSR END_GAME_FIELD_BALL_MOVE_TASKS          ; END GAME FIELD & BALL MOVEMENT TASKS & CLEAR CUTSCENE()
   SET_PLAY_STATUS_PLAY_OVER[]
   SET_BALL_X_EQUAL_TO_LOS_X
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT         ; JUMP -> P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT 

 _F}_P2_FG_PLAY  

_F{_P2_ONSIDES_RETURN 							; DONE

P2_ONSIDES_KICK_RETURN:                          ; P2 ONSIDES KICK RETURN
   LDA #ONSIDES_KICK_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   SET_ADDR_XY[addr] OFF_RECOVER_ONSIDE_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] DEF_RECOVER_ONSIDE_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; P1 ON OFFENSE UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS) 
   
@recovery_loop:                                ; P2 ONSIDES KICK LOOP
	_WHILE EQUAL
   RETURN_1_FRAME 
   LDA BALL_STATUS                             ; DOES BALL STATUS = ONSIDES RECOVERED?
   AND #$0C                                    ; 
   BEQ @recovery_loop                          ; NO-> P2 ONSIDES KICK LOOP
   AND #$04                                    ; DID P1 RECOVER KICK?
   BEQ @p1_def_recovers_onsides                ; YES-> P1 RECOVERED P2 ONSIDE KICK
   
@p2_off_recovers_onside: 
   SET_LOS_X_EQUAL_TO_BALL_X
   SET_P2_KR_SCROLL_LIMITS[]
   SET_ADDR_XY[addr] OFF_ONSIDE_KICK_RET_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR            ; UPDATE ALL P2 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] DEF_ONSIDE_KICK_RET_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON OFFENSE(X,Y= PLAY CODE ADDRESS)
   JSR ADD_31MS_TO_P2_MAN_DEFENDER             ; ADD 31 MS TO TOP SPEED FOR P2 ACTIVE PLAYER()
   LDA #SCROLL_IN_ONSCR_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                  
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 8
   JMP P2_KICKOFF_RETURN_CHECKS                ; JUMP -> P2 KICKOFF RETURN CHECKS

@p1_def_recovers_onsides:                      ; P1 RECOVERED P1 ONSIDE KICK
   JSR DRAW_RECOVER_TEXT                       ; DRAW RECOVER TEXT()
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT         ; JUMP ->P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT

_F}_P2_ONSIDES_RETURN

_F{_P2_PASS_TIPPED_RESULT 						; DONE
  
P1_TIPS_PASS:                                  ; P2 PASS TIPPED
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   LDA #PASS_BLOCK_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   RETURN_IN_NUM_FRAMES 40
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID  + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   
P1_TIP_OUTCOME_LOOP:                           ; P2 CHECK FOR PASS RESULT AFTER TIP LOOP
   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCC CHECK_P1_TIP_PASS_RESULT                ; PLAY NOT OVER OR OOB? YES-> CHECK for INTERCEPTION
   JMP P2_PASS_BALL_OOB_OR_HIT_GROUND          ; NO->P2 PASS PLAY OVER

CHECK_P1_TIP_PASS_RESULT:                      ; P2 CHECK for INTERCEPTION
   SWITCH_A_BIT[] BALL_STATUS
   CASE_BIT[bit_addr] 7, P1_INTERCEPTS_PASS    ; checks for jumping int
   CASE_BIT[bit_addr] 5, P2_RECEPTION	       ; checks for jumping rec

  
@check_ball_collided:                          ; CHECK FOR BALL COLLIDED
   ASL                                         ; DOES BALL STATUS = PASS CAUGHT AFTER TIP? =0X08 MASK
   ASL                                         ; 
   BCS @check_who_caught_ball_after_tip         ; YES->P2 BALL RECOVERED
   BIT BALL_COLLISION                          ; DOES BALL STATUS = BALL COLLIDED
   BVC P1_TIP_OUTCOME_LOOP                     ; NO-> P2 CHECK FOR PASS RESULT AFTER TIP LOOP
   
@tipped_ball_collided:   
   RETURN_IN_NUM_FRAMES 6 
   LDA BALL_STATUS                             ; DOES BALL STATUS = INTERCEPTED, CAUGHT, RECOVERED AFTER TIP
   AND #$A8                                    ; 
   BNE P1_TIP_OUTCOME_LOOP                     ; YES-> P2 PASS TIPPED LOOP
   JMP P2_PASS_BALL_OOB_OR_HIT_GROUND
   
@check_who_caught_ball_after_tip:             ; P2 BALL CAUGHT AFTER TIP
   ASL                                         ; DID P2 WR CATCH BALL?
   BCS @wr_catches_tip                         ; YES->   P2 WR CAUGHT TIPED PASS
   JMP P1_INTERCEPTS_PASS                      ; NO->    PLAYER 1 DEFENDER GETS INTERCEPTION

@wr_catches_tip:                               ; P2 WR CAUGHT TIPED PASS
   JMP P2_RECEPTION                            ; YES-> P2 RUN PLAY CHECKS LOOP

_F}_P2_PASS_TIPPED_RESULT   

_F{_P2_SAFETIED                                	; DONE
   
P2_SAFETIED:                                   ; P2 SAFETIED
   PLAY_SOUND_ID SAFETY_SONG 
   SET_CLOCK_TO_STOPPED
   LDA #SAFETY_POINTS                          ; LOAD SAFETY POINTS
   JSR ADD_PTS_TO_P1_QTR_TOT                   ; ADD POINTS TO P1 SCORE (A=PTS)  
   SET_BALL_STATUS_SPECIAL_SAFETY[]
   LDA #SAFETY_BANNER_ID +  SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                  
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   LDA #P1_TEAM_CITY_BANNER_ID +  SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG 
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   LDA #LARGE_SCORE_SCROLL_BANNER_ID +  SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 90
   LDA #CLEAR_ONSCR_BANNER_BANNER_ID            ; SET BANNER ID =   CLEAR NAMETABLE 0
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA #$00                                    ; SET IRQ0 SPLIT NAMETABLE = NAMETABLE 0
   STA IRQ_0_NAMETABLE                         ;
   JMP P2_TO_P1_POSSESSION_CHANGE_KICKOFF      ; JUMP TO P2 to P1 POSSESION CHANGE KICKOFF

_F}_P2_SAFETIED 

_F{_P2_TD 										; DONE

DO_PLAYER_2_TOUCHDOWN:                                     ; PLAYER CROSSED LEFT ENDZONE FOR TD
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   SET_PLAY_STATUS_TOUCHDOWN[]
   LDA #JUMPING_CHEERLEADER_SIDELINE_ID        ; SET SIDELINE ANIMATION  = JUMPING CHEERLEADERS, EXCITED CROWD
   STA SIDELINE_ANI_TIMER                      ;
   SET_CLOCK_TO_STOPPED
   LDA #TD_POINTS                              ; LOAD TD POINTS
   JSR ADD_PTS_TO_P2_QTR_TOT                   ; ADD POINTS TO P2 SCORE(A= PTS)
   LDA #TOUCHDOWN_BANNER_ID +  SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA POSSESSION_STATUS                       ; WAS TD = DEFENSIVE TD?
   BPL P2_DEFENSIVE_TD                        ; YES->  DRAW P2 DEFENSIVE TD CUTSCENE
   
P2_OFFENSIVE_TD:   
   SET_ADDR_XY[addr] OFF_PLAYERS_CHEER_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR            ; UPDATE ALL P2 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS) 
   SET_ADDR_XY[addr] DEF_PLAYERS_CRY_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; P1 ON OFFENSE UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS)
   JSR PLAY_P2_TD_MUSIC_UPDATE_BANNER_UPDATE_STATS                             
   LDX #RUSHING_TOUCHDOWN_SCENE_ID             ; SET SCENE ID = RUN TD
   IF_PASS_NOT_ATTEMPTED[ADDR] DRAW_P2_TD_CUTSCENE
   
@else_set_to_passing_td_scene_id
   INX                                         ; SCENE ID++ = PASS TD

DRAW_P2_TD_CUTSCENE:                               ; DRAW P2 TD CUTSCNE
   TXA                                         ; LOAD CUTSCENE VALUE (PASS OR RUN TD)
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JMP CHECK_IF_XP_KICK_NEEDED_P2              ; JUMP->P2 XP CHECK

P2_DEFENSIVE_TD:                               ; DRAW P2 DEFENSIVE TD CUTSCENE
   SET_ADDR_XY[addr] DEF_PLAYERS_CHEER_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR            ; UPDATE ALL P2 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS) 
   
   SET_ADDR_XY[addr] OFF_PLAYERS_CRY_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE ALL DEF PLAYERS PLAY CODE(X,Y= PLAY CODE POINTERS START ADDRESS)
  
  JSR PLAY_P2_TD_MUSIC_UPDATE_BANNER_UPDATE_STATS                              
   INIT_POSSESSION_STATUS_P2_RUN[]             ; 
   LDA #RUSHING_TOUCHDOWN_SCENE_ID             ; LOAD RUSHING TD CUTSCENE INDEX
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   JMP CHECK_IF_XP_KICK_NEEDED_P2              ; JUMP TO P2 XP CHECK

PLAY_P2_TD_MUSIC_UPDATE_BANNER_UPDATE_STATS:   ; P2 PLAY TD SOUND, DRAW TEAM NAME, SET BALL LOCATION, UPDATE STATS
   RETURN_IN_NUM_FRAMES 2
   PLAY_SOUND_ID TOUCHDOWN_DMC 
   RETURN_IN_NUM_FRAMES 90
   LDA #P2_TEAM_CITY_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 90
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   SET_BALL_X_EQUAL_TO_LOCATION[name] FRONT_LEFT_ENDZONE 
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   RTS                                         ; RETURN

CHECK_IF_XP_KICK_NEEDED_P2:                    ; P2 XP CHECK
   IF_QTR_NOT_OVERTIME[ADDR] DO_P2_XP
   JSR CHECK_FOR_QTR_OVER                      ; CHECK FOR QUARTER OVER RESET BIT & SUSPEND SWITCH TASK IF OVER()
      
DO_P2_XP:                                     ; P2 XP START
   JSR SET_TO_1st_DOWN_CLEAR_RAM_VARS_SPRITES  ; SET FIRST DOWN, CLEAR SPRITE DATA AND GRAPHICS, RESET PLAY VARS & ACTIVE PLAYER LOC/ICON()
   LDA #$00                                    ; SET P1/P2 PLAY CALL = ZERO
   STA P1_PLAY_CALL                            ;
   STA P2_PLAY_CALL                            ;
   INIT_POSSESSION_STATUS_P2_XP
   
@set_xp_hash_and_yardline:   
   SAVE_YARDLINE_TO_LOS_X 2
   LDA #MID_HASH_Y                             ; SET Y LOS TO MIDDLE 
   STA LOS_Y                                   ; 
 
@load_formation_and_play 
   LDX #XP_OFF_FORMATION_ID                    ; SET OFFENSIVE FORMATION = FG/XP FORMATION INDEX
   JSR LOAD_P2_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P1 ON OFFENSE() 
   LDX #XP_DEF_PLAY_ID                         ; SET DEFENSIVE PLAY = FG/XP FORMATION INDEX
   JSR LOAD_P1_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSIVE PLAY CODE PTRS P1 ON DEFENSE(X=DEFENSIVE PLAY INDEX) 
   
@set_player_skills_p2_xp:   
   JSR LOAD_ALL_SKILLS_INTO_PLAYER_RAM         ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #KICKER_STARTER_ID + P2_SIDE_BITFLAG    ; SET SIDE(0X80= P2) + SET PLAYER ID(0X16=KICKER)
   LDX #RB1_POSITION_ID + P2_SIDE_BITFLAG      ; SET SIDE(0X80= P2) + SET PLAYER TO LOAD INTO (0X01=RB)
   JSR LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM ; SET PLAYER SKILLS IN PLAYER RAM (A=PLAYER ID, X=PLAYER POINTER TO LOAD INTO)
   SET_P2_FG_XP_SCROLL_LIMITS[]
   JSR UPDATE_FIELD_SCROLL_SETTINGS            ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL)
   JSR SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK                               ; SET GAME STATUS BIT TO ON FIELD & START UPDATE PLAYERS TASK()
   LDA #REDRAW_CLOCK_SCORE_BANNER_ID           ; SET BANNER ID = REDRAW CLOCK AND SCORE
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   RETURN_IN_NUM_FRAMES 8
   JSR CHECK_FOR_P2_HIKE_P1_PLAYER_CHANGE_XP   ; CHECK FOR P1 DEFNDER PLAYER CHANGE AND P2 SNAP NO CLOCK CHECK()
   LDA #P2_ON_FIELD_SONG_SIDE                  ; SET SONG SIDE = P2
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)

CHECK_P2_XP_KICKED:                            ; P2 XP CHECK FOR BALL KICKED LOOP
   RETURN_1_FRAME 
   JSR CHECK_FOR_FUMBLED_SNAP_OR_TOSS          ; CHECK FOR SHOTGUN SNAP/ RUN TOSS IN PROGRESS()
   JSR CHECK_FOR_PLAY_OVER_P2_OOB              ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   BCS EXIT_P2_XP                              ; 
   IF_BALL_NOT_KICKED[ADDR] CHECK_P2_XP_KICKED
   

@wait_xp_cutscene_done:                  		; P2 XP WAIT FOR CUTSCENE OVER LOOP
   RETURN_1_FRAME 
   LDA CUTSCENE_TO_DRAW                        ; DOES CUTSCENE TO PLAY = NONE
   BEQ @wait_xp_cutscene_done           		; NO-> P2 XP WAIT FOR CUTSCENE OVER LOOP
   IF_KICK_NOT_MADE[ADDR] P2_XP_CHECK_FOR_ADDITIONAL_CUTSCENE
   
@add_xp_pts_to_p2_score:   
   LDA #XP_PTS                                    ; LOAD EXTRA POINT AMOUNT
   JSR ADD_PTS_TO_P2_QTR_TOT                   ; ADD POINTS TO P2 SCORE(A= PTS)
   
P2_XP_CHECK_FOR_ADDITIONAL_CUTSCENE:           ; P2 XP PLAY CUTSCNE CHECK
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()

EXIT_P2_XP:
   JMP P2_TO_P1_POSSESSION_CHANGE_KICKOFF                               ; JUMP-> P2 to P1 POSSESION CHANGE KICKOFF

_F}_P2_TD    

_F{_P2_INTERCEPTED								; DONE

P1_INTERCEPTS_PASS:                            ; PLAYER 1 DEFENDER GETS INTERCEPTION
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   RETURN_IN_NUM_FRAMES 3
   SET_LOS_X_EQUAL_TO_BALL_X
   
   SET_ADDR_XY[addr] INT_RETURN_DEFENSE_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR            ; UPDATE ALL P2 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] INT_RETURN_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR            ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON DEFENSE(X,Y= PLAY CODE ADDRESS)
   
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED().
   LDA #INTERCEPTION_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA #P1_ON_FIELD_SONG_SIDE                   ; LOAD PLAYER 1 SOUND VALUE
   JSR GET_ON_FIELD_SONG_ID                    ; UPDATE CURRENTLY PLAYING SOUND BASED ON GAME TYPE(A=P1 or P2)
   STA ON_FIELD_SONG_TO_PLAY_AFTER_INT        ; SAVE SONG ID TO PLAY AFTER INTERCEPTION MUSIC 
   JSR P1_CHECK_FOR_TOUCHBACK                  ; CHECK FOR P1 TOUCHBACK()
   BCS P2_TO_P1_POSS_CHANGE_PLAY_SELECT       ; TOUCHBACK? YES->P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT
   RETURN_IN_NUM_FRAMES 60
   LDA #SCROLL_CLOCK_SCORE_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   
UPDATE_SCROLL_LIMITS_P2_TURNOVER:              ; UPDATE SCROLL THRESHOLDS AFTER P2 GETS TURNOVER
   SET_P2_INT_PR_FUM_SCROLL_LIMITS[]   

@p1_int_pr_fumble_return_checks:                                        ; P1 TURNOVER RETURN CHECKS LOOP 
   RETURN_1_FRAME 
   JSR DO_CUTSCENE_IF_NEEDED                   ; PLAY CUTSCENE IF NEEDED()
   JSR CHECK_FOR_TD_P1                         ; CHECK FOR P1 ENDZONE CROSS() 
   JSR CHECK_IF_BALL_FUMBLED                   ; CHECK FOR FUMBLE()
   JSR CHECK_FOR_PLAY_OVER_P1_OOB              ; CHECK FOR PLAY OVER OR OUT OF BOUNDS()
   BCC @p1_int_pr_fumble_return_checks         ; NO TD, FUM, PLAY OVER? JUMP->P1 INTERCEPTION RETURN CHECKS LOOP

@turnvoer_play_over:    
   JSR CHECK_FOR_P1_SAFETY                     ; CHECK FOR SAFETY P1 ENDZONE()
   JSR STOP_CURRENTLY_PLAYING_SOUND            ; STOP CURRENTLY PLAYING SOUND LOOP()
   RETURN_IN_NUM_FRAMES 2
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT      ; JUMP -> P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT

_F}_P2_INTERCEPTED
 
_F{_P2_TO_P1_POSSESSION_CHANGE 					; DONE

P2_TO_P1_POSSESSION_CHANGE_KICKOFF:            ; P2 to P1 POSSESION CHANGE KICKOFF
   JSR CHECK_FADE_OUT_P2_TO_P1                 ; STOP CLOCK, UPDATE STATS, UPDATE FIELD LOCA HASH, DOWN, SET POSS P2, CHECK QTR OVER()
   JMP P2_KICKOFF_START                        ; JUMP->PLAYER 2 KICKOFF START

P2_TO_P1_POSS_CHANGE_PLAY_SELECT:              ; P1 to P2 POSSESION CHANGE TO PLAY SELECT
   JSR CHECK_FADE_OUT_P2_TO_P1                 ; STOP CLOCK, UPDATE STATS, UPDATE FIELD LOCA HASH, DOWN, SET POSS P2, CHECK QTR OVER()
   JMP P1_PLAY_SELECT_START                    ; JUMP-> PLAYER 1 PLAY SELECT START

CHECK_FADE_OUT_P2_TO_P1:                               ; P2 to P1 STOP CLOCK, UPDATE STATS, UPDATE FIELD LOCA HASH, DOWN, SET POSS P2, CHECK QTR OVER()
   LDA BG_FADE                                 ; DOES BACKGROUND FADE = FADED OUT COMPLETELY
   BEQ @stop_clock_kill_tasks_update_stats      ; 
   JSR P2_to_P1_SIDE_CHANGE_BANNER_AND_MUSIC   ; PLAY SIDE CHANGE MUSIC and BANNER P2 to P1()

@stop_clock_kill_tasks_update_stats:           ; P2 TO P1 STOP CLOCK, KILL TASKS, SET POSESSION STATUS TO P1
   SET_CLOCK_TO_STOPPED 
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   JSR UPDATE_IN_GAME_TEAM_PLAYER_STATS        ; UPDATE SRAM TEAM/PLAYER STATS()
   JSR SET_CHAINS_HASH_SET_DOWN_TO_FIRST       ; UPDATE FIELD POSITION, HASHMARK, FD MARKER, AND SET DOWN TO 1() 
   INIT_POSSESSION_STATUS_P1_NEW_PLAY[]
   RETURN_IN_NUM_FRAMES 2                      ; 
   JMP CHECK_FOR_QTR_OVER                      ; JUMP->CHECK FOR QUARTER OVER RESET BIT & SUSPEND SWITCH TASK IF OVER()

_F}_P2_TO_P1_POSSESSION_CHANGE 
 
_F{_CHECK_FOR_FIRST_DOWN_OR_TOD					; DONE
 
 distance_past_1st 	= LOCAL_7				; word size
 draw_chains 		= LOCAL_8				   ; 
 true				= $FF
 false				= $00
 nine_yards			= $48
 ten_yards			= $50
 eleven_yards		= $58 
 chain_chance		= $03					   ; =25% chance of chains by bitwise and of random number
 
P1_CHECK_FOR_FIRST_DOWN:                       ; CHECK IF P1 PAST FIRST DOWN MARKER()
   LDA BALL_X+1	                               ; DISTANCE PAST FIRST = BALL X- FD X
   SEC                                         ;
   SBC FD_MARKER_X                             ;
   STA distance_past_1st                       ;
   LDA BALL_X+2	                               ; 
   SBC FD_MARKER_X+1                           ; 
   STA distance_past_1st +1                    ; 
   BCS P1_CLOSE_TO_FIRST                       ; CLOSE TO FIRST DOWN->  P1 CLOSE TO FIRST DOWN
   JMP STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ; NO->  STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()

P1_CLOSE_TO_FIRST:                             ; P1 CLOSE TO FIRST DOWN
   JSR CHECK_FOR_CHAIN_DRAW                    ; CHECK FOR FIRST DOWN()
   BCC STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ; NO FD-> STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()
@else_p1_first:   
   MMC3_SRAM_WRITE_ENABLE 
   INC P1_FIRST_DOWNS                          ; INCREASE P1 FIRST DOWNS
   MMC3_SRAM_WRITE_DISABLE 
   
   LDA draw_chains                             ; CLOSE ENOUGH FOR DRAWING CHAINS
   BMI DRAW_P1_FIRST_DOWN                      ; YES-> DRAW P1 FIRST DOWN CHAINS
  
@play_p1_1st_music_and_do_banner:     
   PLAY_SOUND_ID P1_FD_SONG 
   SUSPEND_TASK_DIRECT[task] UPDATE_CLOCK_TASK 
   LDA #FIRST_DOWN_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                 
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   LDX #UPDATE_CLOCK_TASK                      ; SET TASK INDEX = TASK 0 (CLOCK) 
   JSR RESUME_TASK                             ; RESUME TASK(X=TASK INDEX)
   CLC                                         ; CLEAR CARRY = FIRST DOWN MADE
   RTS                                         ;
   
DRAW_P1_FIRST_DOWN:                            ; DRAW P1 FIRST DOWN 
   LDA #P1_CHAINS_FIRST_DOWN_SCENE_ID          ; LOAD P1 FIRST DOWN INDEX
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   CLC                                         ; CLEAR CARRY = FIRST DOWN MADE
   RTS                                         ; RETURN

P2_CHECK_FOR_FIRST_DOWN:                       ; CHECK IF P2 PAST FIRST DOWN MARKER()
   LDA FD_MARKER_X                             ; DISTANCE PAST FIRST = FD (X) – BALL (X)
   SEC                                         ;
   SBC BALL_X+1	                               ;
   STA distance_past_1st                        ;
   LDA FD_MARKER_X+1                           ;
   SBC BALL_X+2	                               ;
   STA distance_past_1st+1                     ; 
   BCC STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ;
   JSR CHECK_FOR_CHAIN_DRAW                    ; CHECK FOR FIRST DOWN()
 
@p2_no_first: 
   BCC STOP_SOUND_CHECK_TURNOVER_ON_DOWNS      ; FIRST DOWN MADE? NO->STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()
   
@else_p2_first:   
   MMC3_SRAM_WRITE_ENABLE  
   INC P2_FIRST_DOWNS                          ; INCREASE P2 FIRST DOWNS 
   MMC3_SRAM_WRITE_DISABLE 
   LDA draw_chains                             ; DRAW CHAINS FOR PLAYER FIRST DOWN?
   BMI DRAW_P2_FIRST_DOWN                      ; YES-> DRAW P2 FIRST DOWN WITH CHAINS
 
@play_p2_1st_music_and_do_banner:   
   PLAY_SOUND_ID P2_FD_SONG 
   SUSPEND_TASK_DIRECT[task] UPDATE_CLOCK_TASK
   LDA #FIRST_DOWN_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 60
   LDX #UPDATE_CLOCK_TASK                       ; SET TASK INDEX = TASK 0 (CLOCK)
   JSR RESUME_TASK                             ; RESUME TASK(X=TASK INDEX)
   CLC                                         ; CLEAR CARRY = FIRST DOWN MADE
   RTS                                         ; RETURN

DRAW_P2_FIRST_DOWN:                            ; DRAW P2 FIRST DOWN WITH CHAINS
   LDA #P2_CHAINS_FIRST_DOWN_SCENE_ID          ; SET BANNER ID = P2 CHAINS FIRST DOWN 
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   CLC                                         ; CLEAR CARRY = FIRST DOWN MADE
   RTS                                         ; RETURN

STOP_SOUND_CHECK_TURNOVER_ON_DOWNS:            ; STOP SOUND LOOP AND CHECK FOR TURNOVER ON DOWNS()
   JSR STOP_CURRENTLY_PLAYING_SOUND            ; STOP CURRENTLY PLAYING SOUND LOOP ()
   INC DOWN                                    ; NEXT DOWN = DOWN +1
   LDA DOWN                                    ;
   CMP #FOURTH_DOWN + 1	                       ; IS NEXT DOWN > 4 
   RTS                                         ; RETURN

   
											    	
CHECK_FOR_CHAIN_DRAW:                          ; CHECK FOR CHAINS()
   LDA distance_past_1st+1                     ; PLAY CLOSE ENOUGH FOR FIRST DOWN CHECK?
   BNE @set_draw_chains_false                  ; NO_>  SET DOWN TO FIRST DOWN WITHOUT FIRST DOWN CHAINS 
   LDA RANDOM_1                                ; LOAD RANDOM # 
   LDX distance_past_1st                       ; IS DISTANCE PAST FIRST LOS MARKER < 10 YARDS PAST MARKER
   CPX #ten_yards                              ;
   BCC DRAW_CHAINS_NOT_FIRST_CHECK             ; YES->  DRAW CHAINS CHECK
   CPX #eleven_yards                           ; DISTANCE > 11 YARDS PAST MARKER
   BCS @set_draw_chains_false                  ; YES-> SET DOWN TO FIRST DOWN WITHOUT FIRST DOWN CHAINS
   
@random_chance_chain_draw:   				   ; if within 1 yard past los
   AND #chain_chance                           ; MAKE RANDOM 75 % CHANCE OF NO CHAIN DRAW
   BNE @set_draw_chains_false                  ; RANDOM NOT ZERO->  SET DOWN TO FIRST DOWN WITHOUT FIRST DOWN CHAINS
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   LDA #true                                   ; SET $45 to FF = DRAW CHAINS 
   JMP @save_draw_chains_set_down_to_first     ;
   
@set_draw_chains_false:                        ; SET DOWN TO FIRST DOWN WITHOUT FIRST DOWN CHAINS
   LDA #false                                  ; SET $45 to 0 =  DON’T DRAW CHAINS

@save_draw_chains_set_down_to_first:
   STA draw_chains                             ; SAVE DRAW CHAINS (YES OR NO)
   LDA #FIRST_DOWN                             ; SET DOWN = FIRST DOWN
   STA DOWN                                    ;
   LDA #JUMPING_CHEERLEADER_SIDELINE_ID        ; SET SIDELINE ANIMATION  = JUMPING CHEERLEADERS, EXCITED CROWD
   STA SIDELINE_ANI_TIMER                      ;
   SEC                                         ; SET CARRY = FIRST DOWN  MADE
   RTS                                         ; RETURN

DRAW_CHAINS_NOT_FIRST_CHECK:                   ; DRAW CHAINS CHECK
   CPX #nine_yards                             ; WITHIN +/-1 YARD OF FD
   BCC @no_first_exit                           ; NO->  SHORT OF FIRST DOWN NO CUTSCNE

@random_chance_no_first_chains_cut 
   AND #chain_chance                                    ; CHECK RANDOM FOR CUTSCNE
   BNE @no_first_exit                           ; 75 CHANCE TO  SHORT OF FIRST DOWN NO CUTSCNE
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   LDA DOWN                                    ; = DOWN + SHORT OF FD CUTSCENE OFFSET 
   CLC                                         ;
   ADC #CHAINS_NO_FIRST_SCENE_IDS_START[]      ;
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   
@no_first_exit:                                ; SHORT OF FIRST DOWN NO CUTSCNE 
   CLC                                         ; CLEAR CARRY = FIRST DOWN NOT MADE
   RTS                                         ; RETURN

_F}_CHECK_FOR_FIRST_DOWN_OR_TOD

_F{_UPDATE_HASHMARK_FOR_NEXT_SNAP             ; DONE

UPDATE_HASH_ONLY_IF_NOT_FIRST:                 ; UPDATE LINE OF SCRIMMAGE AND HASHMARK TO SNAP FROM()
   LDA DOWN                                    ; IS DOWN = 1st down?
   BNE NO_1ST_SET_HASH                         ; NO->  NOT FIRST DOWN
SET_CHAINS_HASH_SET_DOWN_TO_FIRST:             ; NEW FIRST DOWN
   LDA BALL_X+1	                               ; SET FIRST DOWN CHAIN X LOCATION  = BALL X LOC  
   STA FD_MARKER_X                             ;
   LDA BALL_X+2	                               ;
   STA FD_MARKER_X+1                           ;
   LDA #$00                                    ; SET DOWN =  1
   STA DOWN                                    ;
   
NO_1ST_SET_HASH:                              ; NOT FIRST DOWN
   SET_LOS_X_EQUAL_TO_BALL_X
   
@check_hash_top
   LDA BALL_Y+1                                ; BALL Y > TOP HASH
   CMP #TOP_HASH_Y - $10                       ; 
   BCS @check_hash_bottom                      ; YES->CHECK FOR MIDDLE HASH
   LDA #TOP_HASH_Y - $10                       ; SET HASH MARK = TOP HASH
@check_hash_bottom:                            ; CHECK FOR MIDDLE HASH
   CMP #BOT_HASH_Y  - $10                      ; BALL Y < MIDDLE HASH
   BCC @set_hash                               ; 
   LDA #BOT_HASH_Y - $10                       ; SET HASH MARK = BOTTOM HASH
@set_hash:                                     ;
   CLC                                         ;
   ADC #$10                                    ; = LOCATION + 10
   STA LOS_Y                                   ; SET LINE OF SCRIMMAGE Y = LOCATION
   RTS                                         ; RETURN

_F}_UPDATE_HASHMARK_FOR_NEXT_SNAP

_F{_CHECK_FOR_TD								; DONE

CHECK_FOR_TD_P1:                               ; CHECK FOR P1 ENDZONE CROSS
   COMPARE_P1_BALL_CARRIER_TO_LOCATION[name] FRONT_RIGHT_ENDZONE
   BCS @p1_touchdown                           ; YES-> IN P1 ENDZONE
   RTS                                         ; RETURN
   
@p1_touchdown:                                 ; IN P1 ENDZONE
   PLA                                         ; REMOVE RETURN ADDRESS 
   PLA                                         ; 
   JMP DO_PLAYER_1_TOUCHDOWN                    ; JUMP -> PLAYER CROSSED P1 ENDZONE TD

CHECK_FOR_TD_P2:                               ; CHECK FOR P2 ENDZONE CROSS
   COMPARE_P2_BALL_CARRIER_TO_LOCATION[name] FRONT_LEFT_ENDZONE + 1
   BCC @p2_touchdown                           ; P2 IN ENDZONE? YES->P2 IN ENDZONE
   RTS                                         ; RETURN

@p2_touchdown:                                 ; P2 IN ENDZONE
   PLA                                         ; REMOVE RETURN ADDRESS
   PLA                                         ;
   JMP DO_PLAYER_2_TOUCHDOWN                   ; JUMP-> PLAYER CROSSED P2 ENDZONE TD

_F}_CHECK_FOR_TD   

_F{_CHECK_FOR_TOUCHBACK							; DONE
   
P1_CHECK_FOR_TOUCHBACK:                        ; CHECK FOR TOUCHBACK P1() 
   COMPARE_BALL_X_TO_LOCATION[name] FRONT_LEFT_ENDZONE +1 
   BCS @no_touchback                           ; NOT IN ENDZONE->  P1 NO TOUCHBACK 

@p1_touchback:  
   LDX #TOUCHBACK_BANNER_ID + SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG    
   JSR DO_TOUCHBACK                            ; SET BALL STATUS TO TOUCHBACK AND DO TOUCHBACK()
   SET_YARDLINE_X 20 
   JSR SAVE_FD_LOS_BALL_X_FOR_TOUCHBACK[xloc]  ; STORE X LOCATION INTO BALL LOCATION (X), FIRST DOWN MARKER(X), LINE OF SCRIMMAGE(X) 
   RTS                                         ; RETURN
@no_touchback:                                        ; P1 NO TOUCHBACK 
   CLC                                         ; CLEAR CARRY = NOT A TOUCHBACK
   RTS                                         ; RETURN

P2_CHECK_FOR_TOUCHBACK:                        ; CHECK FOR P2 TOUCHBACK()
   COMPARE_BALL_X_TO_LOCATION[name] FRONT_RIGHT_ENDZONE  
   BCC @no_touchback                           ; NOT IN ENDZONE->  P2 NO TOUCHBACK

@p2_touchback:   
   LDX #TOUCHBACK_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR DO_TOUCHBACK                            ; SET BALL STATUS TO TOUCHBACK AND DO TOUCHBACK()
   SET_YARDLINE_X 80 
   JSR SAVE_FD_LOS_BALL_X_FOR_TOUCHBACK[xloc]  ; STORE X LOCATION INTO BALL LOCATION (X), FIRST DOWN MARKER(X), LINE OF SCRIMMAGE(X)
@no_touchback:                                 ; P2 NO TOUCHBACK
   RTS                                         ; RETURN


   
   
DO_TOUCHBACK:                                  ; SET BALL STATUS TO TOUCHBACK AND DO TOUCHBACK()
   SET_BALL_STATUS_SPECIAL_TOUCHBACK[]
   LDA BALL_STATUS                             ; DOES BALL STATUS = BALL INTERCEPTED
   BPL @draw_banner                            ; NO-> DRAW BANNER
   RETURN_IN_NUM_FRAMES 60
   JSR STOP_CURRENTLY_PLAYING_SOUND            ; STOP CURRENTLY PLAYING SOUND LOOP ()
   RETURN_IN_NUM_FRAMES 02
   LDA #TOUCHBACK_MESSAGE_BAR_ID               ; SET SCENE ID = TOUCHBACK BANNER
   LDX #MESSAGE_BAR_DRAW_SCRIPT_BANK           ; SET BANK = BANK 16
   JSR DRAW_SCENE_RETURN_PREV_8000_BANK        ; DO CUTSCENE (A= CUTSCNE INDEX, X= BANK)
   JMP @play_TB_song_fade_out                  ; JUMP ->PLAY TOUCHBACK SOUND AND SET CLOCK TO STOPPED
@draw_banner:                                  ; DRAW BANNER 
   TXA                                         ; LOAD BANNER ID (TOUCHBACK SCROLL IN FROM LEFT OR RIGHT) 
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
@play_TB_song_fade_out:                        ; PLAY TOUCHBACK SOUND AND SET CLOCK TO STOPPED
   PLAY_SOUND_ID TOUCHBACK_SONG
   RETURN_IN_NUM_FRAMES 60
   JSR WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL     ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   SET_CLOCK_TO_STOPPED
   RTS                                         ; RETURN



SAVE_FD_LOS_BALL_X_FOR_TOUCHBACK[xloc]:        ; STORE X LOCATION INTO BALL LOCATION (X), FIRST DOWN MARKER(X), LINE OF SCRIMMAGE(X)
   STY FD_MARKER_X                             ; 
   STY LOS_X                                   ;
   STY BALL_X+1	                               ;
   STX FD_MARKER_X+1                           ;
   STX LOS_X+1                                 ;
   STX BALL_X+2	                               ;
   SEC                                         ; = touchback ooccured
   RTS                                         ; RETURN
   
_F}_CHECK_FOR_TOUCHBACK

_F{_CHECK_FOR_SAFETY                           ; DONE

CHECK_FOR_P1_SAFETY:                           ; CHECK FOR P1 SAFETY()
   COMPARE_BALL_X_TO_LOCATION[name] FRONT_LEFT_ENDZONE+1  
   BCC @player_1_saftied:                      ; YES->P1 SAFETY OCCURRED
   RTS                                         ; EXIT
@player_1_saftied:                              ; P1 SAFETY OCCURRED
   PLA                                         ;
   PLA                                         ;
   JMP P1_SAFTIED                               ; JUMP->P1 SAFTIED

CHECK_FOR_P2_SAFETY:                           ; CHECK FOR P2 SAFETY()
   COMPARE_BALL_X_TO_LOCATION[name] FRONT_RIGHT_ENDZONE 
   BCS @player_2_saftied                       ; YES->P2 SAFETY OCCURRED
   RTS                                         ; EXIT
@player_2_saftied:                                        ; P2 SAFETY OCCURRED
   PLA                                         ; REMOVE RETURN ADDRESS
   PLA                                         ; 
   JMP P2_SAFETIED                              ; JUMP->P2 SAFTIED

_F}_CHECK_FOR_SAFETY

_F{_CHECK_FOR_PLAY_OVER							; DONE
   
CHECK_FOR_PLAY_OVER_P1_OOB:                    ; CHECK FOR PLAY OVER OR P1 ACTIVE PLAYER OUT OF BOUNDS
   IF_P1_MAN_NOT_BALL_CARRIER[ADDR] CHECK_IF_PLAY_OVER
   
@else:   
   LOAD_P1_MAN_PLAYER_Y_POS
   JMP CHECK_PLAYER_OOB                        ; JUMP ->CHECK FOR PLAYER OUT OF BOUNDS

CHECK_FOR_PLAY_OVER_P2_OOB:                    ; CHECK FOR PLAY OVER OR P2 ACTIVE PLAYER OUT OF BOUNDS
   IF_P2_MAN_NOT_BALL_CARRIER[ADDR] CHECK_IF_PLAY_OVER
   
 @else:
   LOAD_P2_MAN_PLAYER_Y_POS

CHECK_PLAYER_OOB:                              ; CHECK FOR PLAYER OUT OF BOUNDS
   CMP #OUT_OF_BOUNDS_TOP_Y                    ; PLAYER Y < OUT OF BOUNDS TOP
   BCC PLAYER_OUT_OF_BOUNDS                    ; YES-> PLAYER OUT OF BOUNDS
   CMP #OUT_OF_BOUNDS_BOT_Y+1                  ; PLAYER Y > OUT OF BOUNDS BOTTOM
   BCS PLAYER_OUT_OF_BOUNDS                    ; YES-> PLAYER OUT OF BOUNDS

CHECK_IF_PLAY_OVER:                            ; CHECK FOR PLAY OVER             
   IF_PLAY_NOT_OVER[ADDR] PLAY_NOT_OVER
@else:   
  JMP PLAY_OVER_PLAYER_TACKLED:               ; JUMP PLAY OVER BUT PLAYER NOT OUT OF BOUNDS *** don't need jump **** 

PLAYER_OUT_OF_BOUNDS:                          ; PLAYER OUT OF BOUNDS
   SET_CLOCK_TO_STOPPED 
   SET_BALL_STATUS_SPECIAL_BALL_OOB[]  
   SET_PLAY_STATUS_PLAY_OVER[]
   
PLAY_OVER_PLAYER_TACKLED:                      ; PLAY OVER BUT PLAYER NOT OUT OF BOUNDS
   PLAY_SOUND_ID WHISTLE_SOUND 
   RETURN_IN_NUM_FRAMES 30
   SEC                                         ; INIDICATE PLAY OVER
   RTS                                         ;
PLAY_NOT_OVER:                                 ; PLAY CONTINUES
   CLC                                         ; INIDICATE PLAY NOT OVER
   RTS                                         ;

_F}_CHECK_FOR_PLAY_OVER 
  
KICK_BLOCK_RECOVERY:                           ; KICK BLOCK ROUTINE *** WASTE ***
   JMP FUMBLE_RECOVERY_START                   ; JUMP ->FUMBLE RECOVER ROUTINE START

_F{_CHECK_FOR_FUMBLES_TOSS_AND_NORMAL 			; DONE        
   
CHECK_FOR_FUMBLED_SNAP_OR_TOSS:                ; CHECK FOR FUMBLE IF PLAY IS  SHOTGUN SNAP/ RUN TOSS ()

   IF_TOSS_SHOTGUN_SNAP_NOT_IN_PROGRESS[ADDR] CHECK_FUMBLE_SNAP_EXIT
  
   IF_BALL_HAS_NOT_COLLIDED[ADDR] CHECK_FUMBLE_SNAP_EXIT
   RETURN_IN_NUM_FRAMES 8
   
   IF_BALL_NOT_MOVING[ADDR] CHECK_FUMBLE_SNAP_EXIT
   
   IF_PLAY_OVER[ADDR] CHECK_FUMBLE_SNAP_EXIT
   
   SET_BALL_STATUS_SPECIAL_BALL_SNAP_TOSS_DONE[]
   SET_P1_AND_P2_MAN_NOT_BALL_CARRIER[]
   JMP BALL_FUMBLED_START                      ; JUMP->BALL FUMBLED
   
CHECK_FUMBLE_SNAP_EXIT:                        ; EXIT
   RTS                                         ; RETURN

CHECK_IF_BALL_FUMBLED:                         ; CHECK FOR FUMBLE
   IF_BALL_FUMBLED[ADDR] BALL_FUMBLED_START

@else_ball_not_fumbled:   
   CLC                                         ; CLC =  BALL NOT FUMBLED
   RTS                                         ; RETURN
   
_F}_CHECK_FOR_FUMBLES_TOSS_AND_NORMAL  

_F{_ONSIDE_AND_FUMBLE_RECOVERY_LOGIC			; DONE

BALL_FUMBLED_START:                            ; BALL FUMBLED
   PLAY_SOUND_ID FUMBLE_SOUND
   CLEAR_CLOCK_SPEED_PUNT
   SET_SPRITE_OFFSCREEN PASS_TGT_INDICATOR_SPR_Y
   SET_PLAY_STATUS_MAN_CANT_PASS[]
   LDA #FUMBLE_BANNER_ID                       ;
   JSR ADD_POSSESSION_INFO_TO_BANNER_ID_FUM_KB                              

init_fumble_x_loc = LOCAL_3	
ball_oob_location = LOCAL_5

FUMBLE_RECOVERY_START:                         ; FUMBLE RECOVER ROUTINE START
   LDA POSSESSION_STATUS                       ; SET TEMP POSSESION STATUS = POSESSION STATUS
   STA FUM_LAST_TEAM_POSS                      ;
   BMI @p2_fumbled                             ; IS CURRENT TEAM WITH BALL = P2 ? YES->PLAYER 2 FUMBLED
                                               ; PLAYER 1 FUMBLED
@p1_fumbled: 											   
   RETURN_1_FRAME 
   SET_ADDR_XY[addr] OFF_RECOVER_BALL_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON OFFENSE(X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] DEF_RECOVER_BALL_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS)
   JMP @save_fumble_location                   ; JUMP->SAVE FUMBLED BALL STARTING LOCATION

@p2_fumbled:                                   ; PLAYER 2 FUMBLED
   RETURN_1_FRAME 
   SET_ADDR_XY[addr] OFF_RECOVER_BALL_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS) 
   
   SET_ADDR_XY[addr] DEF_RECOVER_BALL_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON OFFENSE(X,Y= PLAY CODE ADDRESS)

@save_fumble_location:                         ; SAVE FUMBLED BALL STARTING LOCATION
   LDA BALL_X+1	                               ; SAVE BALL LOCATION(X) in $40/$41 
   STA init_fumble_x_loc                       ;
   LDA BALL_X+2	                               ;
   STA init_fumble_x_loc+1                     ;
   
@wait_for_ball_recovered:                      ; WAIT FOR BALL RECOVERED LOOP
   _WHILE PLUS
   
   RETURN_1_FRAME 
   LDA BALL_STATUS                             ; DOES BALL STATUS = FUMBLE RECOVERED
   AND #$0C                                    ; 
   BNE @fumble_recovered						; YES-> FUMBLE RECOVERED
   LDA PLAY_STATUS                             ; DOES PLAY STATUS = PLAY OVER?
   _END_WHILE

@ball_fumbled_oob:   						   ; FUMBLE WENT OOB
   LDA BALL_X+1	                               ; SAVE BALL LOCATION IN TEMP LOCATION
   STA ball_oob_location                       ;
   LDA BALL_X+2	                               ;
   STA ball_oob_location+1                     ;
   LDA #OUT_OF_BOUNDS_ONSCR_BANNER_ID          ; SET BANNER ID =  BALL OUT OF BOUNDS SCROLL BANNER
   JSR ADD_POSSESSION_INFO_TO_BANNER_ID_FUM_KB ; DO BALL FUMBLED OR BALL OOB BANNER()
   RETURN_IN_NUM_FRAMES 60
   JSR END_GAME_FIELD_BALL_MOVE_TASKS          ; END GAME FIELD & BALL MOVEMENT TASKS & CLEAR CUTSCENE()
   
@check_where_to_spot_fumble:   
   LDY ball_oob_location                       ;  
   LDX ball_oob_location +1                    ; 

 @check_init_fumble_loc_vs_current_ball:   
   LDA init_fumble_x_loc                       ; IF BALL FUMBLED FORWARD OUT OF BOUNDS IT GOES BACK TO ORIGINAL BALL LOCATION
   CMP ball_oob_location                       ;
   LDA init_fumble_x_loc +1                    ;
   SBC ball_oob_location +1                    ;
   ROR                                         ;
   ROR                                         ;
   EOR POSSESSION_STATUS                       ;
   ROL                                         ;
   BMI @save_fumble_spot_location              ; LOAD NEW LOCATION
 
@set_location_to_init_fumble: 
   LDY init_fumble_x_loc                       ; LOAD ORIGINAL BALL PLAYER X LOCATION
   LDX init_fumble_x_loc +1                    ;

@save_fumble_spot_location:                    ; SAVE BALL LOCATION AND RETURN
   STY BALL_X+1	                               ; 
   STX BALL_X+2	                               ;
   RTS                                         ; RETURN

@fumble_recovered:      						; FUMBLE RECOVERED
   SET_RAM_Y_INDEX[start_length_value] CAN_COLLIDE_STATUS_OFFENSE, SIZE_OF_COLLISION_STATUS, $FF
   
   
  IF_P2_TOUCHED_BALL_LAST[ADDR] P2_RECOVERED_FUMBLE
  

_F{_P1_RECOVERS_FUMBLE							; DONE

P1_RECOVERED_FUMBLE:                             ; P1 RECOVERS FUMBLE
   LDA #RECOVER_P1_NAME_BANNER_ID + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                   
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA POSSESSION_STATUS                       ; DOES POSSESSION STATUS = P2 HAD BALL
   BMI @p1_recovers_opp_fumble                  ; YES->PLAYER 1 RECOVERS P2 FUMBLE/ONSIDE
   
@p1_recovers_own_fumble:   
   SET_ADDR_XY[addr] OFF_RECOVERS_OWN_FUM_PLAY_PTRS[]                     
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR            ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON DEFENSE(X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] FUM_RET_DEF_PLAY_PTRS[]     
   JMP @end_of_p1_recovers_fumble                ; JUMP->END OF P1 FUMBLE RECOVERY

@p1_recovers_opp_fumble:                       ; P1 RECOVERS P2 FUMBLE
   SET_ADDR_XY[addr] DEF_RET_LOST_FUM_PLAY_PTRS[]
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR            ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON DEFENSE(X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] OFF_DEFENDS_LOST_FUM_PLAY_PTRS[]


@end_of_p1_recovers_fumble:                    ; END OF P1 RECOVERS FUMBLE
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P2 PLAY CODE ADDRESSES OF NON-MAN CONTROL PLAYERS ONLY (X,Y= PLAY CODE ADDRESS) 
   JSR SET_BALL_STATUS_NOT_FUMBLED_AFTER_RECOVERY                             
   LDA #P1_ON_FIELD_SONG_SIDE                   ; SET SONG SIDE = P1
   JSR SET_ON_FIELD_SONG                        ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   IF_P2_FUMBLED_BALL[ADDR] P1_CHECK_FOR_TOUCHBACK_RECOVER_OPP_FUMBLE
 

@set_fumble_play_over:    
   SEC                                         ; SEC =  BALL NOT FUMBLED
   RTS                                         ; RETURN

P1_CHECK_FOR_TOUCHBACK_RECOVER_OPP_FUMBLE:    ; CHECK FOR P1 TOUCHBACK ON FUMBLE
   PLA                                         ; REMOVE RETURN ADDR
   PLA                                         ;
   JSR P1_CHECK_FOR_TOUCHBACK                  ; CHECK FOR P1 TOUCHBACK()
   BCC @update_scroll_limits                   ; BALL IN ENDZONE FOR TOUCHBACK?  NO->P1 RETURNS P2 FUMBLE
 
@p1_fumble_touchback: 
   JMP P2_TO_P1_POSS_CHANGE_PLAY_SELECT       ; YES->JUMP TO P2 to P1 POSSESION CHANGE TO P1 PLAY SELECT

@update_scroll_limits:                         ; P1 RETURNS P2 FUMBLE
   JMP UPDATE_SCROLL_LIMITS_P2_TURNOVER        ; JUMP->UPDATE SCROLL THRESHOLDS AFTER P1 GETS TURNOVER
   
_F}_P1_RECOVERS_FUMBLE


_F{_P2_RECOVERS_FUMBLE							; DONE

P2_RECOVERED_FUMBLE:                           ; P2 RECOVERED FUMBLE
   LDA #RECOVER_P2_NAME_BANNER_ID  + SCROLL_BANNER_IN_FROM_LEFT_BIT_FLAG                                  
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   LDA POSSESSION_STATUS                       ; DOES POSSESSION STATUS = P1 HAD BALL
   BPL @p2_recovers_opp_fumble                 ; YES-> P2 RECOVERS P1 FUMBLE 

@p2_recovers_own_fumble:  

   SET_ADDR_XY[addr] OFF_RECOVERS_OWN_FUM_PLAY_PTRS[] 
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR            ; UPDATE ALL P2 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] FUM_RET_DEF_PLAY_PTRS[]
   JMP @end_of_p2_recovers_fumble              ; JUMP-> END OF P2 FUMBLE RECOVERY  

@p2_recovers_opp_fumble:                       ; P2 RECOVERS P1 FUMBLE 
   SET_ADDR_XY[addr] DEF_RET_LOST_FUM_PLAY_PTRS[]
   JSR UPDATE_ALL_P2_PLAY_CODE_ADRR            ; UPDATE ALL P2 PLAYERS AND ACTIVE MAN PLAYER(X,Y= PLAY CODE ADDRESS)
   
   SET_ADDR_XY[addr] OFF_DEFENDS_LOST_FUM_PLAY_PTRS[]

@end_of_p2_recovers_fumble:                    ; END OF P2 FUMBLE RECOVERY
   JSR UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN ; UPDATE P1 ACTIVE PLAYER PLAY CODE ADDRESS PLAYER ON OFFENSE(X,Y= PLAY CODE ADDRESS)
   JSR SET_BALL_STATUS_NOT_FUMBLED_AFTER_RECOVERY                               
   LDA #P2_ON_FIELD_SONG_SIDE                  ; SET SONG SIDE = P2 SIDE
   JSR SET_ON_FIELD_SONG                       ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   IF_P1_FUMBLED_BALL[ADDR] P2_CHECK_FOR_TOUCHBACK_RECOVER_OPP_FUMBLE 

 @set_fumble_play_over:   
   SEC                                         ;
   RTS                                         ; RETURN

P2_CHECK_FOR_TOUCHBACK_RECOVER_OPP_FUMBLE:     ; CHECK FOR P2 TOUCHBACK ON FUMBLE
   PLA                                         ;
   PLA                                         ;
   JSR P2_CHECK_FOR_TOUCHBACK                  ; CHECK FOR P2 TOUCHBACK()
   BCC @update_scroll_limits                   ; BALL IN ENDZONE FOR TOUCHBACK?  NO->P2 RETURNS P1 FUMBLE

@p2_fumble_touchback:    
   JMP P1_TO_P2_POSSESSION_CHANGE_P2_PLAY_SELECT 
   
@update_scroll_limits:                         ; 
   JMP UPDATE_SCROLL_LIMITS_P1_TURNOVER        ; JUMP->UPDATE SCROLL THRESHOLDS AFTER P2 GETS TURNOVER
   
_F}_P2_RECOVERS_FUMBLE   
   
_F}_ONSIDE_AND_FUMBLE_RECOVERY_LOGIC

_F{_MISC_FUMBLE_FUNCTIONS						; DONE

 temp_banner_id = LOCAL_8  
 
ADD_POSSESSION_INFO_TO_BANNER_ID_FUM_KB:       ; SHOW BALL FUMBLED OR KICK BLOCK BANNER()
   STA temp_banner_id                          ; SET PROPER BANNER CODE
   LDA POSSESSION_STATUS                       ;
   EOR #$FF                                    ;
   AND #$40                                    ;
   ASL                                         ;
   ORA temp_banner_id                          ;
   JMP UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)

SET_BALL_STATUS_NOT_FUMBLED_AFTER_RECOVERY:                                  
   SET_BALL_NOT_FUMBLED
   RETURN_1_FRAME 
   RTS                                         ; RETURN

 _F}_MISC_FUMBLE_FUNCTIONS  


_F{_CHECK_FOR_QTR_OVER							; DONE

CHECK_FOR_QTR_OVER:                            ; CHECK FOR QUARTER OVER RESET BIT & SUSPEND SWITCH TASK IF OVER()
   IF_GAME_STATUS_END_OF_QTR[ADDR] QTR_OVER_SUSPEND_TASK
   RTS                                         ; RETURN

QTR_OVER_SUSPEND_TASK:                         ; RESET QUARTER OVER BIT AND SUSPEND CURRENT TASK
   SET_GAME_STATUS_QTR_NOT_OVER[]
   JSR SUSPEND_TASK                            ; SUSPEND CURRENT TASK()
   RTS                                         ; RETURN

_F}_CHECK_FOR_QTR_OVER   

_F{_CLEAR_VARIABLES_FOR_XP_KICKOFF             ; DONE

addr_low = LOCAL_7 

SET_TO_1st_DOWN_CLEAR_RAM_VARS_SPRITES:
   LDA #$00                                    ; SET TO FIRST DOWN, CLEAR VALUES, CLEAR SPRITE DATA AND GRAPHICS
   STA DOWN                                    ;
CLEAR_RAM_VARS_SPRITES:                        ; CLEAR PLAY STATUS, BALL INFO BYTES,PASS TIMER, PASS TARGETS, AND SPRITES() 
   LDA #$00                                    ; CLEAR PLAY STATUS & BALL INFO BYTES
   STA PLAY_STATUS                             ;
   STA BALL_STATUS                             ;
   STA BALL_STATUS_SPECIAL                     ;
   STA BALL_COLLISION                          ;
   STA UNUSED_KICK_MEMORY                      ; CLEAR KICKING BYTE
   STA PASS_TIMER_DIV_BY_4	                   ; CLEAR PASS TIMER
   STA TACKLER_ID                              ; CLEAR DEFENDER ID
   SET_RAM_Y_INDEX[start_length_value] PASS_TARGET_INFO, $06, $00
   JSR SET_ALL_SPR_OFFSCREEN                   ; CLEAR SPRITE GRAPHICS()
                                               ; CLEAR PLAYER RAM
   LDA #<SIZE_OF_ALL_PLAYER_RAM                ; SET LENGTH = 0x02C0
   LDX #>SIZE_OF_ALL_PLAYER_RAM                ; 
   LDY #<PLAYER_RAM_START                      ; SET RAM START ADDRESS = 0x039F
   STY addr_low                                ;
   LDY #>PLAYER_RAM_START                      ;
   JSR CLEAR_RAM                               ; CLEAR RAM (A,X= LENGTH, $44,Y= LOCATION START) 
   SAVE_ADDR16_TO_ZP_VAR[addr_zp] P1_RAM_FIRST_PLAYER, P1_MAN_PLAYER_ADDR
   SAVE_ADDR16_TO_ZP_VAR[addr_zp] P2_RAM_FIRST_PLAYER, P2_MAN_PLAYER_ADDR
   LDA #INIT_ICON_BITFLAG                      ; SET P1 & P2 PLAYER ICON = NOT SET
   STA P1_MAN_POS_ID_ICON_STATUS               ;
   STA P2_MAN_POS_ID_ICON_STATUS               ;
   RTS                                         ; RETURN

 _F}_CLEAR_VARIABLES_FOR_XP_KICKOFF  
  
_F{_END_SPECIFIC_TASKS                         ; DONE
  
WAIT_UNTIL_BANNER_DONE_FADE_OUT_PAL:           ; CHECK FOR BANNER DONE THEN KILL TASKS AND FADE PALLETE()
   JSR END_GAME_FIELD_BALL_MOVE_TASKS          ; END GAME FIELD & BALL MOVEMENT TASKS & CLEAR CUTSCENE() 
   LDA BANNER_ID                               ; ANY BANNER TO DISPLAY?
   BEQ @fade_out                                ; NO->FADE TO BLACK
   LDA BG_FADE                                 ; DOES BG PALLETE FADE VALUE = FULLY FADE OUT?
   BEQ END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT   ; YES->END ALL TASKS BUT CLOCK AND DRAWING TASK & SET CUTSCENE TO PLAY = NONE()
   RETURN_IN_NUM_FRAMES 20  

@fade_out:                                        ; FADE TO BLACK
   JSR FADE_OUT_BG_SPR_PAL                     ; FADE OUT PALLETTE()

END_ALL_TASKS_BUT_CLOCK_AND_DRAW_CLEAR_CUT:    ; END ALL TASKS BUT CLOCK AND DRAWING TASK & SET CUTSCENE TO PLAY = NONE()
   LDX #COLLISION_CHECK_TASK                   ; LOAD CHECK FOR COLLISSION TASK INDEX = TASK 5
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK TO END)
   LDX #UPDATE_PLAYERS_TASK                    ; LOAD UPDATE PLAYERS TASK INDEX = TASK 6
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK TO END)
   LDX #DRAW_PLAYERS_TASK                      ; LOAD DRAW PLAYERS TASK INDEX = TASK 7
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK TO END)

 END_GAME_FIELD_BALL_MOVE_TASKS:               ; END GAME FIELD &BALL MOVEMENT TASKS & CLEAR CUTSCENE() 
   LDX #GAMEFIELD_TASK                         ; LOAD UPDATE GAME FIELD TASK INDEX = TASK 2
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK TO END)
   LDX #BALL_MOVEMENT_TASK                     ; LOAD BALL MOVEMENT TASK INDEX = TASK 3
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK TO END)
   LDA #$00                                    ; SET CUTSCENE PLAYING TO NONE
   STA CUTSCENE_TO_DRAW                        ;
   RTS                                         ; RETURN
   
_F}_END_SPECIFIC_TASKS   
   
_F{_CHECK_FOR_UPDATE_BANNER                    ; DONE

UPDATE_BANNER:                                 ; UPDATE TOP BANNER()
   TAX                                         ; LOAD BANNER ID
@banner_loop:                                  ; WAIT TILL PREVIOUS BANNER CLEAR LOOP
   LDA BANNER_ID                                     ; ANY BANNER TO DISPLAY?
   BEQ @exit                                   ; NO-> SAVE BANNER TO DRAW
   RETURN_1_FRAME 
   JMP @banner_loop                            ; JUMP ->WAIT TILL PREVIOUS BANNER CLEAR LOOP
@exit:                                         ; SAVE BANNER TO DRAW
   STX BANNER_ID                                     ; SAVE BANNER CODE IN CURRENT BANNER
   RTS                                         ; ETURN

_F}_CHECK_FOR_UPDATE_BANNER   
   
_F{_UPDATE_SCORE_FUNCTIONS						; DONE

qtr_score_index = LOCAL_6
overtime_qtr		= $04

ADD_PTS_TO_P1_QTR_TOT:                         ; ADD POINTS TO HOME SCORE(A=PTS)
   PHA                                         ; SAVE POINTS TO ADD
   LDY QUARTER                                 ; LOAD QUARTER
   CPY #overtime_qtr                                    ; IS OT? 
   BCC @add_pts_to_p1_qtr_pts                  ; NO-> ADD POINTS TO HOME CURRENT QUARTER
   LDY #$00                                    ; ELSE SET QUARTER = 1st QUARTER
@add_pts_to_p1_qtr_pts:                               ; ADD POINTS TO CURRENT P1 QUARTER PTS
   STY qtr_score_index                         ; SAVE QTR INDEX IN $43
   LDX P1_QTR_SCORES,Y                         ; LOAD P1 CURRENT SCORE FOR QTR
   JSR ADD_AMOUNT_TO_TOTAL_DECIMAL             ; ADD SCORE TO QTR SCORE DECIMAL FORMAT(A=SCORE TO ADD, X= CURRENT SCORE)
   LDY qtr_score_index                         ; SAVE NEW SCORE IN CURRENT QTR
   STA P1_QTR_SCORES,Y                         ; 
   PLA                                         ; LOAD POINTS TO ADD
   LDX P1_TOTAL_SCORE                          ; LOAD HOME TOTAL POINTS
   JSR ADD_AMOUNT_TO_TOTAL_DECIMAL             ; ADD SCORE TO QTR SCORE DECIMAL FORMAT(A=SCORE TO ADD, X= CURRENT SCORE)
   STA P1_TOTAL_SCORE                          ; SAVE HOME TOTAL POINTS
   RTS                                         ; ETURN

ADD_PTS_TO_P2_QTR_TOT:                         ; ADD POINTS TO P2 SCORE(A=PTS) 
   PHA                                         ; SAVE POINTS TO ADD
   LDY QUARTER                                 ; IS QUARTER = OT?
   CPY #overtime_qtr                           ; 
   BCC @add_pts_to_p2_qtr_pts                  ; NO->ADD POINTS TO CURRENT QUARTER
   LDY #$00                                    ; ELSE SET QUARTER = 1st QUARTER
@add_pts_to_p2_qtr_pts:                        ; ADD POINTS TO CURRENT P2 QUARTER PTS
   STY qtr_score_index                         ; SAVE QTR INDEX IN $43
   LDX P2_QTR_SCORES,Y                         ; LOAD P2 CURRENT SCORE FOR QTR
   JSR ADD_AMOUNT_TO_TOTAL_DECIMAL             ; ADD SCORE TO QTR SCORE DECIMAL FORMAT(A=SCORE TO ADD, X= CURRENT SCORE)
   LDY qtr_score_index                         ; SAVE NEW SCORE IN CURRENT QTR
   STA P2_QTR_SCORES,Y                         ;
   PLA                                         ; LOAD POINTS TO ADD
   LDX P2_TOTAL_SCORE                          ; LOAD AWAY TOTAL POINTS
   JSR ADD_AMOUNT_TO_TOTAL_DECIMAL             ; ADD SCORE TO QTR SCORE DECIMAL FORMAT(A=SCORE TO ADD, X= CURRENT SCORE)
   STA P2_TOTAL_SCORE                          ; SAVE AWAY TOTAL POINTS
   RTS                                         ; ETURN


ones_place_value	= LOCAL_7   
tens_place_value    = LOCAL_8
tens_place_bitmask	= $F0
ones_place_bitmask	= $0F					   ; *** no need for tens place
max_score			= $99
	
ADD_AMOUNT_TO_TOTAL_DECIMAL:                   ; ADD SCORE TO QTR SCORE DECIMAL FORMAT(A=SCORE TO ADD, X= CURRENT SCORE)
   TAY                                         ; SAVE SCORE to ADD IN Y 
@save_tens_place_value:   
   AND #tens_place_bitmask                     ; SAVE 10’S PLACE VALUE IN $45
   STA tens_place_value                        ;

@save_ones_place_value:    
   TYA                                         ; SAVE 1’S PLACE VALUE IN $44
   AND #ones_place_bitmask                     ;
   STA ones_place_value                        ;
   TXA                                         ; LOAD CURRENT QTR SCORE
   AND #ones_place_bitmask                     ; =1’S PLACE DIGIT = 1’S PLACE CURRENT VALUE + 1’S PLACE NEW VALUE 
   CLC                                         ;
   ADC ones_place_value                        ;
   STA ones_place_value                        ; 
   CMP #$0A                                    ; IF VALUE GREATER THAN 10 ADD TO 10’S PLACE
   BCC @Loop1                                  ;
   SBC #$0A                                    ; AND SUBTRACT 10 FROM 10’S PLACE DIGIT
   STA ones_place_value                        ; SAVE 1’S PLACE VALUE
   LDA tens_place_value                        ; 10’S PLACE DIGIT = CURRENT VALUE +10
   CLC                                         ;
   ADC #$10                                    ; 
   STA tens_place_value                        ;
@Loop1:                                        ; 
   TXA                                         ; LOAD CURRENT QTR SCORE 
   AND #tens_place_bitmask                     ; MASK OUT ONES PLACE
   CLC                                         ;
   ADC tens_place_value                        ; 10’S PLACE DIGIT = 10’S PLACE CURRENT VALUE + 10’S PLACE NEW VALUE 
   ORA ones_place_value                        ; COMBINE DIGITS TOGETHER TO GET FULL NUMBER
   
@check_score_greater_than_99:  
   CMP #max_score +1                           ;
   BCC @exit                                   ;
   
@set_score_to_99:   
   LDA #max_score                              ;
@exit:                                         ; EXIT 
   RTS                                         ; RETURN

_F}_UPDATE_SCORE_FUNCTIONS

_F{_DRAW_RECOVER                               ;DONE

DRAW_RECOVER_TEXT:                             ; DRAW RECOVER TEXT()
   LDA #CLEAR_OFFSCR_BANNER_BANNER_ID          ; SET BANNER ID = CLEAR NAMETABLE 1 (OFFSCREEN) BANNER
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO)
   RETURN_IN_NUM_FRAMES 8
   LDA #RECOVER_MESSAGE_BAR_ID                 ; LOAD RECOVER SCENE INDEX 
   LDX #MESSAGE_BAR_DRAW_SCRIPT_BANK           ; LOAD BANK 16
   JSR DRAW_SCENE_RETURN_PREV_8000_BANK        ; DRAW SCENE (X=SCENE BANK, A=SCENE CODE)
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2          ; LOAD BANK 20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)     
   RETURN_IN_NUM_FRAMES 30
   LDA #CLEAR_ONSCR_BANNER_BANNER_ID           ; SET BANNER ID = CLEAR NAMETABLE 0 (ONSCREEN) BANNER
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO) 
   RETURN_1_FRAME 
   LDA #$00                                    ; UPDATE IRQ0 SPLIT XSCROLL TO ONSCREEN NAMETABLE
   STA IRQ_0_NAMETABLE                         ;
   RTS                                         ; RETURN

_F}_DRAW_RECOVER
 
_F{_SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK   ; DONE
 
SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK:    ; SET GAME STATUS BIT TO ON FIELD START UPDATE PLAYERS TASK()
   SET_GAME_STATUS_PLAYERS_ON_FIELD[]
   LDA #PLAYER_SCRIPT_COMMANDS_LOGIC_BANK_1    ; SET TASK 6 A000 BANK = BANK 20
   STA UPDATE_PLAYERS_TASK + TASK_BANK_A000    ; 
   LDY #<(SWAP_AND_INIT_PLAYER_RAM_COLLISION_UPDATE-1)                            
   LDA #>(SWAP_AND_INIT_PLAYER_RAM_COLLISION_UPDATE-1)                      
   LDX #UPDATE_PLAYERS_TASK                    ; SET TASK INDEX = TASK 6
   JSR CREATE_TASK                             ; CREATE TASK(Y,A= ADDRESS, X= TASK INDEX)
   RETURN_IN_NUM_FRAMES 2
   RTS                                         ; RETURN

_F}_SET_GAME_STATUS_ON_FIELD_START_PLAYER_TASK   

_F{_DEFENDER_CHANGE_BEFORE_HIKE                 ; DONE

player_ram_addr				= LOCAL_1
hike_delay_timer			= LOCAL_7 
team_control_temp 			= LOCAL_7 
current_defender 			= LOCAL_8   

hike_delay_frames			= $28					   ; 40 frames

 
CHECK_FOR_P2_HIKE_P1_PLAYER_CHANGE:            ; CHECK FOR P2 HIKE AND P1 DEFENDER PLAYER CHANGE()
   JSR CHECK_IF_CLOCK_SHOULD_RUN_NOT_IMPLEMENTED                               ; CHECK IF CLOCK SHOULD BE STARTED (NOT IMPLEMENTED)
CHECK_FOR_P2_HIKE_P1_PLAYER_CHANGE_XP:         ; CHECK FOR P2 HIKE AND P1 DEFENDER PLAYER CHANGE NO CLOCK CHECK()
   LDA #RE_POSITION_ID                         ; SET CURRENT DEFENDER SELECTED = FIRST DEFENDER (RE)
   STA current_defender                        ;
   LDA #RE_STARTER_ID + ICON_CHANGE_BITFLAG       ; SET CURRENT P1 ICON = RE1 + UPDATE ICON
   STA P1_MAN_POS_ID_ICON_STATUS               ; 
   LDA #<P1_RAM_QB_RE                          ; SET P1 ACTIVE PLAYER POINTER to FIRST PLAYER = 0x039F
   STA P1_MAN_PLAYER_ADDR                      ;
   LDA #>P1_RAM_QB_RE                          ;
   STA P1_MAN_PLAYER_ADDR+1                    ;
   LDA #$00                                    ; CLEAR TEMP VARIABLE $44
   STA team_control_temp                       ; 
   LDA_ABS_A_ZP TEAM_CONTROL_TYPES             ; LOAD PLAYER TYPE *** USES NON ZERPAGE LOAD INSTRUCTION CAN FIX
   CMP #$10                                    ; IS PLAYER 1 COA/COM/SKP  
   ROL team_control_temp                       ; SAVE IN $44 
   AND #$0F                                    ; IS PLAYER 2 COA/COM/SKP
   CMP #$01                                    ; 
   ROL team_control_temp                       ; SAVE IN $44
   RTS_JUMP_TABLE[memVal_tableAddr] team_control_temp, P2_HIKE_P1_DEFENDER_COMMAND_TABLE 
 
                        
P2_HIKE_P1_DEFENDER_COMMAND_TABLE:             ; P2 HIKE P1 DEFENDER PLAYER CHANGE CHECK SERVICE TABLE

.DW P2_MAN_HIKE_P1_MAN_DEF-1                  ; PLAYER 2 MAN, PLAYER 1 MAN
.DW P2_COM_HIKE_P1_MAN_DEF-1                  ; PLAYER 2 COM, PLAYER 1 MAN
.DW P2_MAN_HIKE_P1_COM_DEF-1                  ; PLAYER 2 MAN, PLAYER 1 COM 
.DW COM_VS_COM_SNAP_DELAY-1                    ; PLAYER 1 COM, PLAYER 2 COM

P2_MAN_HIKE_P1_MAN_DEF:                       ; PLAYER 1 MAN, PLAYER 2 MAN
   LDA #hike_delay_frames                      ; SET HIKE DELAY TIMER AFTER PLAYER CHANGE = 0.66 SEC
   STA hike_delay_timer                        ; 
@check_defender_change:                        ; CHECK for P2 HIKE AND P1 DEFENDER SWITCH LOOP
   RETURN_1_FRAME 
   JSR UPDATE_P1_DEFENDER_BEFORE_HIKE          ; UPDATE DEFENDER ON BUTTON PRESS
   DEC hike_delay_timer                        ; IS HIKE DELAY TIMER AFTER PLAYER CHANGE DONE?
   BNE @check_defender_change                  ; NO ->CHECK for P2 HIKE AND P1 DEFENDER SWITCH LOOP 
   INC hike_delay_timer                        ;
@check_a_press_for_hike:   
   LDA JOY_PRESS_2                             ; LOAD P2 PRESS
   BPL @check_defender_change                  ; A PRESSED? NO-> CHECK for P2 HIKE AND P1 DEFENDER SWITCH LOOP 

@ball_snapped: 
   JMP P2_BALL_SNAPPED                         ; *** could use BMI *** YES->P2 BALL SNAPPED

P2_COM_HIKE_P1_MAN_DEF:                       ; PLAYER 1 MAN D, PLAYER 2 COM O
   LDA #hike_delay_frames                      ; SET HIKE DELAY TIMER AFTER PLAYER CHANGE = 0.66 SEC
   STA hike_delay_timer                        ; 
@check_defender_change:
   RETURN_1_FRAME 
   JSR UPDATE_P1_DEFENDER_BEFORE_HIKE          ; UPDATE DEFENDER ON BUTTON PRESS()
   DEC hike_delay_timer                       ; 
   BNE @check_defender_change
   
P2_BALL_SNAPPED:                               ; P2 BALL SNAPPED
   LDY #PLAY_CODE_ADDR                         ; SET ACTIVE PLAYER PLAY CODE ADDRESS TO 8018 Actual rom addres 0x28028    
   LDA #<$8018                                 ; COMMAND at 28028 = E4 player take control *** could change to areea in play code ***
   STA (P1_MAN_PLAYER_ADDR ),Y                 ; 
   INY                                         ; 
   LDA #>$8018                                 ;      
   STA (P1_MAN_PLAYER_ADDR ),Y                 ;
   LDY #COMMAND_COUNTER                        ; SET PLAYER COMMAND COUNTER TO 1 FRAME
   LDA #$01                                    ; 
   STA (P1_MAN_PLAYER_ADDR ),Y                 ; 
   INY                                         ; SET PLAY COMMAND ADDRESS to $8000-1 = PLAYER SCRIPT PROCESSING START
   LDA #<$8000-1                               ; 
   STA (P1_MAN_PLAYER_ADDR ),Y                 ;
   INY                                         ;
   LDA #>$8000-1                               ;
   STA (P1_MAN_PLAYER_ADDR ),Y                 ;
   JSR SET_BALL_SNAPPED_START_CLOCK_EXCEPT_XP  ; SET BALL STATUS TO SNAPPED AND START CLOCK IF NOT XP()
   RTS                                         ;

P2_MAN_HIKE_P1_COM_DEF:                        ; PLAYER 2 MAN O, PLAYER 2 COM D
   RETURN_1_FRAME 
   LDA JOY_PRESS_2                             ; LOAD P2 BUTTON PRESS
   BPL P2_MAN_HIKE_P1_COM_DEF                  ; IS A PRESSED? NO-> PLAYER 1 MAN D, PLAYER 2 COM O
   JSR SET_BALL_SNAPPED_START_CLOCK_EXCEPT_XP  ; SET BALL STATUS TO SNAPPED AND START CLOCK IF NOT XP()
   RTS                                         ; RETURN

UPDATE_P1_DEFENDER_BEFORE_HIKE:                ; UPDATE P1 DEFENDER ON BUTTON PRESS()
   BIT JOY_PRESS_1                             ; IS P1 BUTTON PRESS = "A" BUTTON PRESSED?  
   BMI @a_press_go_to_next_defender            ; YES->  P1 “A" BUTTON PRESSED UPDATE DEFENDER
   BVC @exit                                    ; NO BUTTONS PRESSED->  EXIT
 
@b_press_go_to_prev_defender:				   ;  
   DEC current_defender                        ; "B" PRESSED-> CURRENT P1 DEFENDER--
   BPL @save_icon_value_update_man_player_addr ; CURRENT DEFENDER BETWEEN 0x00 to 0x0A? YES-> SAVE ICON VALUE AND UPDATE ACTIVE PLAYER POINTER
   LDA #SS_POSITION_ID                         ; NO-> SET CURRENT P1 DEFENDER = STRONG SAFETY 
   STA current_defender                        ; 
   JMP @save_icon_value_update_man_player_addr ; JUMP->SAVE ICON VALUE AND UPDATE ACTIVE PLAYER POINTER
   
@a_press_go_to_next_defender:                  ; P1 “A" BUTTON PRESSED UPDATE DEFENDER
   INC current_defender                        ; CURRENT P1 DEFENDER ++
   LDA current_defender                        ; IS CURRENT P1 DEFENDER > STRONG SAFETY
   CMP #SS_POSITION_ID + 1                      ; 
   BCC @save_icon_value_update_man_player_addr ; NO->  SAVE ICON VALUE AND UPDATE ACTIVE PLAYER POINTER
   LDA #RE_POSITION_ID                         ; ELSE SET CURRENT P1 DEFENDER = FIRST P1 DEFENDER = RE
   STA current_defender                        ; SAVE IN CURRENT P1 DEFENDER 

@save_icon_value_update_man_player_addr:       ; SAVE ICON VALUE AND UPDATE ACTIVE PLAYER POINTER
   LDA current_defender                        ; LOAD CURRENT P1 DEFENDER
   CLC                                         ;
   ADC #RE_STARTER_ID + ICON_CHANGE_BITFLAG       ; ADD MASK VALUE TO GET CORRECT PLAYER POINTER
   STA P1_MAN_POS_ID_ICON_STATUS               ; SAVE IN CURRENT P1 ICON
   LDA current_defender                        ; LOAD CURRENT P1 DEFENDER
   JSR GET_P1_P2_RAM_ADDR                      ; GET PLAYER POINTER(A=player ID) AND SAVE IN 3E 3F()
   LDA player_ram_addr	                       ; SAVE PLAYER POINTER IN  P1 ACTIVE PLAYER POINTER
   STA P1_MAN_PLAYER_ADDR                      ;
   LDA player_ram_addr +1	                   ;
   STA P1_MAN_PLAYER_ADDR +1                   ;
   LDA #hike_delay_frames                      ; SET HIKE DELAY TIMER  = 28 FRAMES = 0.66 SEC 
   STA hike_delay_timer                        ;
@exit:                                         ; EXIT
   RTS                                         ; RETURN 

  


   
CHECK_P1_HIKE_P2_PLAYER_CHANGE:                ; CHECK FOR P1 HIKE AND P2 DEFENDER PLAYER CHANGE()
   JSR CHECK_IF_CLOCK_SHOULD_RUN_NOT_IMPLEMENTED                               ; CHECK IF CLOCK SHOULD BE STARTED (NOT IMPLEMENTED)
CHECK_P1_HIKE_P2_DEF_CHANGE_NO_CLOCK:          ; CHECK FOR P1 HIKE AND P@ DEFENDER PLAYER CHANGE NO CLOCK CHECK()
   LDA #RE_POSITION_ID + P2_SIDE_BITFLAG       ; SET CURRENT DEFENDER SELECTED = FIRST P2 DEFENDER (RE)
   STA current_defender                        ;
   LDA #RE_STARTER_ID + ICON_CHANGE_BITFLAG       ; SET CURRENT P2 ICON = CHANGING
   STA P2_MAN_POS_ID_ICON_STATUS               ;
   LDA #<P2_RAM_QB_RE                          ; SET P2 ACTIVE PLAYER POINTER = FIRST P2 PLAYER ADDR = 0x04FF
   STA P2_MAN_PLAYER_ADDR                      ;
   LDA #>P2_RAM_QB_RE                          ;
   STA P2_MAN_PLAYER_ADDR + 1                  ;
   LDA #$00                                    ; CLEAR  VARIABLE 
   STA team_control_temp                       ;
   LDA_ABS_A_ZP TEAM_CONTROL_TYPES             ; *** uneeded absolute load,  IS P1 PLAYER TYPE = COA/COM/SKP
   CMP #$10                                    ; 
   ROL team_control_temp                       ; 
   AND #$0F                                    ; IS P2 PLAYER TYPE = COA/COM/SKP
   CMP #$01                                    ;
   ROL team_control_temp                       ; 
   RTS_JUMP_TABLE[memVal_tableAddr] team_control_temp, P1_HIKE_P2_DEFENDER_COMMAND_TABLE 
 
    
P1_HIKE_P2_DEFENDER_COMMAND_TABLE:             ; P1 HIKE P2 DEFENDER PLAYER CHANGE CHECK SERVICE TABLE

.DW P1_MAN_HIKE_P2_MAN_DEF-1                   ; PLAYER 1 MAN, PLAYER 2 MAN
.DW P1_MAN_HIKE_P2_COM_DEF-1                   ; PLAYER 1 MAN, PLAYER 2 COM
.DW P1_COM_HIKE_P2_MAN_DEF-1                   ; PLAYER 1 COM, PLAYER 2 MAN
.DW COM_VS_COM_SNAP_DELAY-1                    ; PLAYER 1 COM, PLAYER 2 COM

P1_MAN_HIKE_P2_MAN_DEF:                        ; PLAYER 1 MAN O, PLAYER 2 MAN D
   LDA #hike_delay_frames                      ; SET HIKE DELAY TIMER AFTER PLAYER CHANGE = 0.66 SEC
   STA hike_delay_timer                        ; 
   
@check_defender_change:                        ; CHECK for P1 HIKE AND P2 DEFENDER SWITCH LOOP
   RETURN_1_FRAME      
   JSR UPDATE_P2_DEFENDER_ON_BUTTON_PRESS      ; UPDATE P2 DEFENDER ON BUTTON PRESS
   DEC hike_delay_timer                        ; HIKE DELAY TIMER-- IS TIMER DONE?
   BNE @check_defender_change                  ; NO->CHECK for P1 HIKE AND P1 DEFENDER SWITCH LOOP
   INC hike_delay_timer                        ;
   
@check_hike:
   LDA JOY_PRESS_1                             ; DOES P1 PRESS = A PRESSED?
   BPL @check_defender_change                  ; NO-> CHECK for P1 HIKE AND P2 DEFENDER SWITCH LOOP
   JMP P1_BALL_SNAPPED                         ; YES-> P2 BALL SNAPPED

P1_COM_HIKE_P2_MAN_DEF:                        ; PLAYER 1 COM O, PLAYER 2 MAN D
   LDA #hike_delay_frames                      ; SET HIKE DELAY TIMER AFTER PLAYER CHANGE = 0.66 SEC
   STA hike_delay_timer                        ;
@check_defender_change:                        ; CHECK FOR P2 MAN DEFENDER PLAYER CHANGE VS COM 
   RETURN_1_FRAME 
   JSR UPDATE_P2_DEFENDER_ON_BUTTON_PRESS      ; UPDATE P2 DEFENDER ON BUTTON PRESS() 
   DEC hike_delay_timer                        ; DECREMENT HIKE DELAY TIMER. IS TIMER DONE? 
   BNE @check_defender_change                  ; NO->CHECK FOR P2 MAN DEFENDER PLAYER CHANGE VS COM
   
   
P1_BALL_SNAPPED:                               ; P1 BALL SNAPPED
   LDY #PLAY_CODE_ADDR                         ; SET P1 ACTIVE PLAYER PLAY CODE ADDRESS TO 8018 Actual rom addres 0x28028 
   LDA #<$8018                                 ; COMMAND at 28028 = E4 player take control
   STA (P2_MAN_PLAYER_ADDR),Y                  ;
   INY                                         ;
   LDA #>$8018                                 ;
   STA (P2_MAN_PLAYER_ADDR),Y                  ;
   LDY #COMMAND_COUNTER                        ; SET PLAYER COMMAND COUNTER TO 1 FRAME
   LDA #$01                                    ;
   STA (P2_MAN_PLAYER_ADDR),Y                  ;
   INY                                         ; SET PLAY COMMAND ADDRESS to $8000-1 = PLAYER SCRIPT PROCESSING START
   LDA #<$8000-1                               ;
   STA (P2_MAN_PLAYER_ADDR),Y                  ;
   INY                                         ;
   LDA #>$8000-1                               ;
   STA (P2_MAN_PLAYER_ADDR),Y                  ;
   JSR SET_BALL_SNAPPED_START_CLOCK_EXCEPT_XP  ; SET BALL STATUS TO SNAPPED AND START CLOCK IF NOT XP()
   RTS                                         ; RETURN

P1_MAN_HIKE_P2_COM_DEF:                        ; PLAYER 1 MAN, PLAYER 2 COM
   RETURN_1_FRAME 
   LDA JOY_PRESS_1                             ; IS P1 BUTTON PRESS =  A PRESSED?
   BPL P1_MAN_HIKE_P2_COM_DEF                  ; NO-> PLAYER 1 MAN O, PLAYER 2 COM O
   JSR SET_BALL_SNAPPED_START_CLOCK_EXCEPT_XP  ; SET BALL STATUS TO SNAPPED AND START CLOCK IF NOT XP()
   RTS                                         ; RETURN 

UPDATE_P2_DEFENDER_ON_BUTTON_PRESS:            ; UPDATE P2 DEFENDER ON BUTTON PRESS()
   BIT JOY_PRESS_2							   ; DOES P2 BUTTON PRESS = "A" BUTTON PRESSED?  
   BMI @a_press_go_to_next_defender            ; YES->  P2 “A" BUTTON PRESSED UPDATE DEFNDER
   BVC @exit                                   ; NO BUTTONS PRESSED->  EXIT
   
@b_press_go_to_prev_defender:   
   DEC current_defender                        ; "B" PRESED-> CURRENT P2 DEFENDER --
   BMI @save_icon_value_update_man_player_addr ; CURRENT P2 DEFENDER  BETWEEN 0x00 to 0x0A? YES-> SAVE ICON VALUE AND UPDATE ACTIVE PLAYER POINTER
   LDA #SS_POSITION_ID +P2_SIDE_BITFLAG        ; SET CURRENT P2 DEFENDER = P2 STRONG SAFETY 
   STA current_defender                        ; 
   JMP @save_icon_value_update_man_player_addr ; JUMP ->SAVE ICON VALUE AND UPDATE P2 ACTIVE PLAYER POINTER
   
@a_press_go_to_next_defender:                  ; P2 “A" BUTTON PRESSED UPDATE DEFNDER
   INC current_defender                        ; INCREASE CURRENT P2 DEFENDER
   LDA current_defender                        ; IS CURRENT P2 DEFENDER > P2 STRONG SAFETY
   CMP #SS_POSITION_ID + P2_SIDE_BITFLAG +1    ;
   BCC @save_icon_value_update_man_player_addr ; NO->  SAVE ICON VALUE AND UPDATE ACTIVE PLAYER POINTER
   LDA #RE_POSITION_ID + P2_SIDE_BITFLAG       ; ELSE SET CURRENT P2 DEFENDER TO FIRST P2 DEFENDER
   STA current_defender                        ;
   
@save_icon_value_update_man_player_addr:       ; SAVE ICON VALUE AND UPDATE ACTIVE PLAYER POINTER
   LDA current_defender                        ; LOAD CURRENT P2 DEFENDER
   CLC                                         ;
   ADC #RE_STARTER_ID + ICON_CHANGE_BITFLAG       ; ADD MASK VALUE TO GET CORRECT PLAYER POINTER
   STA P2_MAN_POS_ID_ICON_STATUS               ; SAVE IN CURRENT P2 ICON
   LDA current_defender                                      ;
   JSR GET_P1_P2_RAM_ADDR                      ; GET PLAYER POINTER(A=player ID) AND SAVE IN 3E 3F()
   LDA player_ram_addr                         ; SAVE in P2 ACTIVE PLAYER POINTER
   STA P2_MAN_PLAYER_ADDR                      ;
   LDA player_ram_addr + 1                     ;
   STA P2_MAN_PLAYER_ADDR + 1                  ;
   LDA #hike_delay_frames                      ; SET HIKE DELAY TIMER AFTER PLAYER CHANGE = 0.66 SEC
   STA hike_delay_timer                        ;
   
@exit:                                         ; EXIT 
   RTS                                         ; RETURN

_F}_DEFENDER_CHANGE_BEFORE_HIKE   
  
_F{_CHECK_SNAP_PUNT                             ; DONE

p1_man_bitmask		= $F0
p2_man_bitmask		= $0F
 
CHECK_FOR_SNAP_P2DEF_CHANGE_P1PUNT:            ; P1 PUNT WAIT FOR SNAP
   JSR CHECK_IF_CLOCK_SHOULD_RUN_NOT_IMPLEMENTED                               ; CHECK IF CLOCK SHOULD BE STARTED (NOT IMPLEMENTED)
   LDA TEAM_CONTROL_TYPES                      ; IS PLAYER 1 TYPE  = MAN?
   AND #p1_man_bitmask                         ;
   BNE COM_VS_COM_SNAP_DELAY                   ; NO-> COM PUNT SNAP DELAY
   LDX #JOY_1                                  ; SET JOYPAD INDEX = PLAYER 1
   JMP CHECK_FOR_PUNT_SNAP                     ;
   
CHECK_FOR_SNAP_P1DEF_CHANGE_P2PUNT:            ; P2 PUNT WAIT FOR SNAP
   JSR CHECK_IF_CLOCK_SHOULD_RUN_NOT_IMPLEMENTED                               ; CHECK IF CLOCK SHOULD BE STARTED (NOT IMPLEMENTED)
   LDA TEAM_CONTROL_TYPES                      ; IS PLAYER 2 TYPE  =  MAN?
   AND #p2_man_bitmask                         ;
   BNE COM_VS_COM_SNAP_DELAY                   ; NO-> COM PUNT SNAP DELAY
   LDX #JOY_2                                  ; SET JOYPAD INDEX = PLAYER 2

CHECK_FOR_PUNT_SNAP:                           ; WAIT FOR PUNT SNAP LOOP
   RETURN_1_FRAME      
   LDA CURR_JOY_PRESS,X                        ; DOES PUNTER SIDE JOY PRESS = A PRESSED?
   BPL CHECK_FOR_PUNT_SNAP                     ; NO-> WAIT FOR PUNT SNAP LOOP
   JSR SET_BALL_SNAPPED_START_CLOCK_EXCEPT_XP  ; SET BALL STATUS TO SNAPPED AND START CLOCK IF NOT XP()
   RTS                                         ; RETURN 

COM_VS_COM_SNAP_DELAY:                         ; COM PUNT SNAP DELAY
   LDA RANDOM_2                                ; MAKE COM PUNT SNAP DELAY = 60-91 FRAMES = 1 TO 1.5 SEC 
   AND #$1F                                    ;
   CLC                                         ;
   ADC #$3C                                    ;
   JSR DELAY_TASK                              ; SWITCH TASK (A=# FRAMES BEFORE RETURNING)
   JSR SET_BALL_SNAPPED_START_CLOCK_EXCEPT_XP  ; SET BALL STATUS TO SNAPPED AND START CLOCK IF NOT XP()
   RTS                                         ; RETURN

CHECK_IF_CLOCK_SHOULD_RUN_NOT_IMPLEMENTED:     ; CHECK IF CLOCK SHOULD BE STARTED (NOT IMPLEMENTED)
   LDA CLOCK_RUN_TYPE                          ; LOAD CLOCK STATUS
   LSR                                         ; CHECK FOR STATUS NOT IMPLEMENTED WILL ALWAYS  LOOP 1
   BCC @exit                                   ; 
   SET_CLOCK_TO_RUNNING
@exit:                                         ; EXIT
   RTS                                         ; RETURN

SET_BALL_SNAPPED_START_CLOCK_EXCEPT_XP:        ; SET BALL STATUS TO SNAPPED AND START CLOCK if NOT XP() 
   SET_PLAY_STATUS_BALL_SNAPPED[] 
   LDA POSSESSION_STATUS                       ; DOES PLAY TYPE = EXTRA POINT
   AND #$1C                                    ;
   CMP #$14                                    ; 
   BEQ @exit                       			   ; YES->DON’T START CLOCK
   SET_CLOCK_TO_RUNNING  
   
@exit:                                         ; DON’T START CLOCK
   RETURN_IN_NUM_FRAMES 2
   RTS                                         ; RETURN

_F}C_HECK_SNAP_PUNT   
   
_F{_SET_ONFIELD_SONG						  ; DONE	
  
game_type_bitmask = $03  
SET_ON_FIELD_SONG:                             ; PLAY SOUND LOOP BASED ON GAME TYPE AND PLAYER (A=PLAYER)
   JSR GET_ON_FIELD_SONG_ID                    ; GET MAIN GAME LOOP SOUND INDEX
   JMP PLAY_SOUND                              ; JUMP PLAY SOUND (A= SOUND TO PLAY)

GET_ON_FIELD_SONG_ID:                          ; GET MAIN GAME LOOP SOUND INDEX
   LSR                                         ; SET SOUND INDEX BASED ON GAME TYPE
   LDA GAME_STATUS                             ; 
   AND #game_type_bitmask                      ; 
   ROL                                         ;
   TAX                                         ;
   LDA @song_loop_ids,X                        ; LOAD SOUND ID FROM MAIN GAME LOOP SOUND TABLE
   RTS                                         ; RETURN

@song_loop_ids:                                ; MAIN GAME LOOP SOUND TABLE
   .DB  P1_PRE_SEASON_SONG                     ; PRESEASON SONG P1
   .DB  P2_PRE_SEASON_SONG                     ; PRESEASON SONG P2 
   .DB  P1_PROBOWL_SONG                        ; PRO-BOWL  SONG P1
   .DB  P2_PROBOWL_SONG                        ; PRO-BOWL  SONG P2  
   .DB  P1_SEASON_SONG                         ; SEASON SONG P1
   .DB  P2_SEASON_SONG                         ; SEASON    SONG P2 
   .DB  P1_PLAYOFF_SONG                        ; PLAYOFF   SONG P1
   .DB  P2_PLAYOFF_SONG                        ; PLAYOFF   SONG P2

_F}_SET_ONFIELD_SONG

_F{_LOAD_P1_OR_P2_OFF_PLAY_INFO               ; DONE

play_list_addr 		= LOCAL_7
def_reaction_addr	= LOCAL_7
formation_index		= $0F
off_play_code_addr	= LOCAL_7
 
P1_ON_OFF_LOAD_PLAY_INFO_PLAYER_RAM:           ; P1 ON OFFENSE LOAD PLAY INFORMATION INTO PLAYER RAM(A=PLAY SELECTED
   LDX P1_PLAY_CALL                            ; LOAD P1 SLOT CALLED 
   LDA P1_PLAYBOOK_EDIT,X                      ; GET ACTUAL PLAY CALLED USING SLOT AS INDEX TO P1 PLAYBOOK IN SRAM
   LDX #SIZE_OF_OFF_PLAY_INFO                  ; EACH PLAY ENTRY=  24 BYTES
   JSR MULTIPLY_A_TIMES_X                      ; = (PLAY ENTRY LENGTH * PLAY NUMBER)
   LDA play_list_addr                          ; ADD PLAY LIST OFFSET TO OFFENSIVE PLAY LIST STARTING ADDRESS= 0x1D410=B400
   CLC                                         ; SAVE IN $44,45
   ADC #<PLAY_LIST_START_ADDR                  ;
   STA play_list_addr                          ;
   LDA play_list_addr+1                        ;
   ADC #>PLAY_LIST_START_ADDR                  ;
   STA play_list_addr+1                        ;
   LDX #PLAY_NAME_AND_REACTIONS_BANK           ; LOAD BANK15      
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)    

@load_defensive_call  
   LDA P2_PLAY_CALL                            ; LOAD P2 SLOT CALLED
   CLC                                         ;
   ADC #LENGTH_OF_PLAY_NAME	                   ; ADD 16 BYTES TO SKIP PAST PLAY NAME
   TAY                                         ;

@save_off_play_index:   
   LDA (play_list_addr),Y                      ; LOAD OFFENSIVE PLAY INDEX TO RUN BASED ON DEFENSIVE SLOT
   STA OFF_PLAY_INDEX                          ; SAVE In $7A

@set_formation: 
   LDY #formation_index                        ; SAVE FORMATION CODE IN X
   LDA (play_list_addr),Y                      ; 
   TAX                                         ; 
   JSR LOAD_P1_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P1 ON OFFENSE() 

@get_def_reaction_based_on_off_play:   
   LDA #$00                                    ;
   STA def_reaction_addr +1                    ;
   LDY P1_PLAY_CALL                            ; LOAD PLAYER 1 PLAY CALL 
   LDA P1_PLAYBOOK_EDIT,Y                      ; GET ACTUAL PLAY
@multiply_by_eight_add_starting_offset:  
   ASL                                         ; SHIFT OUT BOTTOM NIBBLE 
   ROL def_reaction_addr +1                    ; GET DEFENSIVE REACTION INDEX BASED ON OFFENSIVE PLAY
   ASL                                         ;
   ROL def_reaction_addr +1                    ;
   ASL                                         ;
   ROL def_reaction_addr +1                    ;
   CLC                                         ;
   ADC #<DEF_REACTION_START_ADDR               ; = OFFSET PLUS DEFENSIVE REACTION LIST STARTING ADDR  0X1DC10= $BC00 
   STA def_reaction_addr                       ;
   LDA def_reaction_addr+1                     ;
   ADC #>DEF_REACTION_START_ADDR               ;
   STA def_reaction_addr+1                     ;
   LDX #PLAY_NAME_AND_REACTIONS_BANK           ; LOAD BANK15 
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   LDY P2_PLAY_CALL                            ; SAVE DEFENSIVE PLAY INDEX IN X
   LDA (def_reaction_addr),Y                   ; 
   TAX                                         ; 
   JSR LOAD_P2_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSE PLAY CODE PTRS P2 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   LDA OFF_PLAY_INDEX                          ; LOAD OFFENSIVE PLAY INDEX  
   LDX #SIZE_OF_ALL_PLAY_POINTERS              ; LOAD 11 * 2 POINTER BYTES PER PLAY 
   JSR MULTIPLY_A_TIMES_X                      ; (OFFENSIVE PLAY INDEX * BYTES PER PLAY)
   LDA off_play_code_addr                      ;
   CLC                                         ;
   ADC #<OFF_PLAY_PTR_START_ADDR               ; SET OFFENSIVE PLAY POINTER START ADDR IN OFFENSIVE PLAY PLAYER PTRS ADDR
   STA OFF_PLAY_ADDR                           ;
   LDA off_play_code_addr+1                    ;
   ADC #>OFF_PLAY_PTR_START_ADDR               ;
   STA OFF_PLAY_ADDR + 1                       ;
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2           ; SET BANK = 20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   RTS                                         ; RETURN

 
 

 
P2_ON_OFF_LOAD_PLAY_INFO_PLAYER_RAM:                                     ; P2 ON OFFENSE LOAD PLAY INFORMATION INTO PLAYER RAM(A=PLAY SELECTED)
   LDX P2_PLAY_CALL                            ; LOAD P2 SLOT CALLED
   LDA P2_PLAYBOOK_EDIT,X                      ; GET ACTUAL PLAY CALLED USING SLOT AS INDEX TO P2 PLAYBOOK IN SRAM
   LDX #SIZE_OF_OFF_PLAY_INFO                  ; EACH PLAY ENTRY=  24 BYTES
   JSR MULTIPLY_A_TIMES_X                      ; = (PLAY ENTRY LENGTH * PLAY NUMBER)
   LDA play_list_addr                          ; ADD PLAY LIST OFFSET TO OFFENSIVE PLAY LIST STARTING ADDRESS= 0x1D410=B400
   CLC                                         ; SAVE IN $44,45
   ADC #<PLAY_LIST_START_ADDR                  ;
   STA play_list_addr                          ;
   LDA play_list_addr+1                        ;
   ADC #>PLAY_LIST_START_ADDR                  ;
   STA play_list_addr+1                        ;
   LDX #PLAY_NAME_AND_REACTIONS_BANK           ; LOAD BANK15      
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   LDA P1_PLAY_CALL                            ; LOAD P1 SLOT CALLED
   CLC                                         ;
   ADC #LENGTH_OF_PLAY_NAME                    ; ADD 16 BYTES TO SKIP PAST PLAY NAME
   TAY         								   ;
@save_off_play_index:  
   LDA (play_list_addr),Y                      ; LOAD OFFENSIVE PLAY INDEX TO RUN BASED ON DEFENSIVE SLOT
   STA OFF_PLAY_INDEX                          ; SAVE In $7A
  
@set_formation:  
   LDY #formation_index                        ; SAVE FORMATION CODE IN X
   LDA (play_list_addr),Y                      ;
   TAX                                         ;
   JSR LOAD_P2_OFF_FORMATION                   ; LOAD OFFENSE FORMATION P2 ON OFFENSE() 
   LDA #$00                                    ;
   STA $45                                     ;
   LDY P2_PLAY_CALL                            ; LOAD PLAYER 2 SLOT CALL
   LDA P2_PLAYBOOK_EDIT,Y                      ; GET ACTUAL PLAY
   ASL                                         ; SHIFT OUT BOTTOM NIBBLE
   ROL def_reaction_addr +1                    ; GET DEFENSIVE REACTION INDEX BASED ON OFFENSIVE PLAY CALLED
   ASL                                         ;
   ROL def_reaction_addr +1                    ;
   ASL                                         ;
   ROL def_reaction_addr +1                    ;
   CLC                                         ;
   ADC #<DEF_REACTION_START_ADDR               ; = OFFSET PLUS DEFENSIVE REACTION LIST STARTING ADDR  0X1DC10= $BC00 
   STA def_reaction_addr                       ;
   LDA def_reaction_addr+1                     ;
   ADC #>DEF_REACTION_START_ADDR               ;
   STA def_reaction_addr+1                     ;
   LDX #PLAY_NAME_AND_REACTIONS_BANK           ; LOAD BANK15 
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   LDY P1_PLAY_CALL                            ;
   LDA (def_reaction_addr),Y                  ; SAVE DEFENSIVE PLAY INDEX IN X
   TAX                                         ;
   JSR LOAD_P1_DEFENSE_PLAY_CODE_ADDRESSES     ; LOAD DEFENSIVE PLAY CODE PTRS P1 ON DEFENSE(X=DEFENSIVE PLAY INDEX)

@set_off_play_ptrs_addr:   
   LDA OFF_PLAY_INDEX                          ; LOAD OFFENSIVE PLAY INDEX  
   LDX #SIZE_OF_ALL_PLAY_POINTERS              ; LOAD 11 * 2 POINTER BYTES PER PLAY 
   JSR MULTIPLY_A_TIMES_X                      ; (OFFENSIVE PLAY INDEX * BYTES PER PLAY)
   LDA off_play_code_addr                      ;
   CLC                                         ;
   ADC #<OFF_PLAY_PTR_START_ADDR               ; SET OFFENSIVE PLAY POINTER START ADDR IN OFFENSIVE PLAY PLAYER PTRS ADDR
   STA OFF_PLAY_ADDR                           ;
   LDA off_play_code_addr + 1                  ;
   ADC #>OFF_PLAY_PTR_START_ADDR               ;
   STA OFF_PLAY_ADDR + 1                        ;
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2           ; SET BANK = 20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   RTS                                         ; RETURN

_F}_LOAD_P1_OR_P2_OFF_PLAY_INFO

_F{_LOAD_OFF_FORMATIONS							; DONE

play_ptrs_addr = LOCAL_7

LOAD_P1_OFF_FORMATION:                         ; LOAD OFFENSE FORMATION P1 ON OFFENSE()
   SAVE_ADDR16_TO_ZP_VAR[addr_zp] PLAYER_1_RAM_START, CURRENT_PLAYER_ADDR    ;
   JMP LOAD_OFFENSIVE_FORMATION_ADDRESSES    ; JUMP TO LOAD OFFENSIVE FORMATION POINTERS STARTING ADDRESS

LOAD_P2_OFF_FORMATION:                         ; LOAD OFFENSE FORMATION P2 ON OFFENSE()
   SAVE_ADDR16_TO_ZP_VAR[addr_zp] PLAYER_2_RAM_START, CURRENT_PLAYER_ADDR  

LOAD_OFFENSIVE_FORMATION_ADDRESSES:           ; LOAD OFFENSIVE FORMATION POINTERS STARTING ADDRESS
   LDA #SIZE_OF_ALL_PLAY_POINTERS                ; LOAD 11 PLAYERS * 2 BYTES PER PLAYER = 22 PLAY POINTER BYTES/ PLAY
   JSR MULTIPLY_A_TIMES_X                      ; = (22 POINTER BYTES PER PLAY * PLAY INDEX)
   ADD_16BIT_CONST_TO_ZP[val_addr] $A000, play_ptrs_addr	   ;  = PLAY POINTER OFFSET + OFFENSIVE PLAY POINTER STARTING ADDRESS
   LDX #FORMATION_OFF_PLAY_POINTERS_BANK       ; SET BANK = BANK 3= OFFENSIVE PLAY BANK   
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   JMP LOAD_PLAYER_SCRIPT_ADDR_INTO_PLAYER_RAM ; JUMP TO LOAD PLAY COMMAND POINTERS INTO PLAYER RAM

_F}_LOAD_OFF_FORMATIONS

_F{_LOAD_DEF_PLAY_INFO						    ; DONE	
 

 
LOAD_P1_DEFENSE_PLAY_CODE_ADDRESSES:           ; LOAD DEFENSIVE PLAY CODE PTRS P1 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
   SAVE_ADDR16_TO_ZP_VAR[addr_zp] PLAYER_1_RAM_START, CURRENT_PLAYER_ADDR    
   JMP LOAD_DEFENSIVE_CODE_ADDRESSES           ;

LOAD_P2_DEFENSE_PLAY_CODE_ADDRESSES:           ; LOAD DEFENSIVE PLAY CODE PTRS P2 ON DEFENSE(X=DEFENSIVE PLAY INDEX)
  SAVE_ADDR16_TO_ZP_VAR[addr_zp] PLAYER_2_RAM_START, CURRENT_PLAYER_ADDR   

LOAD_DEFENSIVE_CODE_ADDRESSES:                ; LOAD DEFENSIVE PLAYER POINTERS STARTING ADDRESS
   LDA #SIZE_OF_ALL_PLAY_POINTERS              ; LOAD 11 PLAYERS * 2 BYTES PER PLAYER = 22 PLAY POINTER BYTES/ PLAY
   JSR MULTIPLY_A_TIMES_X                      ;
											   ; PLAY POINTER OFFSET + DEFENSIVE PLAY POINTER STARTING ADDRESS 
   ADD_16BIT_CONST_TO_ZP[val_addr] $A000, play_ptrs_addr
   LDX #DEF_PLAY_POINTERS_BANK                 ; SET BANK = BANK 4 = DEFENSIVE PLAY BANK    
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)

_F}_LOAD_DEF_PLAY_INFO
  
_F{_LOAD_UPDATE_PLAY_CODE_FUNCTIONS				; DONE
 
 player_script_addr = LOCAL_7
 
LOAD_PLAYER_SCRIPT_ADDR_INTO_PLAYER_RAM:       ; LOAD PLAY COMMAND POINTERS INTO PLAYER RAM()
   LDX #NUM_PLAYERS_ON_FIELD                   ; SET NUMBER OF PLAYERS TO UPDATE = 11
   
   _WHILE NOT_EQUAL                            ; UPDATE PLAY CODE ADDRESS LOOP

@copy_ptr_to_player_ram:   
   COPY_PLAY_CODE_ADDR_PLAYER_RAM[data_addr_ram_addr] player_script_addr, CURRENT_PLAYER_ADDR

@set_pointer_to_next:    
   ADD_8_BIT_CONST_TO_16BIT_ADDR[addr_val] player_script_addr, $02

@set_to_next_player:
   SET_PLAYER_RAM_ADDR_TO_NEXT_PLAYER[player_addr] CURRENT_PLAYER_ADDR 
   DEX                                         ; NUMBER OF PLAYERS TO UPDATE --
   
   _END_WHILE
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2           ; LOAD BANK20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   RTS                                         ; RETURN

   
update_man_false  					= $00 
update_man_true						= $80
collision_bitmask					= LOCAL_3
update_man_boolean					= LOCAL_4 
curr_player_ram_addr				= LOCAL_5
play_code_data_addr					= LOCAL_7
ongnd_jump_blkpass_bitmask			= $1C 
coll_ongnd_jump_blkpass_bitmask		= $3C
diving_bitmask						= $18
 
UPDATE_ALL_P1_PLAY_CODE_ADRR_EXCEPT_MAN:       ; P1 UPDATE ALL PLAYER PLAY CODE ADDRESSES EXCEPT ACTIVE PLAYER (X,Y= PLAY CODE ADDRESS)
   LDA #update_man_false                       ; SET UPDATE P1 ACTIVE PLAYER = FALSE 
   JMP p1_save_update_man_true_false

UPDATE_ALL_P1_PLAY_CODE_ADRR:                  ; P1 UPDATE ALL PLAYER PLAY CODE ADDRESSES (X,Y= PLAY CODE ADDRESS)
   LDA #update_man_true                        ; SET UPDATE P1 ACTIVE PLAYER = TRUE

p1_save_update_man_true_false:                    ;
   STA update_man_boolean                      ; SAVE UPDATE P1 ACTIVE PLAYER IN $41
   STY player_script_addr                      ; SAVE COMMAND ADDRESS IN $44, $45
   STX player_script_addr + 1                   ; 
   LDA #<PLAYER_1_RAM_START                    ; SET PLAYER RAM POINTER TO P1 FIRST PLAYER IN $42,43
   STA curr_player_ram_addr                    ;
   LDA #>PLAYER_1_RAM_START                    ;
   STA curr_player_ram_addr + 1                ;
   LDA POSSESSION_STATUS                       ; Player 1 HAS BALL?
   BMI @set_play_code_bank_to_defense          ; NO->  LOAD DEFENSE BANK

@set_play_code_bank_to_offense:   
   LDX #FORMATION_OFF_PLAY_POINTERS_BANK       ; BANK3                                                        
   JMP @p1_load_play_script_addr_all_players   ; 

@set_play_code_bank_to_defense:                ; LOAD DEFENSE PLAY COMMAND BANK
   LDX #DEF_PLAY_POINTERS_BANK                 ; BANK4                                                      

@p1_load_play_script_addr_all_players:         ; P1 LOAD PLAY CODE ADDRESSES TO ALL PLAYER AND LOAD SPECIAL CODE FOR MAN PLAYER
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   JSR SET_ALL_PLAYER_SCRIPT_ADRR_EXCEPT_MAN   ; UPDATE ALL PLAYERS PLAY CODE ADDRESSES EXCEPT ACTIVE PLAYER();
   LDA update_man_boolean                      ; UPDATE ACTIVE PLAYER? 
   BPL @update_play_code_exit                  ; NO-> EXIT
 
@set_P1_man_script_addr:
   COPY_PLAY_CODE_ADDR_PLAYER_RAM[data_addr_ram_addr] player_script_addr, P1_MAN_PLAYER_ADDR 
   

@update_play_code_exit:                        ; EXIT
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2           ; LOAD BANK20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   RTS                                         ; RETURN

UPDATE_ALL_P2_PLAY_CODE_ADRR_EXCEPT_MAN:       ; UPDATE P2 PLAY CODE ADDRESSES OF ALL NON-MAN CONTROL PLAYERS (X,Y= PLAY CODE ADDRESS)
   LDA #update_man_false                       ; SET UPDATE P2 ACTIVE PLAYER = FALSE
   JMP p2_save_update_man_true_false            ;  

UPDATE_ALL_P2_PLAY_CODE_ADRR:                  ; UPDATE P2 PLAY CODE ADDRESSES OF ALL PLAYERS (X,Y= PLAY CODE ADDRESS)
   LDA #update_man_true                        ; SET UPDATE P2 ACTIVE PLAYER = TRUE

p2_save_update_man_true_false:                   ;
   STA update_man_boolean                      ; SAVE UPDATE ACTIVE PLAYER OPTION
   STY player_script_addr                      ; SAVE COMMAND ADDRESS IN $44, $45
   STX player_script_addr +1                   ; 
   LDA #<PLAYER_2_RAM_START                    ; SET PLAYER POINTER = FIRST P2 PLAYER POINTER = 0X04FF
   STA curr_player_ram_addr                    ;
   LDA #>PLAYER_2_RAM_START                    ;
   STA curr_player_ram_addr +1                 ;
   LDA POSSESSION_STATUS                       ; DOES POSSESION STATUS = PLAYER 2 ON DEFENSE?
   BPL @set_to_def_script_bank                 ; YES-> SET BANK = DEFENSE PLAY CODE BANK
   LDX #FORMATION_OFF_PLAY_POINTERS_BANK       ; BANK3 
   JMP @p2_load_play_script_addr_all_players
   
@set_to_def_script_bank:                       ; SET BANK = DEFENSE PLAY CODE BANK
   LDX #DEF_PLAY_POINTERS_BANK                 ; SET BANK = BANK4 = DEFENSIVE PLAY CODE

@p2_load_play_script_addr_all_players:         ; P2 LOAD PLAY CODE ADDRESSES TO ALL PLAYER AND LOAD SPECIAL CODE FOR MAN PLAYER
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   JSR SET_ALL_PLAYER_SCRIPT_ADRR_EXCEPT_MAN                               ; UPDATE ALL PLAYERS PLAY CODE ADDRESSES EXCEPT ACTIVE PLAYER()
   LDA update_man_boolean                      ; UPDATE P2 MAN PLAYER?
   BPL @exit                                   ; NO-> EXIT

@set_P2_man_script_addr:  
   COPY_PLAY_CODE_ADDR_PLAYER_RAM[data_addr_ram_addr] player_script_addr, P2_MAN_PLAYER_ADDR
   
@exit:
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2           ; SET BANK = BANK20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
   RTS                                         ; RETURN

                                               ; PLAYER RAM ? IN $41, PLAYER RAM START ADDRESS $42/$43, Command pointer in $44,$45
SET_ALL_PLAYER_SCRIPT_ADRR_EXCEPT_MAN:         ; UPDATE ALL PLAYERS PLAY CODE ADDRESSES EXCEPT ACTIVE PLAYER()
   LDX #coll_ongnd_jump_blkpass_bitmask	       ; SET CHECK PLAYER COLLISION CHECK = COLLIDED, ON GROUND, JUMPING, BLOCKING BALL
   IF_PLAY_NOT_TOUCHDOWN[ADDR] SAVE_COLLISION_CHECK_MASK
   LDX #ongnd_jump_blkpass_bitmask             ; SET CHECK PLAYER COLLISION CHECK =  ON GROUND, JUMPING, BLOCKING BALL

SAVE_COLLISION_CHECK_MASK:                     ; SAVE CHECK PLAYER COLLISION MASK
   STX collision_bitmask                        ;
   
@wait_for_players_task_to_finish:              ; WAIT FOR UPDATE PLAYERS TASK TO COMPLETE
   LDA UPDATE_PLAYERS_TASK                     ; DOES UPDATE PLAYERS TASK STATUS = RUNNING
   CMP #TASK_RUNNING                           ; 
   BNE @suspend_players_task                   ; NO->SET UPDATE PLAYERS TASK TO SUSPENDED  
   RETURN_1_FRAME 
   JMP @wait_for_players_task_to_finish        ; JUMP->WAIT FOR UPDATE PLAYERS TASK TO COMPLETE

@suspend_players_task:                         ; SET UPDATE PLAYERS TASK TO SUSPENDED
   SUSPEND_TASK_DIRECT[task] UPDATE_PLAYERS_TASK
   
   LDX #NUM_PLAYERS_ON_FIELD                   ; SET NUMBER OF PLAYERS TO UPDATE = 11
   
@update_all_players_loop:                      ; UPDATE P2 PLAYERS PLAY CODE ADDRESS LOOP
   LDY #MOVEMENT_STATUS                        ; 
   LDA update_man_boolean                      ; DOES UPDATE ACTIVE PLAYER = TRUE 
   BMI @set_player_to_not_man_controlled:      ; YES->UPDATE ALL P2 PLAYERS LOOP
 
@check_if_current_player_man_controlled 
   LDA (curr_player_ram_addr),Y                ; IS CURRENT PLAYER  = MAN CONTROLLED
   AND #PLAYER_MAN_COM_BITFLAG                 ; 
   BNE  @go_to_next_player_check_done          ; YES->GO TO NEXT PLAY CODE ADDR, CHECK IF DONE

@set_player_to_not_man_controlled:             ; UPDATE P2 PLAY CODE PLAYER SET TO NOT MAN CONTROLLED
   LDA (curr_player_ram_addr),Y                ; SET PLAYER MOVEMENT = NOT MAN CONTROLLED
   AND #$BF                                    ;
   STA (curr_player_ram_addr),Y                ;
   DEY                                         ;
   LDA (play_code_data_addr),Y                 ; TRANSFER PLAY CODE ADDR TO PLAYER RAM PLAY CODE ADDR
   LDY #PLAY_CODE_ADDR                         ;
   STA (curr_player_ram_addr),Y                ;
   LDY #$01                                    ;
   LDA (play_code_data_addr),Y                 ;
   LDY #PLAY_CODE_ADDR+1                       ;
   STA (curr_player_ram_addr),Y                ;

@check_for_player_collided:   
   LDY #COLLISION_STATUS                       ; DOES PLAYER COLLISION STATUS MATCH ANY COLLISION CHECK 
   LDA (curr_player_ram_addr),Y                ; 
   AND collision_bitmask                       ; 
   BNE @go_to_next_player_check_done           ; YES->GO TO NEXT PLAY CODE ADDR, CHECK IF DONE
 
@check_for_player_diving_on_ground:  
   INY                                         ; DOES PLAYER MOVEMENT = CHOP BLOCK/DIVING FOR TACKLE
   LDA (curr_player_ram_addr),Y                ;
   AND #diving_bitmask                         ;
   BNE @go_to_next_player_check_done           ; YES->GO TO NEXT PLAY CODE ADDR AND PLAYER, CHECK IF DONE
   
@set_command_addr_to_process_command:   
   LDY #COMMAND_COUNTER                        ; SET COMMAND COUNTER = 1 FRAME
   LDA #$01                                    ; 
   STA (curr_player_ram_addr),Y                ;
   INY                                         ; SET COMMAND ADDR = PROCESS COMMAND -1 = $8000-1
   LDA #<JUMP_DO_NEXT_PLAYER_COMMAND -1        ;
   STA (curr_player_ram_addr),Y                ;
   INY                                         ;
   LDA #>JUMP_DO_NEXT_PLAYER_COMMAND -1        ;
   STA (curr_player_ram_addr),Y                ;

 @go_to_next_player_check_done:                  ; GO TO NEXT PLAY CODE ADDR AND PLAYER, CHECK IF DONE 
   
   ADD_8_BIT_CONST_TO_16BIT_ADDR[addr_val] player_script_addr, SIZE_OF_ADDR 
   
   ADD_8_BIT_CONST_TO_16BIT_ADDR[addr_val] curr_player_ram_addr, SIZE_OF_EACH_PLAYER_RAM 
   
   DEX                                         ; NUMBER OF PLAYERS TO CHECK--
   BNE @update_all_players_loop                 ; DONE? NO->UPDATE P2 PLAYERS PLAY CODE ADDRESS LOOP
   
@resume_task_and_exit  
   LDX #UPDATE_PLAYERS_TASK                    ; SET TASK INDEX =  UPDATE PLAYERS TASK INDEX 
   JSR RESUME_TASK                             ; RESUME TASK(X=TASK INDEX)
   RTS                                         ; RETURN
   
_F}_LOAD_UPDATE_PLAY_CODE_FUNCTIONS     

_F{_LOAD_SKILLS								   ; DONE 
 
LOAD_ALL_SKILLS_INTO_PLAYER_RAM:               ; LOAD ALL PLAYER SKILLS(RS,RP,MS,HP) INTO PLAYER RAM() Bank 27 $85A9
   LDA #SET_PLAYER_SKILLS_IN_RAM_BANK          ; SET BANK = 27   
   SET_ADDR_XY[addr] LOAD_ALL_SKILLS_ROUTINE
   JSR SWAP_8000_BANK_AND_JUMP_RETURN          ; SWAP $8000 BANK AND JUMP(X,Y= ADDR, A= BANK) 
   RTS                                         ; RETURN

 player_ram_addr		= LOCAL_1
 temp_player_ram_addr   = LOCAL_3 
 temp_starter_id		= LOCAL_6  

											   ; A= STARTER ID X = POSITION ID, 			
LOAD_SINGLE_PLAYER_SKILLS_TO_PLAYER_RAM:       ; SET PLAYER SKILLS IN PLAYER RAM SPECIFIC PLAYER (A=PLAYER, X=PLAYER POINTER TO LOAD INTO)
   STA temp_starter_id                         ; SAVE PLAYER ID TO GET SKILLS FOR
   TXA                                         ; SET PLAYER TO LOAD INTO
   JSR GET_P1_P2_RAM_ADDR                      ; GET PLAYER POINTER(A=player ID) AND SAVE IN 3E 3F()
   LDA player_ram_addr                         ; SAVE PLAYER POINTER
   STA temp_player_ram_addr                    ;
   LDA player_ram_addr +1                      ;
   STA temp_player_ram_addr+1                  ;
   LDA #SET_PLAYER_SKILLS_IN_RAM_BANK          ; SET BANK = BANK 27
   SET_ADDR_XY[addr] LOAD_SINGLE_PLAYER_SKILLS_ROUTINE
   JSR SWAP_8000_BANK_AND_JUMP_RETURN          ; SWAP $8000 BANK AND JUMP(X,Y= ADDR, A= BANK) 
   RTS                                         ; RETURN

_F}_LOAD_SKILLS 
   
_F{_STOP_CURRENT_SONG							; DONE
 
STOP_CURRENTLY_PLAYING_SOUND:                  ; STOP CURRENTLY PLAYING SOUND LOOP ()
   LDA #STOP_SONG_SOUND_ID                     ; SET SOUND TO PLAY = STOP SOUNDS
   STA SOUND_ID_TO_PLAY                        ; 
   LDX ON_FIELD_SONG_TO_PLAY_AFTER_INT         ; IS SONG ID TO PLAY AFTER INTERCEPTION MUSIC = NONE
   BEQ @exit                                   ; YES->EXIT
   STA ON_FIELD_SONG_TO_PLAY_AFTER_INT 

@exit:                                         ; EXIT
   RTS                                         ; RETURN

 _F}_STOP_CURRENT_SONG   
 
 _F{_MAN_CONTROLLED_PLAYER_FUNCTIONS           ; DONE
 
SET_P1_MAN_TO_KR_P2_MAN_KICKER:                ; SET P1 AND P2 ACTIVE PLAYER POINTERS (P1= 1 PLAYER 11, P2= PLAYER 1)
   LDA #<P1_RAM_RT_SS                          ; SET ACTIVE PLAYER 1 POINTER TO $04DF (RT/SS)
   STA P1_MAN_PLAYER_ADDR                      ;
   LDA #>P1_RAM_RT_SS                          ;
   STA P1_MAN_PLAYER_ADDR + 1                  ;
   LDA #<P2_RAM_KICKER                         ; SET ACTIVE PLAYER 2 POINTER TO $04FF (QB/RE)
   STA P2_MAN_PLAYER_ADDR                      ;
   LDA #>P2_RAM_KICKER                         ;
   STA P2_MAN_PLAYER_ADDR + 1                  ;
   RTS                                         ; RETURN

SET_P2_MAN_TO_KR_P1_MAN_KICKER:                ; SET P1 AND P2 ACTIVE PLAYER POINTERS (P1= 1 PLAYER 1, P2= PLAYER 111)
   LDA #<P2_RAM_RT_SS                          ; SET ACTIVE PLAYER 2 POINTER TO $063F (RT/SS)
   STA P2_MAN_PLAYER_ADDR                      ;
   LDA #>P2_RAM_RT_SS                          ;
   STA P2_MAN_PLAYER_ADDR + 1                  ;
   LDA #<P1_RAM_KICKER                         ; SET ACTIVE PLAYER 1 POINTER TO $039F (QB/RE)
   STA P1_MAN_PLAYER_ADDR                      ;
   LDA #>P1_RAM_KICKER                         ;
   STA P1_MAN_PLAYER_ADDR + 1                  ;
   RTS                                         ;



ADD_31MS_TO_P1_MAN_DEFENDER:                   ; ADD 31 MS TO TOP SPEED FOR P1 MAN DEFENDER()
   LOAD_P1_MAN_PLAYER_MOVEMENT_STATUS
   ASL                                         ; 
   BPL @exit                                   ; NO-> DON’T ADJUST P1 SPEED

@boost_p1_man_speed:   
   LOAD_P1_MAN_PLAYER_MAX_SPEED				   ;
   CLC                                         ;
   ADC #BC_PAST_LOS_MAN_DEF_MS_BOOST           ; 
   STA (P1_MAN_PLAYER_ADDR),Y                  ;  
@exit:                                         ; DON’T ADJUST P1 SPEED
   RTS                                         ; RETURN

ADD_31MS_TO_P2_MAN_DEFENDER:                   ; ADD 31 MS TO TOP SPEED FOR P2 MAN DEFNDER()
   LOAD_P2_MAN_PLAYER_MOVEMENT_STATUS
   ASL                                         ; 
   BPL @exit                                   ; NO->DON’T ADJUST P2 SPEED
   
@boost_p2_man_speed:    
   LOAD_P2_MAN_PLAYER_MAX_SPEED
   CLC                                         ;
   ADC #BC_PAST_LOS_MAN_DEF_MS_BOOST            ; 
   STA (P2_MAN_PLAYER_ADDR),Y                  ; 
@exit:                                         ; DON’T ADJUST SPEED
   RTS                                         ; RETURN

 _F}_MAN_CONTROLLED_PLAYER_FUNCTIONS   
 
_F{_CPU_PLAY_LOGIC                             ; DONE

cpu_kickoff_type	= LOCAL_8  
  
GET_P1_CPU_KICKOFF_TYPE:                       ; GET CPU KICKOFF PLAY CALL, P1 on OFFENSE()
   LDX P1_TOTAL_SCORE                          ; LOAD P1 TOTAL SCORE
   LDY P2_TOTAL_SCORE                          ; LOAD P2 TOTAL SCORE
   JMP GET_CPU_KICK_TYPE                       ; JUMP-> GET CPU PLAY CALL
   
GET_P2_CPU_KICKOFF_TYPE:                       ; GET CPU KICKOFF PLAY CALL, P2 on OFFENSE()
   LDX P2_TOTAL_SCORE                          ; LOAD P2 TOTAL SCORE
   LDY P1_TOTAL_SCORE                          ; LOAD P1 TOTAL SCORE

GET_CPU_KICK_TYPE:                            ; GET CPU PLAY CALL
   STX CPU_OFF_SCORE                           ; SAVE CPU TEAM_SCORE
   STY CPU_OPP_SCORE                           ; SAVE OPPONENT_SCORE 
   JSR SWAP_AND_CHECK_CPU_ONSIDE               ; GET CPU KICKOFF TYPE()
   LDA cpu_kickoff_type                        ; CPU PLAY CALL = ONSIDE KICK?
   BPL @exit                                   ; NO-> EXIT

@set_onsides_bal_status:   
  SET_BALL_STATUS_SPECIAL_ONSIDES_KICK

@exit:                                         ; EXIT 
   RTS                                         ; RETURN

_F}_CPU_PLAY_LOGIC  
   
 _F{_SIDE_CHANGE_BANNER_AND_SONG               ; DONE

P1_to_P2_SIDE_CHANGE_BANNER_AND_MUSIC:         ; PLAY SIDE CHANGE MUSIC and BANNER P1 to P2
   PLAY_SOUND_ID CHANGE_SIDE_SONG
   LDA #NEXT_DEF_NEXT_OFF_BANNER_ID            ; SET BANNER ID = P2 NEXT OFFENSE SIDE CHANGE  + SCROLL IN FROM LEFT
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO) 
   RETURN_IN_NUM_FRAMES 60
   RTS                                         ; RETURN

P2_to_P1_SIDE_CHANGE_BANNER_AND_MUSIC:         ; PLAY SIDE CHANGE MUSIC and BANNER P2 to P1
   PLAY_SOUND_ID CHANGE_SIDE_SONG
   LDA #NEXT_OFF_NEXT_DEF_BANNER_ID +  SCROLL_BANNER_IN_FROM_RIGHT_BIT_FLAG                                    
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO) 
   RETURN_IN_NUM_FRAMES 60
   RTS                                         ; RETURN

 _F}_SIDE_CHANGE_BANNER_AND_SONG    
 
_F{_SET_PLAYERS_CLOSE_TO_PASS					; DONE

max_distance						= $FF				
player_too_far_away					= $FF 
diving_bitmask						= $18 
coll_ongnd_jump_blkpass_bitmask		= $3C

math_temp_1					= LOCAL_1
distance_temp				= LOCAL_2
current_defender_addr		= LOCAL_1
temp_target_addr			= LOCAL_1
distance_limit				= LOCAL_3 
defenders_to_check			= LOCAL_7 
temp_defender_distance		= LOCAL_8
 
SET_AND_ORDER_BALL_COLLISION_PLAYERS:          ; PASS PLAY ORDER CLOSEST PLAYERS TO BALL
   LDA X_DIST_REMAIN_BALL                      ; X PASS DISTANCE REMAINING > Y PASS DISTANCE REMAINING ?  
   CMP Y_DIST_REMAIN_BALL                      ; 
   LDA X_DIST_REMAIN_BALL + 1                  ; 
   SBC Y_DIST_REMAIN_BALL + 1                  ;
   BCS @load_x_distance_remain                 ; YES->LOAD X DISTANCE
   
@load_y_distance_remain:  
   LDX Y_DIST_REMAIN_BALL                      ; SET DIST = Y PASS DISTANCE REMAINING      
   LDA Y_DIST_REMAIN_BALL + 1                  ;
   JMP @save_distance                          ; JUMP->SAVE 3/4 the  (X or Y) distance in $40 with a max of #$FF (32 yards)
   
@load_x_distance_remain:                       ; LOAD X PASS DISTANCE REMAINING
   LDX X_DIST_REMAIN_BALL                      ; SET DIST = Y PASS DISTANCE REMAINING 
   LDA X_DIST_REMAIN_BALL+1                    ; 

@save_distance:                                ; SAVE 3/4 the (X or Y) distance in $40 with a max of #$FF (32 yards)
   STX distance_temp +1                        ; 
   LSR                                         ;
   STA distance_temp                           ;
   ROR distance_temp +1                        ;
   LDX distance_temp +1                        ;
   STX math_temp_1                             ;
   LSR                                         ;
   TAX                                         ;
   ROR distance_temp +1                        ;
   LDA distance_temp +1                        ;
   CLC                                         ;
   ADC math_temp_1                             ;
   STA distance_limit                          ;
   TXA                                         ;
   ADC distance_temp                           ;
   BEQ @initialize_defender_ram_addr           ;
   
@set_max_distance_to_32_yards 
   LDA #max_distance                           ;
   STA distance_limit                          ;
   
@initialize_defender_ram_addr:
   LDA POSSESSION_STATUS                       ; SET POSSESSION VALUE 
   AND #$80                                    ; 
   EOR #$80                                    ; 
   JSR GET_P1_P2_RAM_ADDR                      ; GET FIRST P1 or P2 RAM POINTER AND SAVE IN 3E,3F
   JSR WAIT_UNTIL_NAME_BUFFER_READY            ; WAIT UNTIL TEXT BUFFER IS DONE()
   
   
@init_pass_ram:								   ; sets current closest distance and all player   
   SET_RAM_Y_INDEX[start_length_value] $037F,$06,$FF
   
   LDA #NUM_DEFENDERS_ON_FIELD                 ; SET NUMBER OF DEFENDERS TO CHECK =11 in $44
   STA defenders_to_check                      ; 

@defender_coll_check:                 		   ; DEFNDER VS FINAL BALL CHECK LOOP
   LDY #$00                                    ; IS DEFENDER COLLIDED, ON GROUND, JUMPING, COLLIDING WITH BALL?
   LDA (current_defender_addr),Y               ; 
   AND #coll_ongnd_jump_blkpass_bitmask        ; 
   BNE @set_to_next_defender                   ; YES->GO TO NEXT DEFENDER
  
@check_defender_diving:						   ;  
   INY                                         ; 
   LDA (current_defender_addr),Y               ; IS DEFENDER DIVING FOR TACKLE SEEMS IMPOSSIBLE WITH PASS ¼ OF THE WAY THERE
   AND #diving_bitmask                         ; 
   BNE @set_to_next_defender                   ; YES?->GO TO NEXT DEFENDER
   
 @check_defender_close_enough_to_final_ball:
   JSR COMPARE_PLAYER_FINAL_BALL               ; GET GREATEST X OR Y DISTANCE (3E,3F= PTR) FROM FINAL BALL LOCATION 
   CMP distance_limit                          ; IS PLAYER WITHIN 3/4 OF THE DISTANCE TO THE FINAL BALL LOCATION. Max=32 yards
   BCS @set_to_next_defender                   ; NO-> GO TO NEXT DEFENDER
 
@save_defender_distance:  
   STA temp_defender_distance                  ; SAVE DISTANCE
   CMP FIRST_CLOSEST_DEFENDER_DIST_PASS        ; IS DISTANCE > CLOSEST PLAYER?
   BCS @check_vs_second_closest_player         ; YES-> CHECK 2ND CLOSEST PLAYER

@set_to_closest_player:   
   LDY #$02                                    ; SET NUM PLAYERS TO SORT = 3
   JSR SORT_DEFENDER_DISTANCES[num_defenders]  ; SORT PLAYER LIST AND SAVE DISTANCE AND PLAYER ID (Y=NUM PLAYERS-1 TO SORT)
   JMP @set_to_next_defender                   ; JUMP->GO TO NEXT DEFENDER

@check_vs_second_closest_player:               ; CHECK 2ND CLOSEST PLAYER
   CMP SECOND_CLOSEST_DEF_DIST_PASS            ; IS DISTANCE > 2ND CLOSEST PLAYER?
   BCS @check_vs_third_closest_player          ; YES-> CHECK 3RD CLOSEST PLAYER
 
@set_to_second_closest_player: 
   LDY #$01                                    ; SET NUM PLAYERS TO SORT = 1
   JSR SORT_DEFENDER_DISTANCES[num_defenders]  ; SORT PLAYER LIST AND SAVE DISTANCE AND PLAYER ID (Y=NUM PLAYERS-1 TO SORT)
   JMP @set_to_next_defender                   ; JUMP TO GO TO NEXT DEFENDER

@check_vs_third_closest_player:                ; CHECK 3RD CLOSEST PLAYER
   CMP THIRD_CLOSEST_DEF_DIST_PASS             ; IS DISTANCE > 3RD CLOSEST PLAYER?
   BCS @set_to_next_defender                   ; JUMP->GO TO NEXT DEFENDER
   LDX #$02                                    ; SET OFFSET TO PLAYER =  THIRD PLAYER
   JSR SET_DEF_DIST_POS_ID_PASS                ; SAVE PLAYER DISTANCE AND POSITION ID(X= PLAYER INDEX) 

@set_to_next_defender:                         ; GO TO NEXT DEFENDER
   ADD_8_BIT_CONST_TO_16BIT_ADDR[addr_val] current_defender_addr, SIZE_OF_EACH_PLAYER_RAM 
   DEC defenders_to_check                      ; DEFENDERS TO CHECK-- 
   BNE @defender_coll_check                    ; DONE? NO->DEFENDER VS FINAL BALL CHECK

SET_TARGET_PRIORITY:                            ; SET TARGET PRIORITY
   
@check_collision_status:   
   LDY #COLLISION_STATUS                       ; IS TARGET COLLISION  = COLLIDED, ON GROUND/DIVING, JUMPING?
   LDA (CURRENT_TARGET_ADDR),Y                 ;
   AND #coll_ongnd_jump_blkpass_bitmask        ; 
   BNE @set_player_too_far_away                ; YES?->SET DEFENDER OR PASS TARGET IS TOO FAR, COLLIDED, JUMPING,DIVING
 
@check_movement_status 
   INY                                         ;
   LDA (CURRENT_TARGET_ADDR),Y                 ; IS TARGET MOVEMENT =  CHOP BLOCKING, DIVING AS DEFNDER? (DON’T KNOW HOW THIS COULD BE)
   AND #diving_bitmask                         ;
   BNE @set_player_too_far_away                ; YES->SET DEFENDER OR PASS TARGET IS TOO FAR, COLLIDED, JUMPING,DIVING

@check_target_distance_vs_final_ball:   
   LDA CURRENT_TARGET_ADDR                     ; SAVE TARGET POINTER IN 3E,3F
   STA temp_target_addr                        ;
   LDA CURRENT_TARGET_ADDR +1                  ;
   STA temp_target_addr +1                     ;
   JSR COMPARE_PLAYER_FINAL_BALL               ; GET GREATEST X OR Y DISTANCE (3E,3F= PTR) FROM FINAL BALL LOCATION
   TAX                                         ; SAVE DISTANCE IN X
   LDY #POSITION_ID	                           ; LOAD TARGET POSITION ID
   LDA (CURRENT_TARGET_ADDR),Y                 ; 
   CPX FIRST_CLOSEST_DEFENDER_DIST_PASS        ; IS TARGET DISTANCE < CLOSEST DEFNDER?
   BCC @set_player_to_closest                  ; YES-> SET PASS TARGET TO HIGHEST PRIORITY PLAYER
 
@check_vs_second_defender: 
   CPX SECOND_CLOSEST_DEF_DIST_PASS            ; IS TARGET DISTANCE < 2ND CLOSEST DEFNDER?
   BCC @set_player_to_2nd_farthest_and_sort    ; YES-> SET PASS TARGET TO 2ND PRIORITY PLAYER RE-SORT DEFENDERS

@check_vs_third_defender:   
   CPX THIRD_CLOSEST_DEF_DIST_PASS             ; IS TARGET DISTANCE < 3RD CLOSEST DEFNDER?
   BCC @set_player_to_3rd_farthest_and_sort:   ; YES-> SET PASS TARGET TO 3RD PRIORITY PLAYER RE-SORT DEFENDERS
 
@set_target_to_fourth_priority:  
   JMP @set_player_to_4th_farthest_and_sort    ; NO->SET PASS TARGET TO 4TH PRIORITY PLAYER RE-SORT DEFENDERS

@set_player_too_far_away :                     ; SET DEFENDER OR PASS TARGET IS TOO FAR, COLLIDED, JUMPING,DIVING
   LDA #player_too_far_away	                   ; 

@set_player_to_4th_farthest_and_sort:          ; SET PASS TARGET TO 4TH PRIORITY PLAYER RE-SORT DEFENDERS
   LDX SECOND_CLOSEST_POS_ID_PASS                                   ;
   STX FIRST_CLOSEST_POS_ID_PASS                                   ;
   LDX THIRD_CLOSEST_POS_ID_PASS                                   ;
   STX SECOND_CLOSEST_POS_ID_PASS                                   ;
   LDX FOURTH_CLOSEST_POS_ID_PASS                                   ;
   STX THIRD_CLOSEST_POS_ID_PASS                                   ;
   STA FOURTH_CLOSEST_POS_ID_PASS                                   ;
   JMP SET_PLAYER_PRIORITY_AND_ID              ;
   
@set_player_to_3rd_farthest_and_sort:          ; SET PASS TARGET TO 2ND PRIORITY PLAYER RE-SORT DEFENDERS
   LDX SECOND_CLOSEST_POS_ID_PASS                                   ;
   STX FIRST_CLOSEST_POS_ID_PASS                                   ;
   LDX THIRD_CLOSEST_POS_ID_PASS                                   ;
   STX SECOND_CLOSEST_POS_ID_PASS                                   ;
   STA THIRD_CLOSEST_POS_ID_PASS                                   ;
   JMP SET_PLAYER_PRIORITY_AND_ID              ;

@set_player_to_2nd_farthest_and_sort:          ; SET PASS TARGET TO 3RD PRIORITY PLAYER RE-SORT DEFENDERS
   LDX SECOND_CLOSEST_POS_ID_PASS                                   ;
   STX FIRST_CLOSEST_POS_ID_PASS                                   ;
   STA SECOND_CLOSEST_POS_ID_PASS                                   ;
   JMP SET_PLAYER_PRIORITY_AND_ID              ;
   
@set_player_to_closest:                        ; SET PASS TARGET TO HIGHEST PRIORITY PLAYER
   STA FIRST_CLOSEST_POS_ID_PASS               ;

SET_PLAYER_PRIORITY_AND_ID:                    ;
   LDA FIRST_CLOSEST_POS_ID_PASS               ; LOAD 1ST CLOSEST PLAYER ID
   LDX #$00                                    ; SET PRIORITY =  HIGHEST PRIORITY
   JSR LOAD_BALL_COLLISION_CHECK_ADDR_SET_PASS_PRIORITY                               
   LDA SECOND_CLOSEST_POS_ID_PASS              ; LOAD 2ND CLOSEST PLAYER ID
   LDX #$01                                    ; SET PRIORITY =   2nd HIGHEST PRIORITY
   JSR LOAD_BALL_COLLISION_CHECK_ADDR_SET_PASS_PRIORITY                              
   LDA THIRD_CLOSEST_POS_ID_PASS               ; LOAD 3RD CLOSEST PLAYER ID
   LDX #$02                                    ; SET PRIORITY =   3RD HIGHEST PRIORITY
   JSR LOAD_BALL_COLLISION_CHECK_ADDR_SET_PASS_PRIORITY                               
   LDA FOURTH_CLOSEST_POS_ID_PASS              ; LOAD 4TH CLOSEST PLAYER ID
   LDX #$03                                    ; SET PRIORITY =   4TH HIGHEST PRIORITY
   JSR LOAD_BALL_COLLISION_CHECK_ADDR_SET_PASS_PRIORITY                               
       
@clear_pass_buffer 
   JSR SET_NAME_BUFFER_READY                   ; CLEAR $37E() fixed bank
 
   LDY #PLAY_CODE_ADDR                         ; SET TARGET PLAY CODE ADDR = FINAL TARGET PLAY CODE ADDR 
   LDA TARGET_PLAY_CODE_ADDR                   ; load play code for after catch = man control 
   STA (CURRENT_TARGET_ADDR),Y                 ;
   INY                                         ;
   LDA TARGET_PLAY_CODE_ADDR+1                 ;
   STA (CURRENT_TARGET_ADDR),Y                 ;
 

@set_ball_collision_status_players:

	SET_RAM_Y_INDEX[start_length_value] PLAYERS_CAN_COLLIDE_BALL_PASS, $04, NO_PASS_COLLISION_STATUS
	RTS                                         ; RETURN

player_ram_addr		= LOCAL_1
player_pos_id		= LOCAL_7 
player_priority		= LOCAL_8  

											   ; SET JUMP/DIVE CHECK CODE ADDRESS, SET PRIORITY (A=PLAYER ID, X=PRIORITY) 
LOAD_BALL_COLLISION_CHECK_ADDR_SET_PASS_PRIORITY:                                    
   CMP #player_too_far_away	                   ; IS PLAYER STATUS =  TOO FAR, COLLIDED, JUMPING OR DIVING?
   BEQ @exit                                   ; YES-> DON’T UPDATE PLAY COMMAND ADDRESS
   STX player_priority	                       ; SAVE PLAYER PRIORITY
   STA player_pos_id                           ; SAVE PLAYER ID
   JSR GET_P1_P2_RAM_ADDR                      ; GET PLAYER POINTER(A=PLAYER ID) AND SAVE IN 3E 3F()

@save_player_pass_priority:   
   LDA player_priority	                       ; SAVE PRIORITY IN PLAYER RAM 1D
   LDY #PASS_PRIORITY_PLAYER_RAM               ; 
   STA (player_ram_addr),Y                     ; 
   LDA player_pos_id                           ; DOES PLAYER ID = PLAYER PASS TARGET
   LDY #POSITION_ID	                           ; 
   CMP (CURRENT_TARGET_ADDR),Y                 ;
   BEQ @set_target_pass_collision_addr         ; YES->STORE WR JUMP DIVE CHECK IN PASS TARGET COMMAND POINTER
                        
@set_defender_pass_collision_addr               ; STORE DEFENDER JUMP/DIVE CHECK PLAY CODE ADDRESS IN DEFNDER RAM
   LDY #COMMAND_COUNTER                        ; SET COMMAND COUNTER =  1 FRAME
   LDA #$01                                    ; 
   STA (player_ram_addr),Y                     ;
   INY                                         ; SET COMMAND ADDRESS = CHECK FOR DEFENSIVE JUMP AND DIVE() = $8009-1 
   LDA #<(JUMP_DEF_JUMP_DIVE_CHECK_PASS-1)     ; 
   STA (player_ram_addr),Y                     ;
   INY                                         ;
   LDA #>(JUMP_DEF_JUMP_DIVE_CHECK_PASS-1)     ;
   STA (player_ram_addr),Y                     ;
   RTS                                         ;

@set_target_pass_collision_addr:               ; STORE WR JUMP DIVE CHECK IN PASS TARGET COMMAND POINTER
   LDY #COMMAND_COUNTER                        ; SET COMMAND COUNTER =  1 FRAME
   LDA #$01                                    ;
   STA (player_ram_addr),Y                     ; 
   INY                                         ; SET COMMAND ADDRESS = CHECK FOR OFFENSIVE JUMP AND DIVE() = $8006-1
   LDA #<(JUMP_WR_JUMP_DIVE_CHECK_PASS -1)       ;
   STA (player_ram_addr),Y                     ;
   INY                                         ;
   LDA #>(JUMP_WR_JUMP_DIVE_CHECK_PASS -1)       ;
   STA (player_ram_addr),Y                     ;
@exit:                                         ; DON’T UPDATE PLAY COMMAND ADDRESS
   RTS                                         ; RETURN

SORT_DEFENDER_DISTANCES[num_defenders]:        ; BUBBLE SORT DOWN PLAYER LIST AND SAVE DISTANCE AND PLAYER ID (Y=NUMBER OF PLAYERS TO sort +1)
   LDX #$02                                    ;
@sort:                                         ; 
   LDA DEFENDER_DISTANCES_PASS-1,X             ;
   STA DEFENDER_DISTANCES_PASS,X               ;
   LDA DEFENDER_POS_IDS_PASS-1,X               ;
   STA DEFENDER_POS_IDS_PASS,X                 ;
   DEX                                         ;
   DEY                                         ;
   BNE @sort                                   ;

SET_DEF_DIST_POS_ID_PASS:                      ; SAVE 3RD FARTHEST PLAYER DISTANCE AND 3RD FARTHEST POSITION ID()
   LDA temp_defender_distance                  ;
   STA DEFENDER_DISTANCES_PASS,X               ;
   LDY #POSITION_ID                            ;
   LDA (player_ram_addr),Y                     ;
   STA DEFENDER_POS_IDS_PASS,X                 ;
   RTS                                         ; RETURN

_F}_SET_PLAYERS_CLOSE_TO_PASS 
   
_F{_UPDATE_SCROLL_LIMITS					   ; DONE
 
scroll_offset	= LOCAL_8 
  
UPDATE_FIELD_SCROLL_SETTINGS:                  ; UPDATE SCROLL WINDOW VARS (X=LEFT SCROLL, Y=RIGHT SCROLL, A= XSCROLL) AND START DRAW GAME FIELD
   STX LEFT_SCROLL_LIM_FIELD                   ; SET  LEFT SCROLL THRESHOLD = NEW LEFT THRESHOLD VALUE
   STY RIGHT_SCROLL_LIM_FIELD                  ; SET  RIGHT SCROLL THRESHOLD = NEW RIGHT THRESHOLD VALUE
   STA scroll_offset                           ;
   LDA LOS_X                                   ; X SCROLL LOCATION = LOS -  VALUE 
   SEC                                         ;
   SBC scroll_offset                           ;
   STA CURR_X_SCROLL                           ;
   LDA LOS_X +1                                ;
   SBC #$00                                    ;
   STA CURR_X_SCROLL + 1                       ;

_F}_UPDATE_SCROLL_LIMITS 

_F{_START_DRAW_GAME_FIELD					   ; DONE	

START_DRAW_GAME_FIELD_TASK:                    ; START DRAW GAME FIELD TASK
   LDA #DRAW_GAME_FIELD_BANK                   ; SET TASK 2 A000 BANK = BANK 22
   STA GAMEFIELD_TASK + TASK_BANK_A000         ;
   LDY #<(JUMP_DRAW_GAMEFIELD -1)              ; SET ADDR = UPDATE GAME FIELD
   LDA #>(JUMP_DRAW_GAMEFIELD -1)              ;
   LDX #GAMEFIELD_TASK                         ; SET TASK INDEX = UPDATE GAME FIELD TASK  = TASK 2
   JSR CREATE_TASK                             ; CREATE TASK(Y,A= ADDRESS, X= TASK INDEX)
   RETURN_IN_NUM_FRAMES 4
   RTS                                         ; RETURN

_F}_START_DRAW_GAME_FIELD 

_F{_UPDATE_STATS								 ; DONE	

possession_bitmask	= $1F					   ; masks out possession info 
   
UPDATE_IN_GAME_TEAM_PLAYER_STATS:              ; UPDATE SRAM TEAM/PLAYER STATS
   LDA POSSESSION_STATUS                       ; LOAD POSSESSION STATUS
   BPL @check_P1_fumble_onsides_recovery       ; P1-> CHECK FOR P1 STATS TO UPDATE START()
   JMP UPDATE_IN_GAME_P2_STATS_START           ; P2-> CHECK FOR P2 STATS TO UPDATE START()

@check_P1_fumble_onsides_recovery:             ; CHECK FOR P1 STATS TO UPDATE START()
   IF_BALL_RECOVERED[ADDR] P1_STAT_EXIT_1      ; DOES SPECIAL BALL STATUS = FUMBLE RECOVERY 	
   
@check_P1_fumble:
   IF_BALL_FUMBLED[ADDR]  P1_STAT_EXIT_1 

@check_P1_kickoff:  
	IF_PLAY_NOT_KICKOFF[ADDR] CHECK_P1_PUNT
    JMP UPDATE_P1_KR_STATS                     ; YES-> CALCULATE P1 KICK OFF STATS

CHECK_P1_PUNT:                                 ; P1 CHECK FOR PUNT
   CMP #$0C                                    ; WAS PLAY PUNT?
   BNE @check_P1_FG:                           ; NO->P1 CHECK FOR FG  
   JMP UPDATE_P2_PR_STATS                    ; YES->UPDATE P2 PR STATS

@check_P1_FG:                                  ; P1 CHECK FOR FG
   CMP #$10                                    ; WAS PLAY FG?
   BNE @check_P1_XP                            ; NO->P1 CHECK FOR XP 
   JMP UPDATE_P1_FG_STATS                   ; YES->UPDATE P1 FG STATS
   
@check_P1_XP:                                 ; P1 CHECK FOR XP
   CMP #$14                                    ; WAS PLAY FG?
   BNE @check_P1_Pass_Att                      ; NO-> CHECK FOR P1 PASS ATTEMPT 
   JMP UPDATE_P1_XP_STATS                      ; YES-> UPDATE P1 XP STATS

@check_P1_Pass_Att:                            ; CHECK FOR P1 PASS ATTEMPT
   IF_PASS_NOT_ATTEMPTED[ADDR] CHECK_P1_QB_RUSH_SACK

   IF_PASS_INTERCEPTED[ADDR] UPDATE_QB_STATS_P1_PASS_INTERCEPTED 

@update_P1_Pass_Att:  
   JSR CALCULATE_PLAY_DISTANCE_P1             ; CALCULATE DISTANCE FROM INIT LOC(LEFT)  – CUR LOCATION (RIGHT)()
   LDX #UPDATE_QB_PASS_ATT_IN_GAME_INDEX      ; SET STAT INDEX = QB PASS ATT
   JSR UPDATE_P1_QB_STATS[stat]                ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   
   IF_PASS_NOT_COMPLETED[ADDR] P1_STAT_EXIT_1

@update_p1_passing_stats: 
   LDX #UPDATE_QB_PASS_COMP_IN_GAME_INDEX                                    ; SET STAT INDEX = QB PASS COMP
   JSR UPDATE_P1_QB_STATS[stat]                               ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_QB_PASS_YDS_IN_GAME_INDEX                                    ; SET STAT INDEX = QB PASS YARDS
   JSR UPDATE_P1_QB_STATS[stat]                               ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_REC_IN_GAME_INDEX                                    ; SET STAT INDEX = SKILL PLAYER REC
   JSR UPDATE_P1_NON_QB_STATS[stat]                               ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   LDX #UPDATE_REC_YDS_IN_GAME_INDEX                                    ; SET STAT INDEX = SKILL PLAYER YARDS
   JSR UPDATE_P1_NON_QB_STATS[stat]                               ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P1_STAT_EXIT_1

@add_td_to_qb_wr: 
   LDX #UPDATE_QB_PASS_TD_IN_GAME_INDEX	                                    ; SET STAT INDEX = QB PASS TD
   JSR UPDATE_P1_QB_STATS[stat]                               ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_REC_TD_IN_GAME_INDEX	                                    ; SET STAT INDEX = SKILL PLAYER REC TD
   JMP UPDATE_P1_NON_QB_STATS[stat]                               ;UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)

P1_STAT_EXIT_1:                                          ; NO MORE STATS TO UPDATE
   RTS

CHECK_P1_QB_RUSH_SACK:                        ; P1 CHECK FOR RUSH ATTEMPT/SACK

   IF_QB_SACKED[ADDR] UPDATE_P2_DEFENDER_SACKS
  
@get_qb_rush_yards:  
   JSR CALCULATE_PLAY_DISTANCE_P1              ; CALCULATE DISTANCE FROM INIT LOC(LEFT)  – CUR LOCATION (RIGHT)()
   MMC3_SRAM_WRITE_ENABLE
   INC P1_GM_RUSHES                            ; P1 TEAM RUSHES++
   MMC3_SRAM_WRITE_DISABLE
   LDA P1_MAN_POS_ID_ICON_STATUS               ; WAS RUNNER QB?
   AND #possession_bitmask	                                    ;
   BNE @update_P1_non_QB_rushing_stats         ; NO->UPDATE NON QB RUNNING STATS
   
 @update_P1_QB_rushing_att_yards:
   LDX #UPDATE_QB_RUSH_ATT_IN_GAME_INDEX                                    ; SET STAT INDEX = QB RUSHING ATT
   JSR UPDATE_P1_QB_STATS[stat]                               ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_QB_RUSH_YDS_IN_GAME_INDEX                                    ; SET STAT INDEX = QB RUSHING YD 
   JSR UPDATE_P1_QB_STATS[stat]                               ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P1_RUSHING_STATS_EXIT
 
 @update_P1_QB_rushing_stats:
   LDX #UPDATE_QB_RUSH_TD_IN_GAME_INDEX	                                    ; SET STAT INDEX = QB RUSHING TD
   JMP UPDATE_P1_QB_STATS[stat]                               ; UPDATE P1 QB STATS(X=STAT TO UPDATE)

@update_P1_non_QB_rushing_stats:                                        ; UPDATE NON QB RUNNING STATS
   LDX #UPDATE_RUN_ATT_IN_GAME_INDEX                                    ; SET STAT INDEX = SKILL PLAYER RUSHING ATT
   JSR UPDATE_P1_NON_QB_STATS[stat]                               ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   LDX #UPDATE_RUN_YDS_IN_GAME_INDEX                                   ; SET STAT INDEX = SKILL PLAYER RUSHING YD
   JSR UPDATE_P1_NON_QB_STATS[stat]                               ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P1_RUSHING_STATS_EXIT
   
@update_p1_run_td_stats:   
   LDX #UPDATE_RUN_TD_IN_GAME_INDEX                                    ; SET STAT INDEX = SKILL PLAYER RUSHING TD
   JSR UPDATE_P1_NON_QB_STATS[stat]                               ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   RTS
   
UPDATE_P2_DEFENDER_SACKS:                     ; UPDATE P2 SACKS STATS
   LDX #UPDATE_DEF_SACKS_IN_GAME_INDEX         ; SET STAT INDEX = SACKS
   JSR UPDATE_P2_NON_QB_IN_GM_STATS             ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   
P1_RUSHING_STATS_EXIT:                                       ; EXIT
   RTS                                         ; RETURN

UPDATE_QB_STATS_P1_PASS_INTERCEPTED:           ; UPDATE STATS P1 PASS INTERCEPTED
   JSR CALCULATE_PLAY_DISTANCE_P2              ; CALCULATE DISTANCE FROM INIT LOC(RIGHT)  – CUR LOCATION (LEFT)()  
   LDX #UPDATE_QB_PASS_ATT_IN_GAME_INDEX       ; SET STAT TO UPDATE = QB PASS ATT
   JSR UPDATE_P1_QB_STATS[stat]                ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_QB_PASS_INT_IN_GAME_INDEX       ; SET STAT TO UPDATE = QB INT
   JSR UPDATE_P1_QB_STATS[stat]                ; UPDATE P1 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_DEF_INTS_IN_GAME_INDEX          ; SET STAT TO UPDATE = DEF INT
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   
   IF_PLAY_TOUCHBACK[ADDR] P1_PASS_INT_STATS_EXIT
   
@update_p2_int_return_yards:
   LDX #UPDATE_DEF_INT_YDS_IN_GAME_INDEX       ; SET STAT TO UPDATE = DEF INT YARDS
   JSR UPDATE_P2_NON_QB_IN_GM_STATS           ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P1_PASS_INT_STATS_EXIT

@update_p2_int_tds: 
   LDX #UPDATE_DEF_INT_TDS_IN_GAME_INDEX       ; SET STAT TO UPDATE = DEF TD
   JMP UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   
P1_PASS_INT_STATS_EXIT:                                       ; EXIT 
   RTS                                         ; RETURN

UPDATE_P1_KR_STATS:                           ; UPDATE P1 KR STATS
   IF_KICK_NOT_ONSIDES_LONG_VERSION[ADDR] SET_P1_KR_STATS

@check_p2_recovery:
   IF_P2_HAS_BALL[ADDR] P1_KICKING_STATS_EXIT   

 @check_who_recovered_onsides				   ; *** not possible to reach hear*** 
   LDA P1_MAN_POS_ID_ICON_STATUS               ; DID QB RECOVER ONSIDES
   AND #possession_bitmask	                   ;
   BEQ P1_KICKING_STATS_EXIT                   ; YES->EXIT
   CMP #$06                                    ; DID OL RECOVER ONSIDES
   BCS P1_KICKING_STATS_EXIT                   ; YES->EXIT

SET_P1_KR_STATS:                               ; SET P1 KR STATS
   JSR CALCULATE_PLAY_DISTANCE_P1              ; CALCULATE DISTANCE FROM INIT LOC(LEFT)  – CUR LOCATION (RIGHT)()
   LDX #UPDATE_KR_ATT_IN_GAME_INDEX            ; SET STAT TO UPDATE = KR ATT
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   LDX #UPDATE_KR_YDS_IN_GAME_INDEX	           ; SET STAT TO UPDATE = KR YARDS
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P1_KICKING_STATS_EXIT
   
@update_P1_KR_TD_stats:
   LDX #UPDATE_KR_TDS_IN_GAME_INDEX            ; SET STAT TO UPDATE = KR TD
   JMP UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   
P1_KICKING_STATS_EXIT:                          ; EXIT
   RTS                                         ; RETURN

UPDATE_P2_PR_STATS:                           ; UPDATE P2 PR STATS
   IF_PLAY_TOUCHBACK[ADDR] P2_PR_STAT_EXIT
  
   IF_BALL_NOT_KICKED[ADDR] P2_PR_STAT_EXIT 
   
@update_p2_punt_returner_stats:   
   JSR CALCULATE_PLAY_DISTANCE_P2                               ; CALCULATE DISTANCE FROM INIT LOC(RIGHT)  – CUR LOCATION (LEFT)()
   LDX #UPDATE_PR_ATT_IN_GAME_INDEX	                                    ; SET STAT TO UPDATE = PR ATT
   JSR UPDATE_P2_NON_QB_IN_GM_STATS                               ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   LDX #UPDATE_PR_YDS_IN_GAME_INDEX	                                    ; SET STAT TO UPDATE = PR YARDS
   JSR UPDATE_P2_NON_QB_IN_GM_STATS                               ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P2_PR_STAT_EXIT 
   
@update_p2_punt_returner_td_stats:   
   LDX #UPDATE_PR_TDS_IN_GAME_INDEX	                                    ; SET STAT TO UPDATE = PR TD
   JSR UPDATE_P2_NON_QB_IN_GM_STATS                               ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   
P2_PR_STAT_EXIT:                                    
   RTS                                         ; RETURN

UPDATE_P1_FG_STATS:                            ; UPDATE P1 FG STATS

   IF_BALL_NOT_KICKED[ADDR] P1_FG_STAT_EXIT

@update_P1_FG_Att_stat:    
   LDX #UPDATE_FG_ATT_IN_GAME_INDEX	                                   
   JSR UPDATE_P1_NON_QB_STATS[stat]            
   
   IF_KICK_NOT_MADE[ADDR] P1_FG_STAT_EXIT
   
@update_P1_FG_Made_stat:   
   LDX #UPDATE_FG_MADE_IN_GAME_INDEX                                   
   JSR UPDATE_P1_NON_QB_STATS[stat]                               
   
P1_FG_STAT_EXIT:                                       
   RTS                                         

UPDATE_P1_XP_STATS:                          

   IF_BALL_NOT_KICKED[ADDR] P1_XP_STAT_EXIT
   
@update_P1_XP_Att_stat:
   LDX #UPDATE_XP_ATT_IN_GAME_INDEX	                                   
   JSR UPDATE_P1_NON_QB_STATS[stat]
   
   IF_KICK_NOT_MADE[ADDR] P1_XP_STAT_EXIT
   LDX #UPDATE_XP_MADE_IN_GAME_INDEX                                      
   JSR UPDATE_P1_NON_QB_STATS[stat] 
   
P1_XP_STAT_EXIT:                               
   RTS                                        


UPDATE_IN_GAME_P2_STATS_START:                 ; UPDATE PLAYER 2 STATS()
   IF_BALL_RECOVERED[ADDR] P2_STAT_EXIT_1

@check_P2_fumble:   
   IF_BALL_FUMBLED[ADDR]  P2_STAT_EXIT_1 
  
@check_P2_kickoff:    
   IF_PLAY_NOT_KICKOFF[ADDR] CHECK_P2_PUNT
   JMP CHECK_UPDATE_P2_KR_STATS               ; YES-> UPDATE P1 KICK OFF STATS

CHECK_P2_PUNT:                                 ; P2 CHECK FOR PUNT
   CMP #$0C                                    ; WAS PLAY PUNT?
   BNE @check_P2_FG                            ; NO->P2 CHECK FOR FG 
   JMP UPDATE_P1_PR_STATS                      ; YES->UPDATE P1 PR STATS

@check_P2_FG:                                  ; P2 CHECK FOR FG
   CMP #$10                                    ; WAS PLAY FG/XP?
   BNE @check_P2_XP                            ; NO->P2 CHECK FOR XP 
   JMP UPDATE_P2_FG_STATS                      ; YES->UPDATE P2 FG STATS

@check_P2_XP:                                  ; P2 CHECK FOR XP
   CMP #$14                                    ; WAS PLAY XP?
   BNE @check_P2_pass_attempt                  ; NO-> CHECK FOR P2 PASS ATTEMPT 
   JMP UPDATE_P2_XP_STATS                       ; YES-> UPDATE P2 XP STATS

@check_P2_pass_attempt:                        ; CHECK FOR P2 PASS ATTEMPT

   IF_PASS_NOT_ATTEMPTED[ADDR] CHECK_P2_QB_RUSH_SACK
   
   IF_PASS_INTERCEPTED[ADDR] UPDATE_QB_STATS_P2_PASS_INTERCEPTED 

@update_p2_passing_att_stats: 
   JSR CALCULATE_PLAY_DISTANCE_P2              ; CALCULATE DISTANCE FROM INIT LOC(RIGHT)  – CUR LOCATION (LEFT)()
   LDX #UPDATE_QB_PASS_ATT_IN_GAME_INDEX       ; SET STAT INDEX = QB PASS ATT
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]            ; UPDATE P2 QB STATS(X=STAT TO UPDATE)
   
   IF_PASS_NOT_COMPLETED[ADDR] P2_STAT_EXIT_1
 
@update_p2_passing_stats:  
   LDX #UPDATE_QB_PASS_COMP_IN_GAME_INDEX                                
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]                               
   LDX #UPDATE_QB_PASS_YDS_IN_GAME_INDEX                                   
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]                              
   LDX #UPDATE_REC_IN_GAME_INDEX                                   
   JSR UPDATE_P2_NON_QB_IN_GM_STATS                              
   LDX #UPDATE_REC_YDS_IN_GAME_INDEX                            
   JSR UPDATE_P2_NON_QB_IN_GM_STATS 
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P2_STAT_EXIT_1
 
@add_td_to_p2qb_p2wr: 
   LDX #UPDATE_QB_PASS_TD_IN_GAME_INDEX	                                    ; SET STAT INDEX = QB PASS TD
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]                               ; UPDATE P2 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_REC_TD_IN_GAME_INDEX	                                    ; SET STAT INDEX = SKILL PLAYER REC TD
   JMP UPDATE_P2_NON_QB_IN_GM_STATS                               ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   
P2_STAT_EXIT_1:                                     ; P2 NO MORE STATS TO UPDATE
   RTS                                         ; RETURN

CHECK_P2_QB_RUSH_SACK:                        ; P2 CHECK FOR RUSH ATTEMPT/SACK
   IF_QB_SACKED[ADDR] UPDATE_P1_DEFENDER_SACK_STATS

@get_qb2_rush_yards:   
   JSR CALCULATE_PLAY_DISTANCE_P2              ; CALCULATE DISTANCE FROM INIT LOC(RIGHT)  – CUR LOCATION (LEFT)()
   MMC3_SRAM_WRITE_ENABLE
   INC P2_GM_RUSHES                            ; P2 TEAM RUSHES++
   MMC3_SRAM_WRITE_DISABLE
   LDA P2_MAN_POS_ID_ICON_STATUS               ; WAS RUNNER QB?
   AND #possession_bitmask	                   ;
   BNE @update_P2_non_QB_rushing_stats                              
   LDX #UPDATE_QB_RUSH_ATT_IN_GAME_INDEX
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]                
   LDX #UPDATE_QB_RUSH_YDS_IN_GAME_INDEX       
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P2_RUSHING_STATS_EXIT
   
@update_P2_QB_rushing_td_stats:   
   LDX #UPDATE_QB_RUSH_TD_IN_GAME_INDEX        
   JMP UPDATE_P2_QB_IN_GM_STATS[stat]            

@update_P2_non_QB_rushing_stats:                                       ; UPDATE P2 NON QB RUNNING STATS     
   LDX #UPDATE_RUN_ATT_IN_GAME_INDEX                                    ; SET STAT INDEX = SKILL PLAYER RUSHING ATT
   JSR UPDATE_P2_NON_QB_IN_GM_STATS                               ; UPDATE P2 NON-QB STATS (X = STAT TO UPDATE)
   LDX #UPDATE_RUN_YDS_IN_GAME_INDEX		                                    ; SET STAT INDEX = SKILL PLAYER RUSHING YD
   JSR UPDATE_P2_NON_QB_IN_GM_STATS                               ; UPDATE P2 NON-QB STATS (X = STAT TO UPDATE)  
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P2_RUSHING_STATS_EXIT
   
@update_P2_run_td_stats:  
   LDX #UPDATE_RUN_TD_IN_GAME_INDEX            ; SET STAT INDEX = SKILL PLAYER RUSHING TD
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE P2 NON-QB STATS (X = STAT TO UPDATE)
   RTS                                         ; RETURN

UPDATE_P1_DEFENDER_SACK_STATS:                 ; UPDATE P1 SACKS STATS
   LDX #UPDATE_DEF_SACKS_IN_GAME_INDEX         ; SET STAT INDEX = SACKS
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   
P2_RUSHING_STATS_EXIT:                                
   RTS                                      

UPDATE_QB_STATS_P2_PASS_INTERCEPTED:           ; UPDATE STATS P2 PASS INTERCEPTED
   JSR CALCULATE_PLAY_DISTANCE_P1              ; CALCULATE DISTANCE FROM INIT LOC(LEFT)  – CUR LOCATION (RIGHT)()
   LDX #UPDATE_QB_PASS_ATT_IN_GAME_INDEX                                   ; SET STAT TO UPDATE = QB PASS ATT
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]                ; UPDATE P2 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_QB_PASS_INT_IN_GAME_INDEX                                    ; SET STAT TO UPDATE = QB INT
   JSR UPDATE_P2_QB_IN_GM_STATS[stat]                ; UPDATE P2 QB STATS(X=STAT TO UPDATE)
   LDX #UPDATE_DEF_INTS_IN_GAME_INDEX                                    ; SET STAT TO UPDATE = DEF INT
   JSR UPDATE_P1_NON_QB_STATS[stat]             ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   
   IF_PLAY_TOUCHBACK[ADDR] P2_PASS_INT_STATS_EXIT 
   
@update_p1_int_return_yards:
   LDX #UPDATE_DEF_INT_YDS_IN_GAME_INDEX       ; SET STAT TO UPDATE = DEF INT YARDS
   JSR UPDATE_P1_NON_QB_STATS[stat]             ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   
  IF_PLAY_NOT_TOUCHDOWN[ADDR] P2_PASS_INT_STATS_EXIT
  
@update_p1_int_tds:   
   LDX #UPDATE_DEF_INT_TDS_IN_GAME_INDEX                               
   JMP UPDATE_P1_NON_QB_STATS[stat]                               
   
P2_PASS_INT_STATS_EXIT:                            
   RTS                                        
   

CHECK_UPDATE_P2_KR_STATS:                      ; CHECK FOR UPDATE P2 KR STATS
   
@check_for_onsides   
   IF_KICK_NOT_ONSIDES_LONG_VERSION[ADDR] SET_P2_KR_STATS
  
   IF_P1_HAS_BALL[ADDR] P2_KICKING_STATS_EXIT 
												; *** not possible to reach here*** 
   LDA P2_MAN_POS_ID_ICON_STATUS               ; DID QB RECOVER ONSIDES
   AND #possession_bitmask                     ;
   BEQ P2_KICKING_STATS_EXIT                   ; YES->EXIT
   CMP #$06                                    ; DID OL RECOVER ONSIDES
   BCS P2_KICKING_STATS_EXIT                   ; YES->EXIT

SET_P2_KR_STATS:                                      
   JSR CALCULATE_PLAY_DISTANCE_P2                             
   LDX #UPDATE_KR_ATT_IN_GAME_INDEX	                                  
   JSR UPDATE_P2_NON_QB_IN_GM_STATS                               
   LDX #UPDATE_KR_YDS_IN_GAME_INDEX                                  
   JSR UPDATE_P2_NON_QB_IN_GM_STATS  
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P2_KICKING_STATS_EXIT
   
@update_P2_KR_TD_stats:  
   LDX #UPDATE_KR_TDS_IN_GAME_INDEX	            
   JMP UPDATE_P2_NON_QB_IN_GM_STATS             
   
P2_KICKING_STATS_EXIT:                                    
   RTS                                         ; RETURN

UPDATE_P1_PR_STATS:                           ; UPDATE P1 PR STATS
   IF_PLAY_TOUCHBACK[ADDR] P1_PR_STAT_EXIT
   
   IF_BALL_NOT_KICKED[ADDR] P1_PR_STAT_EXIT 
 
   JSR CALCULATE_PLAY_DISTANCE_P1              ; CALCULATE DISTANCE FROM INIT LOC(LEFT)  – CUR LOCATION (RIGHT)()
   LDX #UPDATE_PR_ATT_IN_GAME_INDEX            ; SET STAT TO UPDATE = PR ATT
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   LDX #UPDATE_PR_YDS_IN_GAME_INDEX            ; SET STAT TO UPDATE = PR YARDS
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   
   IF_PLAY_NOT_TOUCHDOWN[ADDR] P1_PR_STAT_EXIT
   
@update_p1_punt_returner_td_stats:    
   LDX #UPDATE_PR_TDS_IN_GAME_INDEX	           ; SET STAT TO UPDATE = PR TD
   JSR UPDATE_P1_NON_QB_STATS[stat]            ; UPDATE P1 NON-QB PLAYER STATS(X= STAT TO UPDATE)
   
P1_PR_STAT_EXIT::                              ; EXIT
   RTS                                         ; RETURN

UPDATE_P2_FG_STATS:                            ; UPDATE P2 FG STATS
   IF_BALL_NOT_KICKED[ADDR] P2_FG_STATS_EXIT

@update_p2_fg_att:   
   LDX #UPDATE_FG_ATT_IN_GAME_INDEX		       ; SET STAT TO UPDATE = FG ATT
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   IF_KICK_NOT_MADE[ADDR] P2_FG_STATS_EXIT

@update_p2_fg_made:   
   LDX #UPDATE_FG_MADE_IN_GAME_INDEX	        ; SET STAT TO UPDATE = FG MADE
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)

P2_FG_STATS_EXIT:                               ; EXIT
   RTS                                         ; RETURN

UPDATE_P2_XP_STATS:                            ; UPDATE P1 XP STATS
   IF_BALL_NOT_KICKED[ADDR] P2_XP_STATS_EXIT

@update_p2_xp_att:   
   LDX #UPDATE_XP_ATT_IN_GAME_INDEX	           ; SET STAT TO UPDATE = XP ATT
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)
   IF_KICK_NOT_MADE[ADDR] P2_XP_STATS_EXIT

@update_p2_xp_made:    
   LDX #UPDATE_XP_MADE_IN_GAME_INDEX            ; SET STAT TO UPDATE = XP MADE
   JSR UPDATE_P2_NON_QB_IN_GM_STATS            ; UPDATE NON QB P2 STATS (X = STAT TO UPDATE)

P2_XP_STATS_EXIT:                              ; EXIT
   RTS                                         ; RETURN

UPDATE_P1_QB_STATS[stat]:                      ; UPDATE P1 QB STATS()
   LDA #P1_ON_OFFENSE                          ; SET ACTIVE TEAM = P1
   STA TEAM_ON_OFFENSE                         ; 
   LDA P1_TEAM	                               ; IS P1 TEAM = PRO BOWL TEAM?
   CMP #AFC_TEAM_ID	                            ; 
   BCS @set_starter_to_qb                       ; YES->SET STARTER TO QB AND JUMP TO UPDATE P1 UPDATE P1 PROBOWL PLAYER STATS

@load_current_starting_qb:   
   LDA P1_PLAYER_CURRENT_QB_ROSTER_ID          ; CURRENT QB STARTER
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID   ; JUMP->UPDATE IN GAME STATS(A= PLAYER ID, X= STAT TO UPDATE)

@set_starter_to_qb:                            ; SET STARTER TO QB AND JUMP TO UPDATE P1 UPDATE P1 PROBOWL PLAYER STATS 
   LDA #QB_POSITION_ID                         ; SET PLAYER ID = QB
   JMP UPDATE_P1_PROBOWL_STATS                 ; JUMP->UPDATE P1 PROBOWL PLAYER STATS

UPDATE_P1_NON_QB_STATS[stat]:                  ; UPDATE P1 NON-QB PLAYER STATS() 
   LDA #P1_ON_OFFENSE                          ; SET ACTIVE TEAM = P1
   STA TEAM_ON_OFFENSE                         ; 
   LDA P1_TEAM	                               ; IS P1 TEAM = PRO BOWL TEAM?
   CMP #AFC_TEAM_ID	                           ; 
   BCS @mask_possession_info                   ; YES->UPDATE P1 PRO_BOWL STATS
   LDA P1_MAN_POS_ID_ICON_STATUS               ; LOAD P1 ICON PLAYER ID
   AND #possession_bitmask	                   ; MASK POSSESSION INFO
   ASL                                         ; SHIFT FOR TWO BYTES INDEX
   TAY                                         ; 
   LDA P1_PLAYER_ROSTER_ID,Y                   ; LOAD P1 ACTUAL STARTER
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID  ; JUMP->UPDATE IN GAME STATS(A= PLAYER ID, X= STAT TO UPDATE)

@mask_possession_info:                         ; UPDATE P1 PRO_BOWL STATS
   LDA P1_MAN_POS_ID_ICON_STATUS               ; LOAD P1 ICON PLAYER ID
   AND #possession_bitmask	                   ; MASK POSSESSION INFO

UPDATE_P1_PROBOWL_STATS:                       ; UPDATE P1 PROBOWL PLAYER STATS 
   CMP #C_POSITION_ID                          ; IS PLAYER OFFENSIVE LINEMAN 
   BCS @update_p1_PB_exit                      ; YES->EXIT 
   TAY                                         ;
   LDA P1_TEAM	                               ; IS P1 TEAM = NFC PRO BOWL TEAM?
   CMP #NFC_TEAM_ID	                           ; 
   BCS @load_nfc_starter_roster_id             ; YES-> LOAD NFC STARTER FOR STAT UPDATE
 
@load_afc_starter_roster_id:
   LDA SRAM_AFC_STARTERS,Y                     ; LOAD AFC PROBOWL ACTUAL STARTER
   LSR                                         ; SHIFT SINCE EACH STARTER IS TWO BYTES (POS ID + TEAM ID) 
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID                                  ; JUMP->UPDATE IN GAME STATS(A= PLAYER ID, X= STAT TO UPDATE)

@load_nfc_starter_roster_id:                   ; LOAD NFC STARTER FOR STAT UPDATE
   LDA SRAM_NFC_STARTERS,Y                     ; LOAD NFC PROBOWL ACTUAL STARTER
   LSR                                         ; SHIFT SINCE EACH STARTER IS TWO BYTES (POS ID + TEAM ID)
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID                                  ; JUMP->UPDATE IN GAME STATS(A= PLAYER ID, X= STAT TO UPDATE)

 @update_p1_PB_exit:                            ; EXIT
   RTS                                         ; RETURN

UPDATE_P2_QB_IN_GM_STATS[stat]:                      ; UPDATE P2 QB STATS()
   LDA #P2_ON_OFFENSE                                    ; SET CURRENT TEAM = P2
   STA TEAM_ON_OFFENSE                         ;
   LDA P2_TEAM	                               ; IS P2 TEAM = PRO BOWL TEAM?
   CMP #AFC_TEAM_ID	                           ; 
   BCS @set_starter_to_qb                      ; YES-> SET STARTER TO QB AND JUMP TO P1 PROBOWL PLAYER STATS
   LDA P2_PLAYER_CURRENT_QB_ROSTER_ID          ; LOAD CURRENT QB STARTER
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID                                

@set_starter_to_qb:                           ; SET STARTER TO QB AND JUMP TO UPDATE P1 PROBOWL PLAYER STATS
   LDA #QB_POSITION_ID                         ;
   JMP UPDATE_P2_PROBOWL_STATS                 ; JUMP->UPDATE P2 PROBOWL PLAYER STATS


UPDATE_P2_NON_QB_IN_GM_STATS:                  ; UPDATE NON-QB P2 PLAYER STATS()
   LDA #P2_ON_OFFENSE                          ; SET CURRENT TEAM = P2
   STA TEAM_ON_OFFENSE                         ;
   LDA P2_TEAM	                               ; IS P2 TEAM = PRO BOWL TEAM?
   CMP #AFC_TEAM_ID                            ;
   BCS @mask_possession_info                   ; YES->P2 TEAM IS PROBOWL
   LDA P2_MAN_POS_ID_ICON_STATUS               ; LOAD PLAYER 2 ICON
   AND #possession_bitmask                     ; MASK OUT POSSESSION INFO
   ASL                                         ;
   TAY                                         ;
   LDA P2_PLAYER_ROSTER_ID,Y                    ; LOAD P2 ROSTER ID
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID                                   

@mask_possession_info:                          ; P2 TEAM IS PROBOWL
   LDA P2_MAN_POS_ID_ICON_STATUS               ; LOAD P2 ICON PLAYER ID
   AND #$1F                                    ; MASK OUT POSSESSION INFO
   
UPDATE_P2_PROBOWL_STATS:                       ; UPDATE P2 PROBOWL PLAYER STATS (X= stat to update)
   CMP #C_POSITION_ID                          ; IS PLAYER OFFENSIVE LINEMAN
   BCS @p2_pro_bowl_exit                       ; YES->EXIT
   TAY                                         ;
   LDA P2_TEAM	                               ; IS P1 TEAM = NFC PRO BOWL TEAM?
   CMP #NFC_TEAM_ID	                           ;
   BCS @load_p2_nfc_roster_id:                 ; YES->LOAD P2 NFC STARTER FOR STAT UPDATE
   
@load_p2_afc_roster_id:   
   LDA SRAM_AFC_STARTERS,Y                     ; LOAD P2 AFC PRO BOWL ROSTER ID
   LSR                                         ;
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID                                    

@load_p2_nfc_roster_id:                        ; LOAD P2 NFC STARTER FOR STAT UPDATE
   LDA SRAM_NFC_STARTERS,Y                     ;
   LSR                                         ;
   JMP SWAP_AND_ADD_STATS_TO_CURRENT_GAME_IF_VALID                                   
@p2_pro_bowl_exit:                             ; EXIT
   RTS                                         ; RETURN

_F}_UPDATE_STATS   

_F{_CALCULATE_PLAY_DISTANCE 					; DONE
   
CALCULATE_PLAY_DISTANCE_P1:                    ; CALCULATE DISTANCE FROM INIT LOC(LEFT)  – CUR LOCATION (RIGHT)()
   
   screen_distance = LOCAL_7					   ; 16 bit value

   LDA BALL_X+1	                               ; 
   SEC                                         ;
   SBC LOS_X                                   ;
   STA screen_distance                         ;
   LDA BALL_X+2	                               ;
   SBC LOS_X +1                                ;
   STA screen_distance+1                       ;
   JMP convert_screen_distance_to_yards        ; JUMP->CONVERT SCREEN DISTANCE TO ACTUAL YARDS

CALCULATE_PLAY_DISTANCE_P2:                    ; CALCULATE DISTANCE FROM INIT LOC(RIGHT)  – CUR LOCATION (LEFT)()
   LDA LOS_X                                   ;
   SEC                                         ;
   SBC BALL_X+1	                               ;
   STA screen_distance                         ;
   LDA LOS_X +1                                ;
   SBC BALL_X+2	                               ;
   STA screen_distance+1                       ;

convert_screen_distance_to_yards:             ; CONVERT SCREEN DISTANCE TO ACTUAL YARDS
   LDA screen_distance+1                       ; 0X08 = 1YARD 
   AND #$80                                    ;
   BPL @divide                                 ;
   LDA #$FF                                    ; keep sign intact
@divide:                                       ; 2 BYTE DIVIDE BY 8 TO GET YARDS IN DECIMAL VALUE
   LSR                                         ;
   ROR screen_distance+1                       ;
   ROR screen_distance                         ;
   LSR                                         ;
   ROR screen_distance+1                       ;
   ROR screen_distance                         ;
   LSR                                         ;
   ROR screen_distance+1                       ;
   ROR screen_distance                         ;
   RTS                                         ; RETURN

_F}_CALCULATE_PLAY_DISTANCE 

_F{_INJURY_CHECK_NORMAL_AND_SKIP				; DONE

roster_id_bitmask	= $1F
game_type_bit_mask	= $03 
season_game_type	= $02 

P1_TEAM_ID_SRAM		= P1_GM_PLAYERS 
P1_ROSTER_ID_SRAM	= P1_GM_PLAYERS + 1

P2_TEAM_ID_SRAM		= P2_GM_PLAYERS 
P2_ROSTER_ID_SRAM	= P2_GM_PLAYERS + 1

player_roster_id	= LOCAL_7 
injury_chance		= LOCAL_8

 
P1_CHECK_FOR_INJURY_NORM:                      ; P1 NORMAL CHECK FOR INJURY()
   LDY #REGULAR_INJURY_CHANCE                  ; SET P1 REGULAR INJURY CHANCE  = ~1%
   JMP get_actual_p1_starter                  ; JUMP to GET ACUTAL PLAYER 1 STARTER FROM ICON

CHECK_INJURY_KR_PR_P1:                         ; P1 KR/PR CHECK FOR INJURY()
   LDY #KR_PR_INJURY_CHANCE                    ; SET P1 KICK/PUNT INJURY CHANCE   = ~3%

get_actual_p1_starter:                        ; GET ACTUAL P1 STARTER FROM ICON
   LDA P1_MAN_POS_ID_ICON_STATUS               ; LOAD PLAYER 1 ICON
   AND #roster_id_bitmask                      ; MASK OUT POSESSION INFO
   ASL                                         ; SHIFT SINCE TABLE IS TWO BYTES
   TAX                                         ; 
   LDA P1_TEAM_ID_SRAM,X                       ; SET TEAM ID = P1 PLAYER TEAM ID 
   STA CURRENT_TEAM                            ;
   LDA P1_ROSTER_ID_SRAM,X                     ; LOAD ACTUAL PLAYER  FROM CURRENT P1 STARTERS
   JMP INJURY_CHECK_STARTER                    ; JUMP-> INJURY CHECK ROUTINE

P2_CHECK_FOR_INJURY_NORM:                      ; P2 REGULAR CHECK FOR INJURY()
   LDY #REGULAR_INJURY_CHANCE                  ; SET P2 REGULAR INJURY CHANCE  = ~1%
   JMP get_actual_p2_starter                  ; JUMP->GET ACUTAL PLAYER 2 STARTER FROM ICON

CHECK_INJURY_KR_PR_P2:                         ; P2 KR/PR CHECK FOR INJURY()
   LDY #KR_PR_INJURY_CHANCE                     ; SET KR/PR INJURY CHANCE  = ~3%

get_actual_p2_starter:                        ; GET ACTUAL P2 STARTER FROM ICON
   LDA P2_MAN_POS_ID_ICON_STATUS               ; LOAD PLAYER 1 ICON
   AND #roster_id_bitmask                      ; MASK OUT POSESSION INFO
   ASL                                         ; SHIFT SINCE TABLE IS TWO BYTES
   TAX                                         ; 
   LDA P2_TEAM_ID_SRAM,X                     ; SET TEAM ID = P2 PLAYER TEAM ID
   STA CURRENT_TEAM                            ; 
   LDA P2_ROSTER_ID_SRAM,X                     ; LOAD ACTUAL PLAYER  FROM CURRENT P2 STARTERS

INJURY_CHECK_STARTER:                          ; INJURY CHECK ROUTINE
   TAX                                         ; SAVE STARTER in X 
   LDA GAME_STATUS                             ; IS GAME TYPE = SEASON?
   AND #game_type_bit_mask                     ; 
   CMP #season_game_type                       ; 
   BCC NO_INJURY_EXIT                          ; NO->NO INJURY
 
@check_if_ball_player_OOB: 
   IF_BALL_OOB_OR_RECOVERED[ADDR] NO_INJURY_EXIT 

@check_player_vs_injury_rate:   
   TXA                                         ; LOAD STARTER
   AND #roster_id_bitmask                      ; MASK OUT POSSESSION
   STA player_roster_id                        ; SET STARTER = STARTER VALUE
   STY injury_chance                           ; SET INJURY RATE = RATE
   JSR HEAVIER_RANDOMIZE                       ; UPDATE RANDOM 3B, 3C, 3D()            
   CMP injury_chance                           ; RANDOM > INJURY RATE
   BCS NO_INJURY_EXIT                        ; YES-> NO INJURY

@check_for_subs_remaining:					   ;    
   JSR CHECK_IF_PLAYER_CAN_BE_INJURED          ; CHECK IF PLAYER CAN BE INJURED($44=STARTER)
   BCC NO_INJURY_EXIT                        ; NO-> NO INJURY 
   
@do_injury_player_change:   
   JSR DO_PLAYER_CHANGE_FOR_INJURY             ; DO INJURY PLAYER CHANGE()

NO_INJURY_EXIT:                              ; NO INJURY
   RTS                                         ; RETURN

 
p1_has_ball					=   $00
p2_has_ball					=   $FF
current_side_with_ball 		=	LOCAL_7
current_roster_id	   		=	LOCAL_7 
current_player_starter_id 	=	LOCAL_8
 
SKP_VS_SKP_INJURY_START:                       ; SKP VS SKP COM INJURY ROUTINE($44=CURRENT SIDE, $45 =ICON)
   LDA current_player_starter_id                  ; SAVE PLAYER ICON IN P1 ICON AND P2 ICON
   STA P1_MAN_POS_ID_ICON_STATUS               ;
   STA P2_MAN_POS_ID_ICON_STATUS               ;
   AND #roster_id_bitmask                      ; MASK OUT POSSESSION
   ASL                                         ;
   TAY                                         ;
   LDA current_side_with_ball                  ; IS CURRENT PLAYER SIDE = P2 
   BNE @get_p2_teamid_roster_id                ; YES-> SET PLAYER 2 TEAM ID AND PLAYER ID 
 
@get_p1_teamid_roster_id: 
   LDX P1_TEAM_ID_SRAM,Y                       ; SET TEAM ID FROM CURRENT P1 STARTERS 
   LDA P1_ROSTER_ID_SRAM,Y                     ; SET ACTUAL PLAYER  FROM CURRENT P1 STARTERS
   LDY #p1_has_ball                            ; SET_POSSESSION TO P1 
   JMP @check_for_injury                       ;

@get_p2_teamid_roster_id:                      ; SET PLAYER 2 TEAM ID AND PLAYER ID 
   LDX P2_TEAM_ID_SRAM ,Y                      ; SET TEAM ID FROM CURRENT P2 STARTERS 
   LDA P2_ROSTER_ID_SRAM ,Y                   ; SET ACTUAL PLAYER FROM CURRENT P2 STARTERS 
   LDY #p2_has_ball	                           ; SET_POSSESSION TO P2 

@check_for_injury:                             ; 
   STA current_roster_id                       ; SAVE PLAYER ID
   STX CURRENT_TEAM                            ; SET CURRENT TEAM = PLAYERS TEAM
   STY POSSESSION_STATUS                       ; SAVE STARTER INDEX 
   RETURN_IN_NUM_FRAMES 8					   ;
   JSR CHECK_IF_PLAYER_CAN_BE_INJURED          ; CHECK IF PLAYER CAN BE INJURED($44=STARTER $45=INJ RATE)
   BCC @exit                                   ; NO-> EXIT
   LDA CURRENT_WEEK_IN_SEASON                  ; IS CURRENT WEEK > WEEK TO SKIP TO
   CMP WEEK_TO_SKIP_TO                         ; 
   BCC @do_skp_mode_injury                     ; NO->DO AUTO SKIP MODE INJURY

@normal_player_injury_change:
   JSR DO_PLAYER_CHANGE_FOR_INJURY             ; DO INJURY PLAYER CHANGE()
   LDA #SCOREBOARD_NO_QTRS_SCENE_ID	           ; SET BACKGROUND ID = SCOREBOARD
   LDX #LEADERS_SCOREBOARD_SCHED_DRAW_SCRIPT_BANK   ; SET BANK = BANK 8
   JSR DRAW_SCENE_UNTIL_ENDED                  ; START DRAWING TASK AND DO UNTIL THREAD DESTROYED (A= CUTSCENE INDEX, X = BANK)
   LDA #MAIN_GAME_BANK_1                       ; SET BANK = BANK 17
   SET_ADDR_XY[addr] JUMP_UPDATE_LARGE_SCOREBOARD
   JSR SWAP_8000_BANK_AND_JUMP_RETURN          ; SWAP $8000 BANK AND JUMP(X,Y= ADDR, A= BANK)
   RETURN_IN_NUM_FRAMES 8
   RTS                                         ; RETURN

@do_skp_mode_injury:                           ; DO AUTO SKIP MODE INJURY
   LDA #MAIN_GAME_BANK_1                       ; SET BANK = BANK 17
   LDY #<JUMP_PLAYER_INJURED	                   ; SET ADDR = AUTO SKIP INJURY 
   LDX #>JUMP_PLAYER_INJURED	                   ;
   JSR SWAP_8000_BANK_AND_JUMP_RETURN          ; BANK 8000 SWAP AND JUMP (X,Y= ADDR, A= BANK)
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2                                    ; SET BANK =  BANK 20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)
@exit:                                        ; EXIT
   RTS                                         ; RETURN
   
 _F}_INJURY_CHECK_NORMAL_AND_SKIP    

_F{_CHECK_IF_PLAYER_CAN_BE_INJURED  		   ; 


sub_index			= LOCAL_5
current_roster_id	= LOCAL_6
starter_roster_id	= LOCAL_7 
		
LAST_SUB_INDEX			 = $04
INJURED_AND_DOUTBFUL	 = $03
 
CHECK_IF_PLAYER_CAN_BE_INJURED:                ; CHECK IF PLAYER CAN BE INJURED($44=STARTER $45=INJ RATE)
   LDA starter_roster_id              ; IS STARTER = QB
   CMP #$02                                    ; 
   BCS @player_not_qb                          ; NO->PLAYER NOT QB
   EOR #$01                                    ; SWITCH QB STARTER
   JSR CHECK_IF_PLAYER_INJURED                 ; CHECK IF PLAYER IS INJURED(A=PLAYER)
   BCS @no_injury_exit                         ; NO->PLAYER CAN’T BE INJURED
   JMP @player_injured                         ; YES->PLAYER INJURED

@player_not_qb:                                ; PLAYER NOT QB
   LDA #$00                                    ; SET sub_index  = sub 1 = RB3   
   STA sub_index                           	   ;
   LDA #$02                                    ; SET ACTUAL PLAYER = RB1
   STA current_roster_id                       ;
   
@check_all_skill_players:                      ; CHECK ALL SKILL PLAYERS LOOP 
   LDA_ABS_A_ZP current_roster_id              ; LOAD ACTUAL PLAYER
   JSR CHECK_IF_PLAYER_INJURED                 ; CHECK IF PLAYER IS INJURED(A=PLAYER)
   BCC @go_to_next_player                      ; NO-> GO TO NEXT PLAYER
   INC sub_index	                           ; POSITION COUNTER = POSITION COUNTER +1
   
@go_to_next_player:                            ; GO TO NEXT PLAYER
   INC current_roster_id                       ; CURRENT PLAYER ++
   LDA current_roster_id                       ; 
   CMP #LAST_OFFENSIVE_SKILL_PLAYER + 1        ; ALL PLAYERS CHECKED? 
   BCC @check_all_skill_players                ; NO->CHECK ALL SKILL PLAYERS LOOP
   LDA sub_index	                           ; 
   CMP #LAST_SUB_INDEX + 1                     ; DOES CURRENT POSITION = OFFENSIVE LINEMEN
   BCS @no_injury_exit                         ; YES->PLAYER CAN’T BE INJURED

@player_injured:                               ; PLAYER INJURED
   LDA #$01                                    ; SET CLOCK STATUS = INJURY TIME? NOT CHECKED ANYWHERE
   STA CLOCK_RUN_TYPE                          ; 
@set_injury_status:   
   LDA player_roster_id                        ; LOAD ACTUAL STARTER
   LDX #INJURED_AND_DOUTBFUL                   ; SET PLAYER = INJURED AND DOUBTFUL
   JSR SET_INJURY_STATUS                       ; SET PLAYER INJURY STATUS(A= PLAYER, X= INJURY STATUS)
   JSR DO_INJURY_ANIMATION                     ; DO INJURY CUTSCENE()
   LDA TEAM_CONTROL_TYPES                      ; LOAD TEAM CONTROL 
   LDX POSSESSION_STATUS                       ; DOES POSSESSION STATUS = P2 HAS BALL
   BMI @check_player_type                      ; YES->CHECK FOR MAN/COA OR COM CONTROL

@get_p1_control_type:				   
   LOG_SHIFT_RIGHT_4

@check_player_type:                            ; CHECK FOR MAN/COA OR COM CONTROL 
   AND #$0F                                    ; MASK OUT P1 NIBBLE IF NEEDED  
   CMP #$02                                    ; IS PLAYER TYPE =  COM?
   BCS @com_player_injury_exit                 ; YES->PLAYER INJURED. TEAM CONTROL IS COM

@man_coa_player_injury_exit:   
   LDA #$00                                    ; SET CURRENT TEAM CTRL = MAN/COA
   STA INJURY_CONTROL_TYPE                     ;
   SEC                                         ; SET CARRY = PLAYER INJURED
   RTS                                         ;

@com_player_injury_exit:                       ; PLAYER INJURED. TEAM CONTROL IS COM
   LDA #$FF                                    ; SET CURRENT TEAM CTRL = COM CONTROL
   STA INJURY_CONTROL_TYPE                     ;
   SEC                                         ; SET CARRY = PLAYER INJURED
   RTS                                         ; RETURN

@no_injury_exit:                                ; PLAYER CAN’T BE INJURED
   CLC                                         ; CLEAR CARRY = PLAYER CANT BE INJURED
   RTS                                         ; RETURN

 _F}_CHECK_IF_PLAYER_CAN_BE_INJURED   
   
_F{_PLAYER_CHANGE_INJURY  					   ; DONE 
   
DO_PLAYER_CHANGE_FOR_INJURY:                   ; CPU INJURY PLAYER CHANGE
   LDA BALL_X+1	                               ; SAVE CURRENT BALL LOCATION (X) into $7A/7B
   STA TEMP_BALL_LOC                           ;
   LDA BALL_X+2	                               ;
   STA TEMP_BALL_LOC+1                         ;
   LDA #$10                                    ; SET BANK = BANK 17
   LDY #<JUMP_CHANGE_PLAYERS	               ; SET ADDR = COM INJURY ROUTINE POINTER ADDRESS
   LDX #>JUMP_CHANGE_PLAYERS	               ;
   JSR SWAP_8000_BANK_AND_JUMP_RETURN          ; SWAP $8000 BANK AND JUMP(Y,X= ADDRESS, A= BANK TO SWAP TO)
   LDX #HIGH_LEVEL_GAME_LOGIC_BANK_2           ; SET BANK =  BANK 20
   JSR SWAP_A000_BANK                          ; SWAP A000 BANK (X=BANK TO SWAP)     
   JSR SET_BG_SPR_FULLY_FADED_OUT              ; SET PALLETES TO FULLY FADED OUT()
   LDA TEMP_BALL_LOC                           ; RESTORE CURRENT BALL LOCATION (X) from $7A/$7B
   STA BALL_X+1	                               ; 
   LDA TEMP_BALL_LOC+1                         ;
   STA BALL_X+2	                               ;
   RTS                                         ; RETURN

_F}_PLAYER_CHANGE_INJURY
   
.PAD $A000, $00                                ; FILL WITH BLANK SPACE

_F{_CUTSCENE 								   ; DONE
   TEMP_CUT_TABLE_ADDR = LOCAL_7 

DO_CUTSCENE_SAVE_INFO_TO_SRAM:                 ; DO CUTSCENE()
   SEC                                         ; SET TASK STATUS FLAG = BUSY = DON’T DO NORMAL NMI ROUTINE = UPDATE SCREEN ONLY
   ROR TASK_BUSY_FLAG	                       ;
   MMC3_SRAM_WRITE_ENABLE
   TRANSFER_RAM_TO_SRAM_Y_INDEX[source_dest_length] DRAWING_TASK, DRAW_TASK_CUT_SRAM, 5
   MMC3_SRAM_WRITE_DISABLE
   LDX #GAMEFIELD_TASK                         ; SET TASK 2 = UPDATE GAME FIELD TASK
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK INDEX)
   LDX #DRAWING_TASK                           ; SET TASK 4 = DRAWING TASKS
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK INDEX)
   LDX #BANNER_TASK                            ; SET TASK 3 = DRAW BANNER EVENTS, CLOCK/SCORE TASK
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK INDEX)
   LDX #COLLISION_CHECK_TASK                   ; SET TASK 5 = COLLISION CHECK TASK
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK INDEX)
   SUSPEND_TASK_DIRECT[task] UPDATE_PLAYERS_TASK
   SUSPEND_TASK_DIRECT[task] UPDATE_CLOCK_TASK ; SET UPDATE PLAYERS TASK = SUSPENDED
   SUSPEND_TASK_DIRECT[task] DRAW_PLAYERS_TASK 
   LSR TASK_BUSY_FLAG                          ; SET TASK STATUS FLAG = NOT BUSY
   JSR SET_BG_SPR_FULLY_FADED_OUT              ; SET PALLETE TO FADE OUT() 
   RETURN_1_FRAME 
   MMC3_SRAM_WRITE_ENABLE 
                                               ; SAVE SPRITE PPU DATA IN SRAM ($200-$2EF to $6013-$6112)
   TRANSFER_RAM_TO_SRAM_Y_INDEX[source_dest_length] SPRITE_DMA_ADDR, SPRITE_CUT_SRAM, 240
                                               ; SAVE OTHER GAME INFO TO SRAM (PASS TARGETS BALL COLLIDE, CAN COLLIDE) $683-$6F7 to $6103-$6176 
   TRANSFER_RAM_TO_SRAM_Y_INDEX[source_dest_length] PASS_TARGET_INFO, COLL_CUT_SRAM , 116 
                                               ; SAVE IRQ SPLIT INFO
   TRANSFER_RAM_TO_SRAM_Y_INDEX[source_dest_length] IRQ_INFO_START, IRQ_CUT_SRAM  , 19 
                                               ; SAVE SPR CHR BANKS
   TRANSFER_RAM_TO_SRAM_Y_INDEX[source_dest_length] SOFT_CHR_BANKS, SPR_CHR_BANK_CUT_SRAM  , 4 
                                               ;
   TRANSFER_RAM_TO_SRAM_Y_INDEX[source_dest_length] (DRAW_SPRITE_CUTSCENE_STACK -2) , SPR_DRAW_STACK_SRAM  , 28 
                                               ; *** OVERWRITES PART OF SPRITE SRAM BUG ****
   SAVE_16_BIT_ZP_TO_SRAM[zp_sram] CURR_X_SCROLL, X_SCROLL_CUT_SRAM  
   MMC3_SRAM_WRITE_DISABLE                     ;
   LDY #<DEFAULT_IRQ_SPLIT_TABLE               ; SET ADDR = IRQ SPLIT SCREEN ARRAY
   LDX #>DEFAULT_IRQ_SPLIT_TABLE               ; 
   JSR LOAD_IRQ_SPLIT_DATA                     ; DO IRQ SPLIT()
   JSR CLEAR_ALL_NAMETABLES                    ; CLEAR NAMETABLES()
   JSR SET_ALL_SPR_OFFSCREEN                   ; CLEAR SPRITES()

                                               ; DRAW CUTSCENES
   LDA CUTSCENE_TO_DRAW                        ; LOAD CUTSCENE SEQUENCE INDEX
   ASL                                         ; SHIFT SINCE TABLE IS TWO BYTES
   TAX                                         ;
   LDA CUTSCENE_ADDR-2,X                       ; SET CUTSCENE POINTER FROM CUTSCENE POINTER TABLE 
   STA TEMP_CUT_TABLE_ADDR                     ;
   LDA CUTSCENE_ADDR-1,X                       ;
   STA TEMP_CUT_TABLE_ADDR+1                   ;
@do_next_cutscene_in_sequence:                 ; DO CUTSCENES UNTIL REACH FD,FE,FF CUTSCENE
   LDY #$00                                    ; LOAD FIRST CUTSCENE ID FROM CUTSCENE ARRAY
   LDA (TEMP_CUT_TABLE_ADDR),Y                 ;   
   CMP #$FC                                    ; ANY MORE CUTSCENES?
   BCS @cutscene_sequence_done                 ; NO->END OF CUTSCENES
   LDX #CUTSCENE_DRAW_SCRIPT_BANK              ; SET TO BANK 7
   JSR DRAW_SCENE_UNTIL_ENDED                  ; START DRAWING TASK AND DO UNTIL THREAD DESTROYED (A= CUTSCENE INDEX, X = BANK)

   INC TEMP_CUT_TABLE_ADDR                     ; DATA ADDR++
   BNE @jump                                   ;
   INC TEMP_CUT_TABLE_ADDR+1                   ;
@jump:
   JMP @do_next_cutscene_in_sequence          ; JUMP->DO CUTSCENES UNTIL REACH FD,FE,FF CUTSCENE ID

@cutscene_sequence_done:                       ; END OF CUTSCENES
   STA CUTSCENE_TO_DRAW                        ; 
   JSR SET_BG_SPR_FULLY_FADED_OUT              ; SET PALLETES TO FULLY FADED OUT()
   RETURN_1_FRAME 
   JSR CLEAR_ALL_NAMETABLES                    ; CLEAR NAMETABLES()
   JSR SET_ALL_SPR_OFFSCREEN                   ; CLEAR SPRITES()
   MMC3_SRAM_WRITE_ENABLE                      ; SET MMC3 SRAM PROTECT =  WRITEABLE
   TRANSFER_SRAM_TO_RAM_Y_INDEX[source_dest_length] SPRITE_CUT_SRAM, SPRITE_DMA_ADDR, 240
   TRANSFER_SRAM_TO_RAM_Y_INDEX[source_dest_length] COLL_CUT_SRAM, PASS_TARGET_INFO, 116 
   TRANSFER_SRAM_TO_RAM_Y_INDEX[source_dest_length] IRQ_CUT_SRAM, IRQ_INFO_START, 19 
   TRANSFER_SRAM_TO_RAM_Y_INDEX[source_dest_length] SPR_CHR_BANK_CUT_SRAM, SOFT_CHR_BANKS, 4 
   SAVE_16_BIT_SRAM_TO_ZP[sram_zp] X_SCROLL_CUT_SRAM, CURR_X_SCROLL
   LDA #$00                                    ; CLEAR Y SCROLL
   STA CURR_Y_SCROLL                           ;
   STA CURR_Y_SCROLL+1                         ;
   MMC3_SRAM_WRITE_DISABLE  
   SET_SOFT_SPRITE_8_BY_16
   LDA #$00                                    ; SET Y SCROLL= 0
   STA CURR_Y_SCROLL                           ;
   STA CURR_Y_SCROLL+1                         ; 
   STA SOFT_CHR_1000_BANK                      ; SET CHAR $1000 = FIRST CHR BANK
   STA PLAY_COLL_SOUND_STATUS                  ; CLEAR SPRITES COLLIDING 
   STA IRQ_DISABLE_MMC3                        ; CLEAR IRQ DISABLE
   JSR START_DRAW_GAME_FIELD_TASK              ; START DRAW GAME FIELD TASK()
   SEC                                         ;
   ROR TASK_BUSY_FLAG                          ; SET TASK FLAG = BUSY
   LDX #UPDATE_PLAYERS_TASK	                   ; SET TASK INDEX = UPDATE PLAYERS TASK TASK 6
   JSR RESUME_TASK                             ; RESUME TASK(X=TASK INDEX)
   LDX #UPDATE_CLOCK_TASK	                   ; SET TASK INDEX = UPDATE CLOCK TASK TASK 0
   JSR RESUME_TASK                             ; RESUME TASK(X=TASK INDEX)
   LDX #DRAW_PLAYERS_TASK	                   ; SET TASK INDEX = DRAW PLAYERS TASK TASK 7
   JSR RESUME_TASK                             ; RESUME TASK(X=TASK INDEX)

   TRANSFER_SRAM_TO_RAM_Y_INDEX[source_dest_length] SPR_DRAW_STACK_SRAM, (DRAW_SPRITE_CUTSCENE_STACK -2) , 28 
   TRANSFER_SRAM_TO_RAM_Y_INDEX[source_dest_length] DRAW_TASK_CUT_SRAM , DRAWING_TASK,  5

@restart_banner_task:   
   LDA #BANNER_ANIMATION_BANK                  ; SET TASK 3 BANK = 23 
   STA (BANNER_TASK + TASK_BANK)               ;
   LDY #<(JUMP_START_BANNER_TASK-1)	           ; SET ADDR = UPDATE STATUS BAR BANK 23 
   LDA #>(JUMP_START_BANNER_TASK-1)	           ;
   LDX #BANNER_TASK	                           ; SET TASK INDEX = UPDATE TATUS BAR TASK INDEX (TASK 3)
   JSR CREATE_TASK                             ; CREATE TASK(Y,A= ADDRESS, X= TASK INDEX)

@restart_collision_task:   
   LDA #COLLISION_LOGIC_BANK                   ; SET TASK 4 BANK to 23
   STA (COLLISION_CHECK_TASK  + TASK_BANK)     ;
   LDY #<(JUMP_START_COLL_CHECK_TASK -1)       ; SET ADDR = COLLISION CHECK  bank 23
   LDA #>(JUMP_START_COLL_CHECK_TASK -1)       ;
   LDX #COLLISION_CHECK_TASK                   ; SET TASK INDEX =  COLLISION CHECK TASK INDEX (TASK 4)
   JSR CREATE_TASK                             ; CREATE TASK(Y,A= ADDRESS, X= TASK INDEX)
   LSR TASK_BUSY_FLAG                          ; SET TASK FLAG = READY
   RETURN_IN_NUM_FRAMES 2

   @redraw_clock_and_Score
   LDA #REDRAW_CLOCK_SCORE_BANNER_ID           ; SET BANNER ID = REDRAW CLOCK BACKGROUND AND SCORE
   JSR UPDATE_BANNER                           ; UPDATE TOP BANNER(A=BANNER TO DO) 
   
@set_P1_P2_overhead_icons_not_set:   
   SET_P1_MAN_ICON_NOT_SET
   SET_P2_MAN_ICON_NOT_SET
   
@check_for_redraw_down_markers: 
   LDA POSSESSION_STATUS                       ; DOES POSSESSION STATUS = FG, KICKOFF, PUNT, XP
   AND #$1C                                    ; 
   CMP #$10                                    ; DOES POSSESSION STATUS = FG?
   BEQ @update_down_markers_after_cut          ; YES-> UPDATE LOS/FD MARKERS AFTER CUTSCENE 
   CMP #$08                                    ; IS PUNT/XP? 
   BCS @check_for_end_cutscene                 ; YES->CHECK FOR END OF CUTSCENE

@update_down_markers_after_cut:                ; UPDATE LOS/FD MARKERS AFTER CUTSCENE FOR FG,KICKOFF 
   JSR SET_SIDELINE_SCROLL_LOC_UPDATE_DOWN_MARKERS                     ; UPDATE LOS AND FIRST DOWN MARKERS VALUES AND GRAPHICS()

@check_for_end_cutscene:                       ; CHECK FOR END OF CUTSCENE
   LDA CUTSCENE_TO_DRAW                        ; DOES CUTSCENE TO PLAY = END CUTSCENE?
   CMP #$FF                                    ;
   BEQ @set_cutscene_none_exit                 ; YES->SET CUTSCENE TO PLAY TO NONE
   JSR UPDATE_BG_SPR_PAL_SET_TO_FULLY_FADED_IN ; SET BG AND OAM PALLETE TO FULLY FADED IN() 
   RETURN_IN_NUM_FRAMES 2
@set_cutscene_none_exit:                       ; SET CUTSCENE TO PLAY TO NONE
   LDA #$00                                    ; SET CUTSCENE TO PLAY = NONE
   STA CUTSCENE_TO_DRAW                        ;
   RTS                                         ; RETURN

   
   
DO_CUTSCENE_IF_NEEDED:                         ; PLAY CUTSCENE IF NEEDED()
   LDA CUTSCENE_TO_DRAW                        ; CUTSCENE TO PLAY? 
   BEQ @exit                                   ; NO->NO CUTSCENE TO PLAY
   JMP DO_CUTSCENE_SAVE_INFO_TO_SRAM           ; JUMP->DO CUTSCENE()
@exit:                                         ; NO CUTSCENE TO PLAY
   RTS                                         ; RETURN

WAIT_FOR_CUTSCENE_THEN_DO:                     ; WAIT UNTIL THERE IS CUTSCENE TO PLAY (NOT USED)
   RETURN_1_FRAME 
   LDA CUTSCENE_TO_DRAW                        ; CUTSCENE TO PLAY?
   BEQ WAIT_FOR_CUTSCENE_THEN_DO               ; NO-> WAIT UNTIL THERE IS CUTSCENE TO PLAY
   JMP DO_CUTSCENE_SAVE_INFO_TO_SRAM           ; JUMP->DO CUTSCENE()

DRAW_CUTSCENE_PLAY_OVER:                       ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   STA DRAW_SCRIPT_ID	                       ; SAVE CUTSCENE ID 
   SUSPEND_TASK_DIRECT[task] UPDATE_CLOCK_TASK       ;
   LDX #$11                                    ; SET TASK INDEX = SLIDEBAR TASK 
   JSR END_TASK_CHECK_TASK_NOTHING_IN_BUFFERS  ; END TASK(X=TASK TO END)
   JSR SET_BG_SPR_FULLY_FADED_OUT              ; 
   JSR CLEAR_ALL_NAMETABLES                    ; CLEAR NAMETABLES()
   JSR SET_ALL_SPR_OFFSCREEN                   ; CLEAR SPRITES()
   LDA DRAW_SCRIPT_ID	                       ; SET CUTSCENE INDEX = CUTSCENE TO PLAY
   LDX #CUTSCENE_DRAW_SCRIPT_BANK              ; SET BANK =  BANK 7
   JSR DRAW_SCENE_UNTIL_ENDED                  ; START DRAWING TASK AND DO UNTIL THREAD DESTROYED (A= CUTSCENE INDEX, X = BANK) 
   JSR SET_BG_SPR_FULLY_FADED_OUT              ; 
   JSR CLEAR_ALL_NAMETABLES                    ; CLEAR NAMETABLES()
   JSR SET_ALL_SPR_OFFSCREEN                   ; CLEAR SPRITES()
   LDA #$00                                    ; SET Y SCROLL = ZERO
   STA CURR_Y_SCROLL                           ;
   STA CURR_Y_SCROLL+1                         ;
 @restart_banner_task: 
   LDA #BANNER_ANIMATION_BANK                  ; SET TASK 3 BANK =  22
   STA (BANNER_TASK + TASK_BANK)	           ;
   LDY #<(JUMP_START_BANNER_TASK-1)            ; SET ADDR = UPDATE STATUS BAR
   LDA #>(JUMP_START_BANNER_TASK-1)            ; 
   LDX #BANNER_TASK                            ; LOAD UPDATE TATUS BAR TASK INDEX (TASK 3)
   JSR CREATE_TASK                             ; CREATE TASK(Y,A= ADDRESS, X= TASK INDEX)
   RETURN_1_FRAME 
   LDX #UPDATE_CLOCK_TASK	                   ; LOAD TASK 0= UPDATE CLOCK TASK
   JSR RESUME_TASK                             ; RESUME TASK(X=TASK INDEX)
   RTS                                         ; RETURN

_F}_CUTSCENE 

_F{_GENERATE_CUTSCENE_RANDOM				   ; DONE

                                               ; 50%=0xE0 SET   25%=0x20 SET, 25%=0x40 SET, 11% 0x80 SET          
CLEAR_CUTSCENE_TO_PLAY_SET_CUT_RAND:           ; CLEAR CUTSCENE TO PLAY, SET CUTSCENE RANDOM B6 
   LDA #$00                                    ;
   STA CUTSCENE_CHANCE                         ; SET CUTSCENE RANDOM # = DON’T PLAY CUTSCENE
   STA CUTSCENE_TO_DRAW                        ; SET CUTSCENE TO PLAY = NONE
   JSR UPDATE_RANDOM_1                         ; UPDATE RANDOM NUM $3B()
   CMP #$1E                                    ; Set cutscene chance to 11%
   BCS @update_rand_2                          ; 
   LDA CUTSCENE_CHANCE                         ; 
   ORA #$80                                    ;
   STA CUTSCENE_CHANCE                         ; 
@update_rand_2:
   JSR UPDATE_RANDOM_2                         ; UPDATE RANDOM NUM $3C()
   CMP #$3F                                    ; Set cutscene chance to 25%
   BCS @update_rand_3                          ; 
   LDA CUTSCENE_CHANCE                         ; 
   ORA #$40                                    ; 
   STA CUTSCENE_CHANCE                         ; 
@update_rand_3:
   JSR UPDATE_RANDOM_3                         ; UPDATE RANDOM NUM $3D()
   CMP #$3F                                    ; Set cutscene chance to 25%
   BCS @exit                                   ; 
   LDA CUTSCENE_CHANCE                         ; 
   ORA #$20                                    ;  ONLY USED FOR PASS BLOCK CHECK                   
   STA CUTSCENE_CHANCE                         ; 
@exit:                                         ; EXIT
   RTS                                         ; RETURN 

_F}_GENERATE_CUTSCENE_RANDOM  
 
_F{_UPDATE_PASS_TARGET_AND_INDICATOR_ON_PRESS  ; DONE
 
UPDATE_CURRENT_TARGET_AND_INDICATOR:           ; LOAD TARGETS AND SET CURRENT TARGET ICON ()
   LDA POSSESSION_STATUS                       ; DOES POSESSION STATUS = WAITING FOR QB TO PASS?
   AND #$20                                    ;
   BEQ @exit                                   ; NO->EXIT
   LDA CURRENT_PASS_TARGET                     ; LOAD CURRENT TARGET POSITION ID
   BNE @set_p1_or_p2_joy                       ; VALID TARGET? YES->SET BUTTON INDEX

 @exit:                                         ; EXIT
   RTS                                         ; RETURN

@set_p1_or_p2_joy:                             ; SET BUTTON INDEX 
   LDA POSSESSION_STATUS                       ; SET BUTTON INDEX = P1 OR P2 
   AND #$01                                    ;
   TAX                                         ;
   LDA CURR_JOY_PRESS,X                        ; DOES P1 OR P2 BUTTON PRESS = A PRESSED?
   BPL UPDATE_TARGET_INDICATOR                 ; NO->UPDATE TARGET INDICATOR, SET OFFSCREEN IF NEEDED()
   LDX CURRENT_PASS_TARGET                     ; LOAD CURRENT TARGET INDEX
   
@increment_current_target:

	_WHILE EQUAL
	INX                                         ; CURRENT TARGET ++ 
	CPX #MAX_TARGETS + 1                        ; IS TARGET > MAX NUMBER OF TARGETS
	BCC @get_target                             ; NO->SAVE TARGET IN CURRENT TARGET
	
@set_target_to_first_target  
	LDX #$01                                    ; SET CURRENT TARGET TO FIRST TARGET 
	
@get_target:                                   ; SAVE TARGET IN CURRENT TARGET
   LDA PASS_TARGET_INFO,X                          ; loop if target = 0 
   _END_WHILE             					   ;

@save_target_index:  
   STX CURRENT_PASS_TARGET                     ;

   
   
UPDATE_TARGET_INDICATOR:                       ; UPDATE TARGET INDICATOR, SET OFFSCREEN IF NEEDED()

target_player_ram = LOCAL_1
   
@check_for_qb_ready_to_pass:   
   LDA POSSESSION_STATUS                       ; DOES POSSESION STATUS = PASS ATTEMPT NOT POSSIBLE
   AND #$20                                    ; 
   BEQ @target_exit                            ; YES->EXIT
   LDX CURRENT_PASS_TARGET                     ; DOES TARGET INDEX = NO TARGET
   BEQ @target_exit                            ; YES- EXIT

@set_pass_target_sprite_info   
   LDA PASS_TARGET_INFO,X                          ; LOAD CURRENT TARGET ID
   JSR GET_P1_P2_RAM_ADDR                      ; GET PLAYER POINTER AND PUT IN 3E/3F
   LOAD_CUR_TARGET_Y_POS                       ; TARGET Y - 16 pixels
   SEC                                         ;
   SBC #$10                                    ; 
@save_spr_y_positionL   
   STA PASS_TGT_INDICATOR_SPR_Y	               ; SAVE in TARGET INDICATOR Y
   LDA POSSESSION_STATUS                       ; SET PALLETE SET TO USE (P1 OR P2)
   AND #$02                                    ; 
   STA PASS_TGT_INDICATOR_SPR_ATTR             ; 
   LOAD_CUR_TARGET_X_POS                       ; TARGET_X - 4 - SCROLL POSITION
   SEC                                         ; 
   SBC #$04                                    ; 
   TAX                                         ; 
   INY                                         ;
   LDA (target_player_ram),Y                   ; 
   SBC #$00                                    ; 
   TAY                                         ; 
   TXA                                         ; 
   SEC                                         ;
   SBC CURR_X_SCROLL                           ; 
   TAX                                         ; 
   TYA                                         ; 
   SBC CURR_X_SCROLL + 1                       ; IS TARGET =  ON SCREEN?
   BEQ @target_onscreen                        ; YES-> TARGET ONSCREEN
   LDY #OFFSCREEN_TARGET_TILE_ID               ; LOAD TARGET SPRITE OFF SCREEN TILE
   BCS @target_offscreen                       ; IS PLAYER X LOC =  OFFSCREEN RIGHT EDGE? YES-> TARGET OFFSCREEN  
   LDA PASS_TGT_INDICATOR_SPR_ATTR             ; SET TARGET SPRITE ATTRIB = FLIP HORIZONTAL
   ORA #$40                                    ; 
   STA PASS_TGT_INDICATOR_SPR_ATTR             ; 
   LDX #$08                                    ; SET SPRITE = LEFT EDGE
   JMP @save_sprite_tile_and_x_loc             ; JUMP->SAVE TARGET SPRITE X POS AND TILE #

@target_offscreen:                             ; TARGET OFFSCREEN
   LDX #SPRITE_OFFSCREEN                       ; SET TARGET SPRITE X POS = RIGHT EDGE
   JMP @save_sprite_tile_and_x_loc             ; JUMP->SAVE TARGET SPRITE X POS AND TILE #

@target_onscreen:                              ; TARGET ONSCREEN
   LDY #PASS_TARGET_ONSCREEN_TILE_ID           ; LOAD TARGET ON SCREEN TILE

@save_sprite_tile_and_x_loc:                  ; SAVE TARGET SPRITE X POS AND TILE #
   STX PASS_TGT_INDICATOR_SPR_X               ; SET TARGET SPRITE X POS 
   STY PASS_TGT_INDICATOR_SPR_TILE	            ; SET TARGET SPRITE TILE
@target_exit:                                  ; EXIT 
   RTS                                         ; RETURN
   
 _F}_UPDATE_PASS_TARGET_AND_INDICATOR_ON_PRESS  

_F{_INJURY_ANIMATION						   ; DONE
   
DO_INJURY_ANIMATION:                           ; INJURY REF AND STRETCHER GUYS ANIMATION START
   LDA TEAM_CONTROL_TYPES                      ; IS PLAYER TYPE = SKP V SKP
   CMP #$33                                    ; 
   BNE @normal_game_injury_sub                 ;
   JMP SKIP_MODE_INJURY_SUB                    ; YES->SKIP MODE INJURY SUB

@normal_game_injury_sub:
   JSR SET_BG_SPR_FULLY_FADED_OUT              ; FADE OUT PALLETE ()
   JSR SET_ALL_SPR_OFFSCREEN                   ; CLEAR SPRITES()
   LDA #$34                                    ; SET CHAR 1400 BANK = #34
   STA SOFT_CHR_1400_BANK                      ;
   LDA #$07                                    ; SET CHAR 1800 BANK = #07 
   STA SOFT_CHR_1800_BANK                      ;
   SET_SOFT_SPRITE_8_BY_16

   LDY #$EC
@clear_ref_sprite_info_loop:                   ; LOAD REF,STRETCHER GUYS, INJURED PLAYER SPRITE IFO INTO SPRITES 12,13,14,15,16 ($230-$243)
   LDA INJURY_SPRITE_INFORMATION-$EC,Y         ; SAVE SPRITE INFORMATION FROM SPRITE INFORMATION FOR ON FIELD INJURY ANIMATION
   STA $0144,Y                                 ;
   INY                                         ;
   BNE @clear_ref_sprite_info_loop             ;


   LDA BALL_X+1	                                     ; SAVE BALL X IN SCROLL X
   SEC                                         ;
   SBC #$80                                    ;
   STA CURR_X_SCROLL                                     ;
   LDA BALL_X+2	                                     ;
   SBC #$00                                    ;
   STA CURR_X_SCROLL + 1                                     ;
   JSR START_DRAW_GAME_FIELD_TASK                               ; START DRAW GAME FIELD TASK()
                                               ; SET REF SPR COLORS
   LDA #COLOR_BLACK_2                                    ; SET SPRITE PALLETE IN SPR PAL 4 = BLACK
   STA REF_PAL_BUF_1                                   ;
   LDA #COLOR_MEDIUM_ORANGE                                    ; SET SPRITE PALLETE IN SPR PAL 4 = BROWN ORANGE
   STA REF_PAL_BUF_2                                   ;
   LDA #COLOR_WHITE                                    ; SET SPRITE PALLETE IN SPR PAL 4 = WHITE
   STA REF_PAL_BUF_3                                  ;
   LDA_ABS_A_ZP CURR_X_SCROLL                           ; SET IRQ SPLIT 2 XSCROLL = X-SCROLL
   SEC                                         ;
   SBC #$10                                    ;
   STA IRQ_2_XSCROLL	                       ;
   LDA_ABS_A_ZP CURR_X_SCROLL + 1                ;
   SBC #$00                                    ;  
   AND #$01                                    ;
   STA IRQ_2_NAMETABLE                         ;
   LDA POSSESSION_STATUS                       ; DOES POSSESSION STATUS = P2 HAS BALL
   BMI @p2_injured                             ; YES->PLAYER 2 INJURED

@p1_injured:   
   LDY P1_MAN_PLAYER_ADDR                      ; LOAD PLAYER 1 ACTIVE PLAYER POINTER
   LDX P1_MAN_PLAYER_ADDR +1                  ;
   LDA #$00                                    ; SET SPRITE PALLETE TO PLAYER 1
   JMP UPDATE_SPR_PAL_PLAY_INJURY_MUSIC                                  ;

 @p2_injured:                                        ; PLAYER 2 INJURED
   LDY P2_MAN_PLAYER_ADDR                                      ; LOAD P2 ACTIVE PLAYER POINTER
   LDX P2_MAN_PLAYER_ADDR +1                                      ;
   LDA #$02                                    ; SET SPRITE PALLETE = P2

TEMP_MAN_PLAYER_ADDR 	= LOCAL_1 
SPRITE_ANIMATION_FRAMES = $06

UPDATE_SPR_PAL_PLAY_INJURY_MUSIC:             ; UPDATE SPRITE PALLETES AND SPRITE LOCATIONS AND PLAY INJURY SOUND
   STA INJURED_PLAYER_SPR_ATTR	              ; SAVE INJURED PLAYER SPRITE PALLETE IN SPRITE PALLETE ATTRIBUTES 
   STA INJURED_PLAYER_FOOT_SPR_ATTR            ;
   STY TEMP_MAN_PLAYER_ADDR                    ; SAVE PLAYER POINTER IN 3E,3F
   STX TEMP_MAN_PLAYER_ADDR +1                 ;
   LOAD_CUR_TARGET_Y_POS                       ; LOAD INJURED PLAYER Y
   STA INJURED_PLAYER_SPR_Y                    ;
   STA INJURED_PLAYER_FOOT_SPR_Y 	           ;
   STA REF_INJURY_SPR_Y 	                    ;
   LOAD_CUR_TARGET_X_POS                       ; LOAD INJURED PLAYER X
   SEC                                         ;
   SBC #$04                                    ;
   TAX                                         ;
   INY                                         ;
   LDA (TEMP_MAN_PLAYER_ADDR),Y                ; LOAD INJURED PLAYER X BYTE 2
   SBC #$00                                    ;
   TAY                                         ;
   TXA                                         ;
   SEC                                         ;
   SBC CURR_X_SCROLL                           ;
   STA INJURED_PLAYER_SPR_X	                   ;
   CLC                                         ;
   ADC #$08                                    ; SAVE ACTIVE PLAYER X LOCATION -X SCROLL +0x04 IN PLAYER FOOT  X LOCATION
   STA INJURED_PLAYER_FOOT_SPR_X               ;
   SEC                                         ;
   SBC #$0C                                    ; SAVE ACTIVE PLAYER X LOCATION -X SCROLL -0x08 IN STRETCHER 1  X LOCATION
   STA STRETCHER_1_INJURY_SPR_X                ; 
   CLC                                         ;
   ADC #$10                                    ; SAVE ACTIVE PLAYER X LOCATION -X SCROLL +0x08 IN STRETCHER 2  X LOCATION
   STA STRETCHER_2_INJURY_SPR_X                ;
   RETURN_IN_NUM_FRAMES 10
   JSR FADE_IN_BG_AND_SPR_PAL                  ; FADE IN PALLETE()
   PLAY_SOUND_ID INJURY_SONG 
   LDA #$00                                    ; SET REF AND STRETCHER SPRITE TIMERS = ZERO
   STA REF_INJURY_ANI_INDEX                    ;
   STA STRETCHER_INJURY_ANI_INDEX               ; 
   LDA #SPRITE_ANIMATION_FRAMES                ; SET UPDATE SPRITE TILE TIMER TO 6 FRAMES 
   STA SPRITE_ANI_TIMER                        ; 

END_OF_REF_RUN_TILES = $03
END_OF_STRETCHER_TILES = $0C
END_OF_ANIMATION = $FF
REF_HUNCHED_OVER_TILE_ID = $7F
STRETCHER_HUNCHED_OVER_TILE_ID = $6F

@animation_loop:                               ; REFEREE AND STRETCHER GUYES RUNNING FROM OFFSCREEN ANIMATION LOOP
   RETURN_1_FRAME 
   DEC SPRITE_ANI_TIMER                        ;
   BNE @move_ref_check_at_final_loc            ; UPDATE SPRITE TILE TIMER DONE->MOVE REFEREE AND CHECK FOR AT FINAL LOCATION
   LDA #SPRITE_ANIMATION_FRAMES                ; RELOAD UPDATE SPRITE TILE TIMER WITH 6 FRAMES
   STA SPRITE_ANI_TIMER                        ;
   LDX REF_INJURY_ANI_INDEX                    ; IS REFEREE ANIMATION DONE?
   BMI @update_stretcher_tiles                  ; YES-> UPDATE STRETCHER GUYS SPRITE TILES
   LDA REF_RUN_TILES,X                         ; SET REFERREE SPRITE TILE = TILE ID FROM REFEREE TILES
   STA REF_INJURY_SPR_TILE                     ;
   INX                                         ;
   CPX #END_OF_REF_RUN_TILES                   ; AT FINAL SPRITE IN SEQUENCE?
   BCC @save_ref_animation_index                                 ; NO-> SAVE REFEREE TILE INDEX
   LDX #$00                                    ; SET REFEREE SPRITE TILE INDEX = FIRST TILE

@save_ref_animation_index:                     ; SAVE REFEREE TILE INDEX
   STX REF_INJURY_ANI_INDEX                   ;
@update_stretcher_tiles:                       ; UPDATE STRETCHER GUYS SPRITE TILES
   LDX STRETCHER_INJURY_ANI_INDEX                                      ; ARE STRETCHER GUYS ANIMATIONS DONE?
   BMI @move_ref_check_at_final_loc                                 ; YES-> MOVE REFEREE AND CHECK IF AT FINAL LOCATION
   LDA STRETCHER_GUYS_TILES_ATTRIBUTES,X      ; UPDATE STRETCHER GUYS TILE ID AND HORIZONTAL FLIP ATTRIBUTE FOR PANTS
   STA STRETCHER_1_INJURY_SPR_TILE                                  ;
   STA STRETCHER_2_INJURY_SPR_TILE                                   ;
   LDA STRETCHER_GUYS_TILES_ATTRIBUTES+1,X   ;
   STA STRETCHER_1_INJURY_SPR_ATTR                                  ;
   STA STRETCHER_2_INJURY_SPR_ATTR                                  ;
   INX                                         ;
   INX                                         ;
   CPX #END_OF_STRETCHER_TILES                                    ; AT END OF STRETCHER GUYS ANIMATION SEQUENCE
   BCC @save_stretcher_animation_index                                  ; NO-> SAVE STRETCHER GUY TILE INDEX
   LDX #$00                                    ; RESET STRETCHER GUY SPRITE TILE INDEX TO FIRST TILE
@save_stretcher_animation_index:                                        ; SAVE STRETCHER GUY TILE INDEX
   STX STRETCHER_INJURY_ANI_INDEX             ;

@move_ref_check_at_final_loc:                  ; MOVE REFEREE AND CHECK IF AT FINAL LOCATION
   LDA REF_INJURY_ANI_INDEX                    ; IS REFERREE ANIMATION DONE?
   BMI @move_stretcher_guys_check_at_final_loc ; YES-> MOVE STRETCHER GUYS AND CHECK IF AT FINAL LOCATION
   DEC REF_INJURY_SPR_X	                       ; REFEREEE SPRITE X LOCATION--
   LDA REF_INJURY_SPR_X	                       ; 
   SEC                                         ;
   SBC #$0C                                    ;
   CMP INJURED_PLAYER_SPR_X	                 ; IS REFEREE X LOCATION WITHIN 1.5 YARDS OF INJURED PLAYER?
   BCS @move_stretcher_guys_check_at_final_loc ; NO-> MOVE STRETCHER GUYS AND CHECK IF AT FINAL LOCATION

@ref_reached_final_location: 
   LDA #END_OF_ANIMATION                       ; SET REFEREE ANIMATION TIMER = DONE
   STA REF_INJURY_ANI_INDEX                    ;
   LDA #REF_HUNCHED_OVER_TILE_ID               ; SET REFEREE SPRITE TILE =  REF STANDING FEET
   STA REF_INJURY_SPR_TILE                     ;
@move_stretcher_guys_check_at_final_loc:       ; MOVE STRETCHER GUYS AND CHECK IF AT FINAL LOCATION
   LDA STRETCHER_INJURY_ANI_INDEX              ; ARE STRETCHER GUYS DONE?
   BMI @check_both_animations_done             ; YES->CHECK FOR BOTH ANIMATIONS DONE
   DEC STRETCHER_1_INJURY_SPR_Y                ; STRETCHER GUYS Y LOCATION--
   DEC STRETCHER_2_INJURY_SPR_Y                ;
   LDA STRETCHER_1_INJURY_SPR_Y                ; ARE STRETCHER GUYS Y LOCATION > 1.5 YARDS OF INJURED PLAYER Y LOCATION?
   SEC                                         ;
   SBC #$0C                                    ; 
   CMP INJURED_PLAYER_SPR_Y                    ; 
   BCS @check_both_animations_done             ; YES->CHECK FOR BOTH ANIMATIONS DONE
   LDA #END_OF_ANIMATION                       ; SET STRETCHER GUYS ANIMATION = DONE
   STA STRETCHER_INJURY_ANI_INDEX              ;
   LDA #STRETCHER_HUNCHED_OVER_TILE_ID         ; SAVE STRETCHER HEAD TILE IN STRETCHER GUYS SPRITE TILE 
   STA STRETCHER_1_INJURY_SPR_TILE             ; 
   STA STRETCHER_2_INJURY_SPR_TILE             ;
@check_both_animations_done:                   ; CHECK FOR BOTH ANIMATIONS DONE
   LDA REF_INJURY_ANI_INDEX                    ; ARE REFEREE AND STRETCHER GUYS ANIMATIONS DONE?
   AND STRETCHER_INJURY_ANI_INDEX              ;
   BMI @injury_animation_done                  ; YES->DRAW PLAYER INJURY CUTSCENE AND RETURN
   JMP @animation_loop                         ;  NO-> REFEREE AND STRETCHER GUYES RUNNING FROM OFFSCREEN ANIMATION LOOP

@injury_animation_done:                         ; DRAW PLAYER INJURY CUTSCENE AND RETURN
   RETURN_IN_NUM_FRAMES 60
   JSR FADE_OUT_BG_SPR_PAL                     ; FADE OUT PALLETTE()
   LDA #INJURY_SCENE_ID                        ; SET CUTSCENE ID = INJURY
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)
   RTS                                         ; RETURN

SKIP_MODE_INJURY_SUB:                         ; SKIP MODE INJURY ROUTINE
   LDA CURRENT_WEEK_IN_SEASON                  ; CURRENT WEEK > WEEK TO SKIP TO 
   CMP WEEK_TO_SKIP_TO                         ; 
   BCC @exit_skp_mode_injury                    ; NO->EXIT
   JSR FADE_OUT_BG_SPR_PAL                     ; FADE OUT PALLETTE()
   PLAY_SOUND_ID INJURY_SONG 
   LDA #INJURY_SCENE_ID                         ; SET CUTSCENE ID = INJURY
   JSR DRAW_CUTSCENE_PLAY_OVER                 ; PLAY BANK 7 CUTSCENE, PLAY IS OVER ( A=CUTSCENE INDEX)

@exit_skp_mode_injury:                         ; EXIT
   RTS                                         ; RETURN

INJURY_SPRITE_INFORMATION:                     ; SPRITE INFORMATION FOR ON FIELD INJURY ANIMATION  
   .DB $00, $8D, $00, $00                      ; INJURED PLAYER FEET ALL OTHER ATTRIBS CLEARED
   .DB $00, $8F, $00, $00                      ; INJURED PLAYER ?ALL OTHER ATTRIBS CLEARED
   .DB $00, $79, $03, $F0                      ; REFEREE FEET TILE USING 3RD PALLETE SET TO OFFSCREEN X
   .DB $F0, $75, $03, $00                      ; STRETCHER GUY 1 FEET SET TO OFFSCREEN Y
   .DB $F0, $75, $03, $00                      ; STRETCHER GUY 2 FEET SET TO OFFSCREEN Y

REF_RUN_TILES:                                 ; REFEREE RUNNING SPRITE TILES
   .DB $79, $7B, $7D, $FF
      

STRETCHER_GUYS_TILES_ATTRIBUTES:               ; STRETECHER GUYS RUNNING TILES
   .DB $75,$03                                 ; TILE ID, TILE ATTRIBS
   .DB $77,$03
   .DB $6D,$03
   .DB $77,$43
   .DB $75,$43
   .DB $6D,$03
   .DB $FF                                     ; END

_F}_INJURY_ANIMATION     

_F{_UPDATE_LOS_MARKERS

ppu_data_addr_down_marker 	= LOCAL_1
ppu_addr_down_marker		= LOCAL_5
down_marker_x_loc			= LOCAL_7
fd_marker_2 				= LOCAL_7

EIGHT_YARDS					= ONE_YARD * 8 

CHECK_FOR_DRAW_FD_STICK:                       ; UPDATE LOS MARKERS IF THEY ARE OFF SCREEN()
   IF_PLAY_PAST_LOS[ADDR] CHECK_DRAW_SECOND_DOWN_MARKER
   RTS                                         ; RETURN

SET_SIDELINE_SCROLL_LOC_UPDATE_DOWN_MARKERS:    ; UPDATE LOS AND FIRST DOWN MARKERS VALUES AND GRAPHICS()
   LDA FD_MARKER_X                             ; LOAD FIRST DOWN MARKER LOW
   EOR #$FF                                    ; SET SIDELINE X SCROLL
   CLC                                         ;
   ADC #$09                                    ;
   AND #$0F                                    ;
   STA SIDELINE_SCROLL_X                       ; 
   
CHECK_DRAW_SECOND_DOWN_MARKER:                 ; CHECK FOR DRAW SECOND DOWN MARKER
   IF_P2_LAST_HAD_BALL[ADDR] P2_DOWN_MARKER_CHECK

P1_DOWN_MARKER_CHECK:   
   LDA FD_MARKER_X                             ; SET FIRST DOWN MARKER 2   = FIRST DOWN MARKER 1   + 10 yards
   CLC                                         ; 
   ADC #YARDS_TO_2nd_DOWN_MARKER               ;  
   STA fd_marker_2                             ;
   LDA FD_MARKER_X+1                           ; 
   ADC #$00                                    ; 
   STA fd_marker_2+1                           ;  
   LDA fd_marker_2                             ; IS FIRST DOWN MARKER 2  >= START OF P1 ENDZONE?
   CMP #<FRONT_RIGHT_ENDZONE                   ;
   LDA fd_marker_2+1                           ;
   SBC #>FRONT_RIGHT_ENDZONE                   ; 
   BCS P1_DONT_DRAW_2ND_MARKER                 ; YES-> P1 DON’T DRAW SECOND DOWN MARKER
 
P1_DRAW_MARKERS_IF_ON_SCREEN: 
   JSR CHECK_IF_MARKER_TWO_ON_SCREEN                 ;     
   BCS EXIT_FD_MARKER_CHECK                    ; exit if markers not on screen
   JSR DRAW_DOWN_MARKER_2                      ;
   JMP DRAW_DOWN_MARKER_1_SET_PAST_LOS         ; JUMP-> DRAW FIRST DOWN MARKER

P1_DONT_DRAW_2ND_MARKER:                       ; P1 DON’T DRAW SECOND DOWN MARKER
   JSR CHECK_IF_MARKER_ONE_ON_SCREEN                               ; 
   BCS EXIT_FD_MARKER_CHECK                    ;
   JMP DRAW_DOWN_MARKER_1_SET_PAST_LOS         ; JUMP-> DRAW FIRST DOWN MARKER

P2_DOWN_MARKER_CHECK:                         ; P2 HAS BALL
   LDA FD_MARKER_X                             ; SET FIRST DOWN MARKER 2   = FIRST DOWN MARKER 1   - 10 yards
   SEC                                         ;
   SBC #YARDS_TO_2nd_DOWN_MARKER               ;
   STA fd_marker_2                             ;
   LDA FD_MARKER_X+1                           ;
   SBC #$00                                    ;
   STA fd_marker_2+1                           ;
   LDA fd_marker_2                             ;
   CMP #<FRONT_LEFT_ENDZONE                    ;
   LDA fd_marker_2+1                           ;
   SBC #>FRONT_LEFT_ENDZONE                    ; IS DOWN MARKER (FD)  =  PAST P2 ENDZONE?
   BCC P2_DONT_DRAW_2ND_MARKER                 ; YES-> P2 DON’T DRAW SECOND DOWN MARKER
   
P2_DRAW_MARKERS_IF_ON_SCREEN: 
   JSR CHECK_IF_MARKER_TWO_ON_SCREEN           ;
   BCS EXIT_FD_MARKER_CHECK                    ;
   JSR DRAW_DOWN_MARKER_2                      ;
   JMP DRAW_DOWN_MARKER_1_SET_PAST_LOS         ; JUMP-> DRAW FIRST DOWN MARKER

P2_DONT_DRAW_2ND_MARKER:                       ; P2 DON’T DRAW SECOND DOWN MARKER
   JSR CHECK_IF_MARKER_ONE_ON_SCREEN                               ;
   BCS EXIT_FD_MARKER_CHECK                    ;
   JMP DRAW_DOWN_MARKER_1_SET_PAST_LOS         ; JUMP-> DRAW FIRST DOWN MARKER

EXIT_FD_MARKER_CHECK:
   RTS                                         ; RETURN

CHECK_IF_MARKER_TWO_ON_SCREEN:                 ; SET DOWN MARKER LOCATION
   LDA fd_marker_2                             ; = FIRST DOWN MARKER 2 – SCROLL LOCATION (X)
   SEC                                         ;
   SBC CURR_X_SCROLL                           ;
   TAY                                         ;
   LDA fd_marker_2+1                           ;
   SBC CURR_X_SCROLL + 1                       ;
   JMP CHECK_WITHIN_EIGHT_YARDS_OF_SCREEN_LOC   ; JUMP ->ADD 8 YARDS TO X LOC (A,Y = X LOC)

CHECK_IF_MARKER_ONE_ON_SCREEN:                 ; FIRST DOWN MARKER 1 - SCROLL LOCATION 
   LDA FD_MARKER_X                             ; = FIRST DOWN MARKER 2 – SCROLL LOCATION (X)
   SEC                                         ;
   SBC CURR_X_SCROLL                           ;
   TAY                                         ; 
   LDA FD_MARKER_X+1                           ; 
   SBC CURR_X_SCROLL + 1                       ;

CHECK_WITHIN_EIGHT_YARDS_OF_SCREEN_LOC:        ; ADD 8 YARDS TO X LOC (A,Y = X LOC)
   TAX                                         ; 
   TYA                                         ; 
   CLC                                         ; 
   ADC #EIGHT_YARDS	                            ; 
   TAY                                         ; 
   TXA                                         ; 
   ADC #$00                                    ;
   CPY #$80                                    ;
   SBC #$01                                    ;
   RTS                                         ; RETURN

DRAW_DOWN_MARKER_1_SET_PAST_LOS:               ; DRAW FIRST DOWN MARKER
   LDA FD_MARKER_X                             ; SET $45,44 =  FIRST DOWN MARKER X LOC
   STA down_marker_x_loc                       ;
   LDA FD_MARKER_X+1                           ;
   STA down_marker_x_loc +1                    ; 
 
@set_down_marker_1_data_addr :
   LDA #<PPU_DATA_DOWN_MARKER_1                ; SET DATA ADDR =  DOWN MARKER (LOS
   STA ppu_data_addr_down_marker                    ;
   LDA #>PPU_DATA_DOWN_MARKER_1                ;
   STA ppu_data_addr_down_marker +1                 ;
   
@set_play_status_past_los:   
   SET_PLAY_STATUS_PAST_LOS[]
   JMP SET_PPU_ADDR_DOWN_MARKERS               ; JUMP->SET PPU LOCATION FOR FD MARKERS

DRAW_DOWN_MARKER_2:
   LDA #<PPU_DATA_DOWN_MARKER_2                ; SET DATA ADDR =  DOWN MARKER (FIRST DOWN)
   STA ppu_data_addr_down_marker               ;
   LDA #>PPU_DATA_DOWN_MARKER_2                ;
   STA ppu_data_addr_down_marker +1            ;

SET_PPU_ADDR_DOWN_MARKERS:                    ; SET PPU LOCATION FOR FD MARKERS

ppu_addr					= LOCAL_1
side_with_ball				= LOCAL_5
ppu_temp					= LOCAL_8
down_marker_attr_data_addr		= LOCAL_3

   LDA down_marker_x_loc                       ; SET PPU ADDR FOR DOWN MARKER
   CLC                                         ; 
   ADC #$07                                    ;
   TAX                                         ;
   LDA down_marker_x_loc +1                    ;
   ADC #$00                                    ; 
   TAY                                         ;
   TXA                                         ;
   LSR                                         ;
   LSR                                         ;
   LSR                                         ;
   AND #$FE                                    ;
   CLC                                         ;
   ADC #$80                                    ; 
   STA ppu_addr_down_marker                    ;
   TYA                                         ;
   AND #$01                                    ;
   ADC #$08                                    ;
   ASL                                         ;
   ASL                                         ;
   STA ppu_addr_down_marker+1                  ; TRANSFER PPU ADDR TO X,Y
   TAX                                         ; 
   LDY ppu_addr_down_marker                    ; 
   JSR DRAW_RECTANGULAR_IMAGE                  ; UPDATE PPU NUM ROWS(3E,3F= DATA ADDRESS(ROWS AND LENGTH)  X,Y= PPU ADDRESS)
   RETURN_1_FRAME 							   ; 
  
@calulate_ppu_attr_addr
  LDA ppu_addr_down_marker                    ; SET PPU ADDR
   AND #$9C                                    ;
   LSR                                         ;
   LSR                                         ;
   STA ppu_temp                                ;
   AND #$20                                    ;
   LSR                                         ;
   LSR                                         ;
   ORA ppu_temp                                ;
   AND #$0F                                    ;
   STA ppu_temp                                ;
   LDA ppu_addr_down_marker+1                  ;
   ASL                                         ;
   ASL                                         ;
   ASL                                         ;
   ASL                                         ;
   AND #$30                                    ;
   CLC                                         ;
   ADC #$C0                                    ;
   
 @save_down_marker_ppu_addr 
   ORA ppu_temp                                ;
   STA ppu_addr                                ;
   TAY                                         ;
   LDA ppu_addr_down_marker+1                  ;
   AND #$FC                                    ;
   ADC #$03                                    ;
   STA ppu_addr+1                              ; 
   
   LDA #<DOWN_MARKER_ATTR_DATA[]         	   ; SET DATA ADDR
   STA down_marker_attr_data_addr              ;
   LDA #>DOWN_MARKER_ATTR_DATA[]          	   ;
   STA down_marker_attr_data_addr+1            ;
   LDX #$01                                    ; SET ROWS = 1
   LDY #$01                                    ; SET LENGTH =1 
   JSR TRANSFER_BUFFER_TO_SRAM                 ; SAVE PPU DATA IN SRAM (3E,3F= PPU ADDRESS  40,41= DATA ADDRESS, X,Y= DATA LENGTH)
   LDA side_with_ball                          ; DOES PLAYER WITH BALL = P2
   AND #$02                                    ;
   BNE @set_p2_attribute_table_entry           ; YES->SET P2 SIDE BG ATTRIBUTE  VALUE

@set_p1_attribute_table_entry:
   LDA DOWN_MARKER_ATTR_TABLE_VALUE            ;
   AND #$FC                                    ;
   STA DOWN_MARKER_ATTR_TABLE_VALUE            ; JUMP->TRANSFER BG ATRIBUTE VALUES TO PPU
   JMP @transfer_attribute_value_to_ppu
   
@set_p2_attribute_table_entry:                 ; SET P2 SIDE BG ATTRIBUTE VALUE
   LDA DOWN_MARKER_ATTR_TABLE_VALUE                                   ;
   AND #$F3                                    ;
   STA DOWN_MARKER_ATTR_TABLE_VALUE                                   ;

@transfer_attribute_value_to_ppu:              ; TRANSFER BG ATRIBUTE VALUES TO PPU 

ppu_addr 	= LOCAL_1
data_addr	= LOCAL_1
   LDA ppu_addr                                ; SET PPU ADDR = PPU ADDR – 1 ROW
   SEC                                         ;
   SBC #LENGTH_OF_ONE_ROW                      ;
   TAY                                         ;
   LDX ppu_addr+1                              ;
   LDA #<DOWN_MARKER_ATTR_DATA[]          ; SET DATA ADDR = 06F2
   STA data_addr                               ;
   LDA #>DOWN_MARKER_ATTR_DATA[]           ;
   STA data_addr +1                            ;
   JSR TRANSFER_TO_PPU_OR_PPUBUFF_WITH_COMMANDS; TRANSFER DATA TO PPU VIA BUFFER OR DIRECTLY DEPENDING ON FADE VALUES(X,Y= PPU ADDR, 3E,3F= DATA ADDR)
   RTS                                         ; RETURN

DOWN_MARKER_ROWS 	= $03
TILES_PER_ROW	 	= $02 
   
PPU_DATA_DOWN_MARKER_2:                        ; PPU DATA DOWN MARKER 2 (FIRST DOWN)
   .DB DOWN_MARKER_ROWS                        ; ROWS
   .DB TILES_PER_ROW                          ; TILES PER ROW
   .DB $14,$15                                 ; TOP OF MARKER 
   .DB $16,$17                                 ; MIDDLE OF MARKER 
   .DB $1C,$1D                                 ; BOTTOM OF MARKER 

PPU_DATA_DOWN_MARKER_1:                        ; PPU DATA DOWN MARKER 1 (LOS)
   .DB DOWN_MARKER_ROWS                       ; ROWS
   .DB TILES_PER_ROW                           ; TILES PER ROW
   .DB $18,$19                                 ; TOP OF MARKER
   .DB $1A,$1B                                 ; MIDDLE OF MARKER
   .DB $1C,$1D                                 ; BOTTOM OF MARKER

_F}_UPDATE_LOS_MARKERS

_F{_CUTSCENE_SEQUENCE_PTRS_AND_SEQUENCES	   ; DONE
      
CUTSCENE_ADDR:                                 ; CUTSCENE POINTER TABLE
	.WORD PUNT_CUT_SEQ                         ; 0X00 PUNT
    .WORD MISSED_PUNT_BLOCK_CUT_SEQ                         ; 0X01 MISSED PUNT BLOCK
    .WORD PUNT_BLOCK_CUT_SEQ                          ; 0X02 PUNT BLOCKED
    .WORD UNUSED_CUT_SEQ                          ; 0X03 BLACK SCREEN, RETURN
      
    .WORD QB_SACK_CUT_SEQ                          ; 0X04 QB SACK
    .WORD QB_SACK_SIDE_CHANGE_CUT_SEQ                          ; 0X05 QB SACK WITH SIDE CHANGE
	.WORD QB_SAFETY_CUT_SEQ                          ; 0X06 QB SACK WITH SAFETY
	.WORD MISSED_PASS_BLOCK_CUT_SEQ                          ; 0X07 PASS WITH MISSED PASS BLOCK
	.WORD MISSED_PASS_BLOCK_CUT_SEQ                             ; 0X08 PASS WITH MISSED PASS BLOCK
	.WORD PASS_BLOCKED_CUT_SEQ                          ; 0X09 PASS WITH PASS BLOCKED
      
	.WORD P2_FG_MISS_BLOCK_DOWN_MIDDLE_CUT_SEQ                          ; 0X0A P2 FG, MISSED BLOCK, THRU UPRIGHTS
	.WORD P2_FG_MISS_BLOCK_DOWN_MIDDLE_CUT_SEQ                          ; 0X0B P2 FG, THRU UPRIGHTS
	.WORD P2_FG_DOWN_MIDDLE_CUT_SEQ                          ; 0X0C P2 FG, THRU UPRIGHTS 
	.WORD P2_XP_MISSED_BLOCK_DOWN_MIDDLE_CUT_SEQ                          ; 0X0D P2 XP, MISSED BLOCK, GOOD 
	.WORD P2_XP_DOWN_MIDDLE_CUT_SEQ                          ; 0X0E P2 XP, GOOD
	.WORD FG_BLOCKED_CUT_SEQ                          ; 0X0F FG BLOCKED
	.WORD P1_FG_MISS_BLOCK_DOWN_MIDDLE_CUT_SEQ                             ; 0X10 P1 FG, MISSED BLOCK, THRU UPRIGHTS
	.WORD P1_FG_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ                          ; 0X11 P1 FG, MISSED BLOCK, HIT LEFT UPRIGHT, GOOD
	.WORD P1_FG_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ                          ; 0X12 P1 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, GOOD
    .WORD P2_FG_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ                          ; 0X13 P2 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, GOOD
    .WORD P2_FG_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ                          ; 0X14 P2 FG, MISSED BLOCK, HIT LEFT UPRIGHT, GOOD
    .WORD P1_FG_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ                          ; 0X15 P1 FG, MISSED BLOCK, HIT LEFT UPRIGHT, MISS
    .WORD P1_FG_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ                         ; 0X16 P1 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, MISS
	.WORD P1_FG_MISS_BLOCK_WIDE_LEFT_CUT_SEQ                          ; 0X17 P1 FG, MISSED BLOCK, WIDE LEFT MISS
	.WORD P1_FG_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ                             ; 0X18 P1 FG, MISSED BLOCK, WIDE RIGHT MISS
	.WORD P1_FG_MISS_BLOCK_SHORT_CUT_SEQ                             ; 0X19 P1 FG, MISSED BLOCK, SHORT
	.WORD P2_FG_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ                             ; 0X1A P2 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, MISS
	.WORD P2_FG_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ                             ; 0X1B P2 FG, MISSED BLOCK, HIT LEFT UPRIGHT, MISS
	.WORD P2_FG_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ                             ; 0X1C P2 FG, MISSED BLOCK, WIDE RIGHT MISS
	.WORD P2_FG_MISS_BLOCK_WIDE_LEFT_CUT_SEQ                             ; 0X1D P2 FG, MISSED BLOCK, WIDE LEFT MISS
	.WORD P2_FG_MISS_BLOCK_SHORT_CUT_SEQ                             ; 0X1E P2 FG, MISSED BLOCK, SHORT
      
	.WORD P1_FG_DOWN_MIDDLE_CUT_SEQ                             ; 0X1F P1 FG, THRU UPRIGHTS
	.WORD P1_FG_LEFT_UR_GOOD_CUT_SEQ                             ; 0X20 P1 FG, HIT LEFT UPRIGHT, GOOD
	.WORD P1_FG_RIGHT_UR_GOOD_CUT_SEQ                             ; 0X21 P1 FG, HIT RIGHT UPRIGHT, GOOD
	.WORD P2_FG_RIGHT_UR_GOOD_CUT_SEQ                             ; 0X22 P2 FG, HIT RIGHT UPRIGHT, GOOD
	.WORD P2_FG_LEFT_UR_GOOD_CUT_SEQ                             ; 0X23 P2 FG, HIT LEFT UPRIGHT, GOOD
	.WORD P1_FG_LEFT_UR_MISS_CUT_SEQ                             ; 0X24 P1 FG, HIT LEFT UPRIGHT, MISS
	.WORD P1_FG_RIGHT_UR_MISS_CUT_SEQ                             ; 0X25 P1 FG, HIT RIGHT UPRIGHT, MISS
	.WORD P1_FG_WIDE_LEFT_CUT_SEQ                             ; 0X26 P1 FG, WIDE LEFT MISS     
	.WORD P1_FG_WIDE_RIGHT_CUT_SEQ                             ; 0X27 P1 FG, WIDE RIGHT MISS
	.WORD P1_FG_SHORT_CUT_SEQ                             ; 0X28 P1 FG, SHORT
	.WORD P2_FG_RIGHT_UR_MISS_CUT_SEQ                             ; 0X29 P2 FG, HIT RIGHT UPRIGHT, MISS
	.WORD P2_FG_LEFT_UR_MISS_CUT_SEQ                             ; 0X2A P2 FG, HIT LEFT UPRIGHT, MISS
	.WORD P2_FG_WIDE_RIGHT_CUT_SEQ                             ; 0X2B P2 FG, WIDE RIGHT MISS
	.WORD P2_FG_WIDE_LEFT_CUT_SEQ                             ; 0X2C P2 FG, WIDE LEFT MISS
	.WORD P2_FG_SHORT_CUT_SEQ                             ; 0X2D P2 FG, SHORT
	.WORD XP_FG_FG_BLOCK                             ; 0X2E XP/FG ATT, BLOCK
	.WORD XP_FG_FG_BLOCK                             ; 0X2F XP/FG ATT, BLOCK

	.WORD P1_XP_MISS_BLOCK_GOOD_CUT_SEQ                               ; 0X30 P1 XP ATT, MISSED BLOCK, THRU UPRIGHTS, XP GOOD
	.WORD P1_XP_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ                             ; 0X31 P1 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP GOOD
	.WORD P1_XP_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ                             ; 0X32 P1 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP GOOD
	.WORD P2_XP_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ                             ; 0X33 P2 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP GOOD
	.WORD P2_XP_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ                             ; 0X34 P2 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP GOOD
	.WORD P1_XP_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ                             ; 0X35 P1 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP MISS
	.WORD P1_XP_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ                             ; 0X36 P1 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP MISS
	.WORD P1_XP_MISS_BLOCK_WIDE_LEFT_CUT_SEQ                             ; 0X37 P1 XP ATT, MISSED BLOCK, WIDE LEFT XP MISS
	.WORD P1_XP_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ                             ; 0X38 P1 XP ATT, MISSED BLOCK, WIDE RIGHT XP MISS
	.WORD P1_XP_MISS_BLOCK_SHORT_CUT_SEQ                             ; 0X39 P1 XP ATT, MISSED BLOCK, XP SHORT (IMPOSSIBLE)
	.WORD P2_XP_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ                             ; 0X3A P2 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP MISS
	.WORD P2_XP_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ                             ; 0X3B P2 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP MISS
	.WORD P2_XP_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ                             ; 0X3C P2 XP ATT, MISSED BLOCK, WIDE RIGHT XP MISS
	.WORD P2_XP_MISS_BLOCK_WIDE_LEFT_CUT_SEQ                             ; 0X3D P2 XP ATT, MISSED BLOCK, WIDE LEFT XP MISS
	.WORD P2_XP_MISS_BLOCK_SHORT_CUT_SEQ                             ; 0X3E P2 XP ATT, MISSED BLOCK, XP SHORT (IMPOSSIBLE)
   
    .WORD P1_XP_GOOD_CUT_SEQ                          	; 0X3F P1 XP ATT, THRU UPRIGHTS, XP GOOD
	.WORD P1_XP_LEFT_UR_GOOD_CUT_SEQ                             ; 0X40 P1 XP ATT, HIT LEFT UPRIGHT, XP GOOD
	.WORD P1_XP_RIGHT_UR_GOOD_CUT_SEQ                             ; 0X41 P1 XP ATT, HIT RIGHT UPRIGHT, XP GOOD
	.WORD P2_XP_RIGHT_UR_GOOD_CUT_SEQ                             ; 0X42 P2 XP ATT, HIT RIGHT UPRIGHT, XP GOOD
	.WORD P2_XP_LEFT_UR_GOOD_CUT_SEQ                             ; 0X43 P2 XP ATT, HIT LEFT UPRIGHT, XP GOOD
	.WORD P1_XP_LEFT_UR_MISS_CUT_SEQ                             ; 0X44 P1 XP ATT, HIT LEFT UPRIGHT, XP MISS
	.WORD P1_XP_RIGHT_UR_MISS_CUT_SEQ                             ; 0X45 P1 XP ATT, HIT RIGHT UPRIGHT, XP MISS
	.WORD P1_XP_WIDE_LEFT_CUT_SEQ                             ; 0X46 P1 XP ATT, WIDE LEFT,  XP MISS
	.WORD P1_XP_WIDE_RIGHT_CUT_SEQ                             ; 0X47 P1 XP ATT, WIDE RIGHT, XP MISS
	.WORD P1_XP_SHORT_CUT_SEQ                             ; 0X48 P1 XP ATT, XP SHORT(IMPOSSIBLE)
	.WORD P2_XP_RIGHT_UR_MISS_CUT_SEQ                             ; 0X49 P2 XP ATT, HIT RIGHT UPRIGHT, XP MISS
	.WORD P2_XP_LEFT_UR_MISS_CUT_SEQ                             ; 0X4A P2 XP ATT, HIT LEFT UPRIGHT, XP MISS
	.WORD P2_XP_WIDE_RIGHT_CUT_SEQ                             ; 0X4B P2 XP ATT, WIDE RIGHT, XP MISS
	.WORD P2_XP_WIDE_LEFT_CUT_SEQ                             ; 0X4C P2 XP ATT, WIDE LEFT, XP MISS
	.WORD P2_XP_SHORT_CUT_SEQ                             ; 0X4D P2 XP ATT, MISSED BLOCK, XP SHORT (IMPOSSIBLE)

	.WORD OFF_JUMP_CATCH_CUT_SEQ                             ; 0X4E OFF JUMP CATCH
	.WORD OFF_JUMP_CATCH_CUT_SEQ                             ; 0X4F OFF JUMP CATCH
	.WORD OFF_JUMP_MISS_CUT_SEQ                             ; 0X50 OFF JUMP MISS
	.WORD OFF_JUMP_BOUNCE_HANDS_CUT_SEQ                             ; 0X51 OFF JUMP BOUNCE OFF HANDS
	.WORD DEF_JUMP_MIS_CUT_SEQ                             ; 0X52 DEF JUMP MISS
	.WORD DEF_JUMP_BOUNCE_HANDS_CUT_SEQ                             ; 0X53 DEF JUMP BOUNCE OFF HANDS
	.WORD DEF_JUMP_INT_CUT_SEQ                             ; 0X54 DEF JUMP INT
	.WORD DOUBLE_JUMP_OFF_CATCH_CUT_SEQ                             ; 0X55 DOUBLE JUMP + OFF CATCH
	.WORD DOUBLE_JUMP_OFF_WHIFF_CUT_SEQ                             ; 0X56 DOUBLE JUMP + OFF WHIFF
	.WORD DOUBLE_JUMP_BOUNCE_DEF_HANDS_CUT_SEQ                             ; 0X57 DOUBLE JUMP + DEF BOUNCE OFF HANDS
	.WORD DOUBLE_JUMP_DEF_INT_CUT_SEQ                             ; 0X58 DOUBLE JUMP + DEF INT

	.WORD OFF_REG_CATCH_CUT_SEQ                             ; 0X59 OFF REG SIDEWAYS CATCH
	.WORD OFF_REG_BOUNCE_HANDS_CUT_SEQ                             ; 0X5A OFF REG SIDEWAYS BOUNCE OFF HANDS
	.WORD DEF_REG_BOUNCE_HANDS_CUT_SEQ                             ; 0X5B DEF REG SIDEWAYS BOUNCE OFF HANDS
	.WORD DEF_REG_INT_CUT_SEQ                             ; 0X5C DEF REG SIDEWAYS INT
	.WORD DEF_DIVE_MISS_OFF_REG_CATCH_CUT_SEQ                             ; 0X5D DEF DIVE MISS + OFF REG SIDEWAYS CATCH
	.WORD DEF_JUMP_MISS_OFF_REG_CATCH_CUT_SEQ                             ; 0X5E DEF JUMP MISS + OFF REG SIDEWAYS CATCH
	.WORD OFF_DIVE_BOUNCE_OFF_HANDS_CUT_SEQ                             ; 0X5F OFF DIVE + BOUNCE OFF HANDS
	.WORD DEF_DIVE_MISS_BOUNCE_OFF_WR_HANDS_CUT_SE                             ; 0X60 OFF JUMP + BOUNCE OFF HANDS
	.WORD DEF_JUMP_MISS_BOUNCE_OFF_WR_HANDS_CUT_SE                             ; 0X61 DEF JUMP + BOUNCE OFF HANDS
	.WORD DEF_JUMP_TIP_BALL_CUT_SEQ                             ; 0X62 DEF JUMP + BOUNCE OFF HANDS
	.WORD DOUBLE_JUMP_BOUNCE_DEF_HANDS_CUT_SEQ_DUPE                             ; 0X63 DOUBLE JUMP + DEF BOUNCE OFF HANDS (DUPLICATE OF 0X58 ABOVE)
	.WORD DEF_REG_INT_CUT_SEQ_DUPE                             ; 0X64 DEF REG SIDEWAYS INT (DUPLICATE OF 0X5D ABOVE)
	.WORD BALL_HIT_GROUND_REF_INC_CUT_SEQ                             ; 0X65 BALL HITS GROUND + REF INCOMPLETE
	.WORD OFF_DIVE_CATCH_CUT_SEQ                             ; 0X66 OFF DIVE CATCH
	.WORD OFF_DIVE_MISS_CUT_SEQ                             ; 0X67 OFF DIVE MISS
	.WORD DEF_DIVE_INT_CUT_SEQ                             ; 0X68 DEF DIVE INT
	.WORD DEF_DIVE_MISS_CUT_SEQ                             ; 0X69 DEF DIVE MISS

	.WORD DEFAULT_IRQ_SPLIT_TABLE                ; 0X6A INTRO

PUNT_CUT_SEQ:                                  ; 0X00 PUNT
   .DB $03,$FE                                 ; 
MISSED_PUNT_BLOCK_CUT_SEQ:                     ; 0X01 MISSED PUNT BLOCK 
   .DB $03,$09,$FE                             ; 
PUNT_BLOCK_CUT_SEQ:                            ; 0X02 PUNT BLOCKED
   .DB $03,$0A,$FE                             ; 
UNUSED_CUT_SEQ:                                     ; 0X03 BLACK SCREEN UNUSED
   .DB $25,$FE                                 ;
QB_SACK_CUT_SEQ:                                     ; 0X04 QB SACK
   .DB $26,$FF
QB_SACK_SIDE_CHANGE_CUT_SEQ:                                     ; 0X05 QB SACK WITH SIDE CHANGE
   .DB $27,$FF
QB_SAFETY_CUT_SEQ:                                     ; 0X06 QB SACK WITH SAFETY
   .DB $28,$FF
MISSED_PASS_BLOCK_CUT_SEQ:                                     ; 0X07,0X08 PASS WITH MISS BLOCK
   .DB $29,$09,$FE
PASS_BLOCKED_CUT_SEQ:                                     ; 0X09 PASS BLOCKED
   .DB $29,$0D,$FE

P2_FG_MISS_BLOCK_DOWN_MIDDLE_CUT_SEQ:          ; 0X0A,0X0B P2 FG, MISSED BLOCK, THRU UPRIGHTS
   .DB $04,$09,$0E,$10,$11,$FF
P2_FG_DOWN_MIDDLE_CUT_SEQ:                                     ; 0X0C P2 FG, THRU UPRIGHTS
   .DB $04,$0E,$10,$11,$FF
P2_XP_MISSED_BLOCK_DOWN_MIDDLE_CUT_SEQ:                                     ; 0X0D P2 XP, MISSED BLOCK, GOOD
   .DB $04,$09,$10,$23,$FF
P2_XP_DOWN_MIDDLE_CUT_SEQ:                                     ; 0X0E P2 XP, GOOD
   .DB $04,$10,$23,$FF
FG_BLOCKED_CUT_SEQ:                                     ; 0X0F FG BLOCKED
   .DB $04,$0B,$FE
P1_FG_MISS_BLOCK_DOWN_MIDDLE_CUT_SEQ:                                     ; 0X10 P1 FG, MISSED BLOCK, THRU UPRIGHTS
   .DB $04,$09,$0E,$0F,$11,$FF
P1_FG_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X11 P1 FG, MISSED BLOCK, HIT LEFT UPRIGHT, GOOD
   .DB $04,$09,$0E,$14,$11,$FF
P1_FG_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X12 P1 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, GOOD
   .DB $04,$09,$0E,$15,$11,$FF
P2_FG_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X13 P2 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, GOOD
   .DB $04,$09,$0E,$1C,$11,$FF
P2_FG_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X14 P2 FG, MISSED BLOCK, HIT LEFT UPRIGHT, GOOD
   .DB $04,$09,$0E,$1D,$11,$FF
P1_FG_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X15 P1 FG, MISSED BLOCK, HIT LEFT UPRIGHT, MISS
   .DB $04,$09,$0E,$16,$FF
P1_FG_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X16 P1 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, MISS
   .DB $04,$09,$0E,$17,$FF
P1_FG_MISS_BLOCK_WIDE_LEFT_CUT_SEQ:                                     ; 0X17 P1 FG, MISSED BLOCK, WIDE LEFT MISS
   .DB $04,$09,$0E,$18,$FF
P1_FG_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ:                                     ; 0X18 P1 FG, MISSED BLOCK, WIDE RIGHT MISS
   .DB $04,$09,$0E,$19,$FF
P1_FG_MISS_BLOCK_SHORT_CUT_SEQ:                                     ; 0X19 P1 FG, MISSED BLOCK, SHORT
   .DB $04,$09,$0E,$1A,$FF
P2_FG_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X1A P2 FG, MISSED BLOCK, HIT RIGHT UPRIGHT, MISS
   .DB $04,$09,$0E,$1E,$FF
P2_FG_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X1B P2 FG, MISSED BLOCK, HIT LEFT UPRIGHT, MISS
   .DB $04,$09,$0E,$1F,$FF
P2_FG_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ:                                     ; 0X1C P2 FG, MISSED BLOCK, WIDE RIGHT MISS
   .DB $04,$09,$0E,$20,$FF
P2_FG_MISS_BLOCK_WIDE_LEFT_CUT_SEQ:                                     ; 0X1D P2 FG, MISSED BLOCK, WIDE LEFT MISS
   .DB $04,$09,$0E,$21,$FF
P2_FG_MISS_BLOCK_SHORT_CUT_SEQ:                                     ; 0X1E P2 FG, MISSED BLOCK, SHORT
   .DB $04,$09,$0E,$22,$FF

P1_FG_DOWN_MIDDLE_CUT_SEQ:                                     ; 0X1F P1 FG, THRU UPRIGHTS
   .DB $04,$0E,$0F,$11,$FF
P1_FG_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X20 P1 FG, HIT LEFT UPRIGHT, GOOD
   .DB $04,$0E,$14,$11,$FF
P1_FG_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X21 P1 FG, HIT RIGHT UPRIGHT, GOOD
   .DB $04,$0E,$15,$11,$FF
P2_FG_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X22 P2 FG, HIT RIGHT UPRIGHT, GOOD
   .DB $04,$0E,$1C,$11,$FF
P2_FG_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X23 P2 FG, HIT LEFT UPRIGHT, GOOD
   .DB $04,$0E,$1D,$11,$FF
P1_FG_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X24 P1 FG, HIT LEFT UPRIGHT, MISS
   .DB $04,$0E,$16,$FF
P1_FG_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X25 P1 FG, HIT RIGHT UPRIGHT, MISS
   .DB $04,$0E,$17,$FF
P1_FG_WIDE_LEFT_CUT_SEQ:                                     ; 0X26 P1 FG, WIDE LEFT MISS 
   .DB $04,$0E,$18,$FF
P1_FG_WIDE_RIGHT_CUT_SEQ:                                     ; 0X27 P1 FG, WIDE RIGHT MISS
   .DB $04,$0E,$19,$FF
P1_FG_SHORT_CUT_SEQ:                                     ; 0X28 P1 FG, SHORT
   .DB $04,$0E,$1A,$FF
P2_FG_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X29 P2 FG, HIT RIGHT UPRIGHT, MISS
   .DB $04,$0E,$1E,$FF
P2_FG_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X2A P2 FG, HIT LEFT UPRIGHT, MISS
   .DB $04,$0E,$1F,$FF
P2_FG_WIDE_RIGHT_CUT_SEQ:                                     ; 0X2B P2 FG, WIDE RIGHT MISS
   .DB $04,$0E,$20,$FF
P2_FG_WIDE_LEFT_CUT_SEQ:                                     ; 0X2C P2 FG, WIDE LEFT MISS
   .DB $04,$0E,$21,$FF
P2_FG_SHORT_CUT_SEQ:                                     ; 0X2D P2 FG, SHORT
   .DB $04,$0E,$22,$FF
XP_FG_FG_BLOCK:                                     ; 0X2E,0X2F XP/FG ATT, BLOCK
   .DB $04,$0C,$FF


P1_XP_MISS_BLOCK_GOOD_CUT_SEQ:                                     ; 0X30 P1 XP ATT, MISSED BLOCK, THRU UPRIGHTS, XP GOOD
   .DB $04,$09,$0F,$23,$FF
P1_XP_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X31 P1 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP GOOD
   .DB $04,$09,$14,$23,$FF
P1_XP_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X32 P1 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP GOOD
   .DB $04,$09,$15,$23,$FF
P2_XP_MISS_BLOCK_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X33 P2 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP GOOD
   .DB $04,$09,$1C,$23,$FF
P2_XP_MISS_BLOCK_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X34 P2 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP GOOD
   .DB $04,$09,$1D,$23,$FF
P1_XP_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X35 P1 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP MISS
   .DB $04,$09,$16,$FF
P1_XP_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X36 P1 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP MISS
   .DB $04,$09,$17,$FF
P1_XP_MISS_BLOCK_WIDE_LEFT_CUT_SEQ:                                     ; 0X37 P1 XP ATT, MISSED BLOCK, WIDE LEFT XP MISS
   .DB $04,$09,$18,$FF
P1_XP_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ:                                     ; 0X38 P1 XP ATT, MISSED BLOCK, WIDE RIGHT XP MISS
   .DB $04,$09,$19,$FF
P1_XP_MISS_BLOCK_SHORT_CUT_SEQ:                                     ; 0X39 P1 XP ATT, MISSED BLOCK, XP SHORT (IMPOSSIBLE)
   .DB $04,$09,$1A,$FF
P2_XP_MISS_BLOCK_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X3A P2 XP ATT, MISSED BLOCK, HIT RIGHT UPRIGHT, XP MISS
   .DB $04,$09,$1E,$FF
P2_XP_MISS_BLOCK_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X3B P2 XP ATT, MISSED BLOCK, HIT LEFT UPRIGHT, XP MISS
   .DB $04,$09,$1F,$FF
P2_XP_MISS_BLOCK_WIDE_RIGHT_CUT_SEQ:                                     ; 0X3C P2 XP ATT, MISSED BLOCK, WIDE RIGHT XP MISS
   .DB $04,$09,$20,$FF
P2_XP_MISS_BLOCK_WIDE_LEFT_CUT_SEQ:                                     ; 0X3D P2 XP ATT, MISSED BLOCK, WIDE LEFT XP MISS
   .DB $04,$09,$21,$FF
P2_XP_MISS_BLOCK_SHORT_CUT_SEQ:                                     ; 0X3E P2 XP ATT, MISSED BLOCK, XP SHORT (IMPOSSIBLE)
   .DB $04,$09,$22,$FF


P1_XP_GOOD_CUT_SEQ:                                     ; 0X3F P1 XP ATT, THRU UPRIGHTS, XP GOOD
   .DB $04,$0F,$23,$FF
P1_XP_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X40 P1 XP ATT, HIT LEFT UPRIGHT, XP GOOD
   .DB $04,$14,$23,$FF
P1_XP_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X41 P1 XP ATT, HIT RIGHT UPRIGHT, XP GOOD 
   .DB $04,$15,$23,$FF
P2_XP_RIGHT_UR_GOOD_CUT_SEQ:                                     ; 0X42 P2 XP ATT, HIT RIGHT UPRIGHT, XP GOOD
   .DB $04,$1C,$23,$FF
P2_XP_LEFT_UR_GOOD_CUT_SEQ:                                     ; 0X43 P2 XP ATT, HIT LEFT UPRIGHT, XP GOOD
   .DB $04,$1D,$23,$FF
P1_XP_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X44 P1 XP ATT, HIT LEFT UPRIGHT, XP MISS
   .DB $04,$16,$FF
P1_XP_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X45 P1 XP ATT, HIT RIGHT UPRIGHT, XP MISS
   .DB $04,$17,$FF
P1_XP_WIDE_LEFT_CUT_SEQ:                                     ; 0X46 P1 XP ATT, WIDE LEFT,  XP MISS
   .DB $04,$18,$FF
P1_XP_WIDE_RIGHT_CUT_SEQ:                                     ; 0X47 P1 XP ATT, WIDE RIGHT, XP MISS
   .DB $04,$19,$FF
P1_XP_SHORT_CUT_SEQ:                                     ; 0X48 P1 XP ATT, XP SHORT(IMPOSSIBLE)
   .DB $04,$1A,$FF
P2_XP_RIGHT_UR_MISS_CUT_SEQ:                                     ; 0X49 P2 XP ATT, HIT RIGHT UPRIGHT, XP MISS
   .DB $04,$1E,$FF
P2_XP_LEFT_UR_MISS_CUT_SEQ:                                     ; 0X4A P2 XP ATT, HIT LEFT UPRIGHT, XP MISS
   .DB $04,$1F,$FF
P2_XP_WIDE_RIGHT_CUT_SEQ:                                     ; 0X4B P2 XP ATT, WIDE RIGHT, XP MISS
   .DB $04,$20,$FF
P2_XP_WIDE_LEFT_CUT_SEQ:                                     ; 0X4C P2 XP ATT, WIDE LEFT, XP MISS
   .DB $04,$21,$FF
P2_XP_SHORT_CUT_SEQ:                                     ; 0X4D P2 XP ATT, MISSED BLOCK, XP SHORT (IMPOSSIBLE)
   .DB $04,$22,$FF
   
OFF_JUMP_CATCH_CUT_SEQ:                                     ; 0X4E,0X4F OFF JUMP CATCH
   .DB $2B,$FE
OFF_JUMP_MISS_CUT_SEQ:                                     ; 0X50 OFF JUMP MISS
   .DB $2C,$FE
OFF_JUMP_BOUNCE_HANDS_CUT_SEQ:                                     ; 0X51 OFF JUMP BOUNCE OFF HANDS 
   .DB $2D,$FE
DEF_JUMP_MIS_CUT_SEQ:                                     ; 0X52 DEF JUMP MISS
   .DB $05,$FE
DEF_JUMP_BOUNCE_HANDS_CUT_SEQ:                                     ; 0X53 DEF JUMP BOUNCE OFF HANDS
   .DB $06,$FE
DEF_JUMP_INT_CUT_SEQ:                                     ; 0X54 DEF JUMP INT
   .DB $43,$FE
DOUBLE_JUMP_OFF_CATCH_CUT_SEQ:                                     ; 0X55 DOUBLE JUMP + OFF CATCH
   .DB $44,$FE
DOUBLE_JUMP_OFF_WHIFF_CUT_SEQ:                                     ; 0X56 DOUBLE JUMP + OFF WHIFF
   .DB $45,$FE 
DOUBLE_JUMP_BOUNCE_DEF_HANDS_CUT_SEQ:                                     ; 0X57 DOUBLE JUMP + DEF BOUNCE OFF HANDS
   .DB $46,$FE
DOUBLE_JUMP_DEF_INT_CUT_SEQ:                                     ; 0X58 DOUBLE JUMP + DEF INT 
   .DB $47,$FE
OFF_REG_CATCH_CUT_SEQ:                                     ; 0X59 OFF REG SIDEWAYS CATCH
   .DB $2F,$FE
OFF_REG_BOUNCE_HANDS_CUT_SEQ:                                     ; 0X5A OFF REG SIDEWAYS BOUNCE OFF HANDS
   .DB $30,$FE
DEF_REG_BOUNCE_HANDS_CUT_SEQ:                                     ; 0X5B DEF REG SIDEWAYS BOUNCE OFF HANDS
   .DB $07,$FE
DEF_REG_INT_CUT_SEQ:                                     ; 0X5C DEF REG SIDEWAYS INT
   .DB $48,$FE
DEF_DIVE_MISS_OFF_REG_CATCH_CUT_SEQ:                                     ; 0X5D DEF DIVE MISS + OFF REG SIDEWAYS CATCH
   .DB $51,$FE
DEF_JUMP_MISS_OFF_REG_CATCH_CUT_SEQ:                                     ; 0X5E DEF JUMP MISS + OFF REG SIDEWAYS CATCH
   .DB $52,$FE
OFF_DIVE_BOUNCE_OFF_HANDS_CUT_SEQ:                                     ; 0X5F OFF DIVE + BOUNCE OFF HANDS
   .DB $34,$FE
DEF_DIVE_MISS_BOUNCE_OFF_WR_HANDS_CUT_SE:                                     ; 0X60 DEF DIVE MISS + BOUNCE OFF HANDS
   .DB $53,$FE
DEF_JUMP_MISS_BOUNCE_OFF_WR_HANDS_CUT_SE:                                     ; 0X61 DEF JUMP MISS + BOUNCE OFF HANDS
   .DB $54,$FE
DEF_JUMP_TIP_BALL_CUT_SEQ:                                     						; 0X62 DEF JUMP + BOUNCE OFF HANDS
   .DB $06,$FE
DOUBLE_JUMP_BOUNCE_DEF_HANDS_CUT_SEQ_DUPE:                                     ; 0X63 DOUBLE JUMP + DEF BOUNCE OFF HANDS (***DUPLICATE OF 0X57 ABOVE)
   .DB $46,$FE
DEF_REG_INT_CUT_SEQ_DUPE:                                     ; 0X64 DEF REG SIDEWAYS INT (***DUPLICATE OF 0X5C ABOVE)
   .DB $48,$FE   
BALL_HIT_GROUND_REF_INC_CUT_SEQ:               ; 0X65 BALL HITS GROUND + REF INCOMPLETE 
   .DB $31,$FE
OFF_DIVE_CATCH_CUT_SEQ:                                     ; 0X66 OFF DIVE CATCH
   .DB $32,$FE
OFF_DIVE_MISS_CUT_SEQ:                                     ; 0X67 OFF DIVE MISS
   .DB $33,$FE
DEF_DIVE_INT_CUT_SEQ:                                     ; 0X68 DEF DIVE INT
   .DB $55,$FE
DEF_DIVE_MISS_CUT_SEQ:                                     ; 0X69 DEF DIVE MISS
   .DB $08,$FE

DEFAULT_IRQ_SPLIT_TABLE:                                     ; 0X6A IRQ SCREEN SPLIT TABLE
                                               ; CURRENT SCROLL LOCATION , BG CHR BANK 0, BG CHR BANK 1, IRQ 
   .DB $00,$00,$18,$1A,$00
   .DB $00,$00,$18,$1A,$00
   .DB $00,$00,$18,$1A,$00
   .DB $00,$00,$18,$1A

_F}_CUTSCENE_SEQUENCE_PTRS_AND_SEQUENCES

.PAD $A800, $FF                                ; FILL TO $A7FF $FF
